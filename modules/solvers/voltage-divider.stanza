#use-added-syntax(jitx)
defpackage ocdb/modules/solvers/voltage-divider :
  import core
  import collections
  import math
  import jitx with :
    prefix(Resistor) => EModel-
  import jitx/commands
  import ocdb/utils/defaults
  import ocdb/utils/generic-components
  import ocdb/utils/generator-utils
  import ocdb/utils/checks
  import ocdb/utils/design-vars
  import ocdb/utils/db-parts


defstruct Ratio :
  high: Double
  low: Double
  loss: Double

public defstruct VoltageDividerSolution :
  r1: Resistor
  r2: Resistor
  vo: Toleranced ; valid when solving for output target
  vi: Toleranced ; valid when solving for input target


public defn voltage-divider-solver (v-in: Toleranced,
                                    v-out: Toleranced,
                                    current: Toleranced) -> VoltageDividerSolution|False:
  voltage-divider-solver(v-in, v-out, current, false)


public defn voltage-divider-solver (v-known: Toleranced,
                                    v-target: Toleranced,
                                    current: Toleranced,
                                    input-ref?: True|False) -> VoltageDividerSolution|False:
  fatal("Current specified to the voltage-divider-solver cannot be 0.0A") when typ(current) == 0.0
  val v-in = 
    if input-ref? : v-target
    else : v-known
  val v-out = 
    if input-ref? : v-known
    else : v-target
  val goal-r-hi = abs(typ-value(v-in) - typ-value(v-out)) / typ-value(current)
  val goal-r-lo = typ-value(v-out) /  typ-value(current)
  label<VoltageDividerSolution|False> return :
    ; Look for highest acceptable tolerance in EIA standard values: [20% 10% 5% 2% 1% 0.5% 0.25% 0.1%]
    for std-tol in std-tols do :
      val tolerance = std-tol / 100. ; std-tols are in % instead of being unitless
      val lo-tol = tol(1.0, tolerance) ; calc 1.0 - std-tol/100 -> 0.99 for std-tol of 1% but creates Toleranced(1.0, 0.99, 1.01)
      val hi-tol = tol(1.0, tolerance) ; calc 1.0 + std-tol/100 -> 1.01 for std-tol of 1% but creates Toleranced(1.0, 0.99, 1.01)
      if check-voltage-range(v-in, v-out, goal-r-lo, goal-r-hi, lo-tol, hi-tol, tol(1.0), tol(1.0), input-ref?) :
        val solution = voltage-divider-solver(v-in, v-out, current, tolerance, input-ref?)
        if solution is VoltageDividerSolution :
          return(solution)


public defn voltage-divider-solver (v-in: Toleranced,
                                    v-out: Toleranced,
                                    current: Toleranced,
                                    tolerance: Double) -> VoltageDividerSolution|False :
  voltage-divider-solver(v-in, v-out, current, tolerance, false)

public defn voltage-divider-solver (v-given: Toleranced,
                                    v-target: Toleranced,
                                    current: Toleranced,
                                    tolerance: Double,
                                    input-ref?: True|False) -> VoltageDividerSolution|False :
  fatal("Current specified to the voltage-divider-solver cannot be 0.0A") when typ(current) == 0.0
  val v-in = 
    if input-ref? : v-target
    else : v-given
  val v-out = 
    if input-ref? : v-given
    else : v-target
  val goal-r-hi = abs(typ-value(v-in) - typ-value(v-out)) /  typ-value(current)
  val goal-r-lo = typ-value(v-out) /  typ-value(current)
  ; Find best resistors matching output requirement
  val current-tol = (tol+%(current) + tol-%(current) / 2.0) / 100.0
  val hi-res = get-resistance-values(goal-r-hi, tolerance, current-tol)
  val lo-res = get-resistance-values(goal-r-lo, tolerance, current-tol)

  label<VoltageDividerSolution|False> return :
    for ratio in determine-and-sort-fitting-resistance-pairs(v-in, v-out, tolerance, hi-res, lo-res, input-ref?) do :
      println("    - Querying resistors for R1=%_Ω R2=%_Ω tol=%_" % [high(ratio), low(ratio), tolerance])
      val r-his = query-resistors(high(ratio), tolerance)
      val r-los = query-resistors( low(ratio), tolerance)

      if length(r-his) >= 3 and length(r-los) >= 3 :
        val r-hi-cmp = r-his[0]
        val r-lo-cmp = r-los[0]
        val vx = study-solution(v-in, r-hi-cmp, r-lo-cmp, input-ref?)

        if in-range?(v-target, vx) :
          println("      Solved: mpn1=%_, mpn2=%_, v-target=(%_V), current=%_A"
                  % [mpn(r-hi-cmp), mpn(r-lo-cmp), v-out, typ-value(v-out) / low(ratio)])
          if input-ref? :
            return(VoltageDividerSolution(r-hi-cmp, r-lo-cmp, v-given, vx))
          else :
            return(VoltageDividerSolution(r-hi-cmp, r-lo-cmp, vx, v-given))
        else :
          println("      Ignoring: not a solution when taking into account TCRs.")
      else :
        println("      Ignoring: there must at least 3 resistors of each type, only found %_ & %_" % [length(r-his) length(r-los)])

    println("   No solution.")




defn get-resistance-values (goal-r: Double, r-tol: Double, current-tol: Double) -> Tuple<Double> :
  query-available-resistance-values(["min-resistance" => (1. - current-tol) * goal-r,
                                     "max-resistance" => (1. + current-tol) * goal-r,
                                     "tolerance.min" => -1. * r-tol,
                                     "tolerance.max" => r-tol,
                                     "minimum_quantity" => 1,
                                     "mounting" => PREFERRED-MOUNTING,
                                     "case" => get-valid-pkg-list(),
                                     "min-stock" => 5 * DESIGN-QUANTITY], ["tcr"])

defn determine-and-sort-fitting-resistance-pairs (v-given: Toleranced,
                                                  v-target: Toleranced,
                                                  tolerance: Double,
                                                  hi-res: Tuple<Double>,
                                                  lo-res: Tuple<Double>,
                                                  input-ref?: True|False) :
  val lo-tol = tol(1.0, tolerance)
  val hi-tol = tol(1.0, tolerance)
  qsort{loss, _} $
    for rh in hi-res seq-cat :
      for rl in lo-res seq? :
        val vx = compute-voltage-range( v-given, rl, rh, lo-tol, hi-tol, tol(1.0), tol(1.0) input-ref?)
        if in-range?(v-target, vx) :
          val loss = abs(typ-value(v-target) - typ-value(vx))
          One(Ratio(rh, rl, loss))
        else :
          None()

;lo-tol, hi-tol, lo-dr, are Tolerances of positive percentages.
;A value of 1.0 is equivalent to 100%.
public defn compute-voltage-range ( v-given:  Toleranced,
                                    r-lo-val: Double,     r-hi-val: Double,
                                    lo-tol:   Toleranced, hi-tol:   Toleranced,
                                    lo-dr:    Toleranced, hi-dr:    Toleranced
                                    input-ref?: True|False) -> Toleranced :
  val r-lo = r-lo-val * lo-dr * lo-tol
  val r-hi = r-hi-val * hi-dr * hi-tol
  val r-hi-min = min-value(r-hi)
  val r-hi-typ = typ-value(r-hi)
  val r-hi-max = max-value(r-hi)
  val r-lo-max = max-value(r-lo)
  val r-lo-typ = typ-value(r-lo)
  val r-lo-min = min-value(r-lo)
  val v-given-min = min-value(v-given)
  val v-given-typ = typ-value(v-given)
  val v-given-max = max-value(v-given)

  if input-ref? :
    val vi-min = (v-given-min * (1.0 + r-hi-min / r-lo-max))
    val vi-nom = (v-given-typ * (1.0 + r-hi-typ / r-lo-typ))
    val vi-max = (v-given-max * ( 1.0 + r-hi-max / r-lo-min ))
    min-typ-max(vi-min, vi-nom, vi-max)
  else :
    val vo-min = (v-given-min * r-lo-min / (r-lo-min + r-hi-max))
    val vo-nom = (v-given-typ * r-lo-typ / (r-lo-typ + r-hi-typ))
    val vo-max = (v-given-max * r-lo-max / (r-lo-max + r-hi-min))
    min-typ-max(vo-min, vo-nom, vo-max)


public defn check-voltage-range ( v-in:     Toleranced, v-out:    Toleranced,
                                  r-lo-val: Double,     r-hi-val: Double,
                                  lo-tol:   Toleranced, hi-tol:   Toleranced,
                                  lo-dr:    Toleranced, hi-dr:    Toleranced
                                  input-ref?: True|False) -> True|False :
  val vx =
    if input-ref? : v-out
    else : v-in
  val vy = compute-voltage-range(  vx,
                                    r-lo-val, r-hi-val,
                                    lo-tol, hi-tol,
                                    lo-dr, hi-dr,
                                    input-ref?)
  if input-ref? : in-range?(v-in, vy)
  else : in-range?(v-out, vy)


defn query-resistors (resistance: Double, tol: Double) -> Tuple<Resistor> :
  Resistors(["resistance" => resistance,
             "tolerance.min" => -1. * tol,
             "tolerance.max" => tol,
             "minimum_quantity" => 1,
             "mounting" => PREFERRED-MOUNTING,
             "case" => get-valid-pkg-list(),
             "min-stock" => 5 * DESIGN-QUANTITY,
             "_exist" => ["tcr"]],
            25)

defn study-solution (v-given: Toleranced, r-hi: Resistor, r-lo: Resistor, input-ref?: True|False) -> Toleranced :
  val lo-drs = [delta-resistance(r-lo, min-value(OPERATING-TEMPERATURE)), 0., delta-resistance(r-lo, max-value(OPERATING-TEMPERATURE))]
  val hi-drs = [delta-resistance(r-hi, min-value(OPERATING-TEMPERATURE)), 0., delta-resistance(r-hi, max-value(OPERATING-TEMPERATURE))]

  ; Calculate worst case.
  ; TCR can be + or - here. It's a tolerance, not an absolute change in resistance
  val lo-tolerance = tolerance(r-lo) as MinMaxRange
  val hi-tolerance = tolerance(r-hi) as MinMaxRange
  println("      Checking solution for existing components with: tol1=%_, tol2=%_" % [hi-tolerance, lo-tolerance])

  compute-voltage-range(v-given,
                        resistance(r-lo),
                        resistance(r-hi),
                        min-max(1.0 + min(lo-tolerance), 1.0 + max(lo-tolerance)),
                        min-max(1.0 + min(hi-tolerance), 1.0 + max(hi-tolerance)),
                        min-max(1.0 + minimum(lo-drs), 1.0 + maximum(lo-drs)),
                        min-max(1.0 + minimum(hi-drs), 1.0 + maximum(hi-drs))
                        input-ref?)
