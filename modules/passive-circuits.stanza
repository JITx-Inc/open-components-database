#use-added-syntax(esir2)
defpackage ocdb/passive-circuits:
  import core
  import collections
  import math
  import esir2 with :
    prefix(Resistor) => EModel-
  import esir2/repl-lib
  import ocdb/tests/default-harness
  import ocdb/generic-components
  import ocdb/generator-utils
  import ocdb/checks
  import ocdb/design-vars
  import ocdb/db-parts


defstruct Ratio :
  high: Double
  low: Double
  loss: Double

public defstruct VoltageDividerSolution :
  r1: Resistor
  r2: Resistor
  vo: [Double, Double, Double]

;======================================================
;==================== Esir API ========================
;======================================================

public defn make-voltage-divider (in:JITObject, out:JITObject, lo:JITObject, v-out:[Double,Double,Double]) :
  ; Target current through divider is 10x input current (if known)
  if has-property?(out.i-input) :
    make-voltage-divider(in, out, lo, v-out, property(out.i-input) * 10.0)
  else :
  ; Otherwise, set current to be 1ma
    make-voltage-divider(in, out, lo, v-out, 1.0e-3)

public defn make-voltage-divider (in:JITObject, out:JITObject, lo:JITObject,
                                  v-out:[Double,Double,Double],
                                  current:Double) -> VoltageDividerSolution:
  println("Solving voltage divider [in=%_, out=%_, low=%_, v-out=(min=%_V, nom=%_V, max=%_V), current=%_A]"
          % [in, out, lo, v-out[0], v-out[1], v-out[2], current])
  ;Extract voltages from input pin
  val v-in = property(in.voltage)

  val solution = make-voltage-divider(v-in, v-out, current)
  match(solution: VoltageDividerSolution) :
    make-voltage-divider-module(in, out, lo, v-out, to-esir(r1(solution)), to-esir(r2(solution)), vo(solution))
    solution
  else :
    fatal("No solution for voltage divider.")

; Generator function for voltage-divider module. This gets called at the top level
; Parameters
; ------------
; in : input pin, with annotated voltage [min nom max] (V)
; out : output pin. Gets annotated with [min nom max] (V)
; lo : low side pin (usually gnd)
; v-out : Allowable output voltage range [min nom max] (V)
; current : desired dc current of voltage divider (A) assumes output is connected to high-impedance.
; tol : desired tolerance of resistor (in %)
; returns true if fitting resistors were found else false
public defn make-voltage-divider (in:JITObject, out:JITObject, lo:JITObject,
                                  v-out:[Double,Double,Double],
                                  current:Double,
                                  tol:Double) -> VoltageDividerSolution|False :
  val current-tol = 10.
  make-voltage-divider(in, out, lo, v-out, current, tol, current-tol)

; current-tol : Tolerance on the output current (in %)
public defn make-voltage-divider (in:JITObject, out:JITObject, lo:JITObject,
                                  v-out:[Double,Double,Double],
                                  current:Double,
                                  tol:Double,
                                  current-tol: Double) -> VoltageDividerSolution|False :
  println("Solving voltage divider [in=%_, out=%_, low=%_, v-out=(min=%_V, nom=%_V, max=%_V), current=%_A, resistor-tol=%_%%]"
          % [in, out, lo, v-out[0], v-out[1], v-out[2], current, tol])
  val tolerance = tol / 100.
  val current-tolerance = current-tol / 100.
  val v-in = property(in.voltage)

  val solution = make-voltage-divider(v-in, v-out, current, tolerance, current-tolerance)
  match(solution: VoltageDividerSolution) :
    make-voltage-divider-module(in, out, lo, v-out, to-esir(r1(solution)), to-esir(r2(solution)), vo(solution))
    solution
  else :
    fatal("No solution for voltage divider.")

;============================================================
;=============== Voltage Divider Solver =====================
;============================================================

public defn make-voltage-divider (v-in: [Double, Double, Double],
                                  v-out: [Double,Double,Double],
                                  current: Double) -> VoltageDividerSolution|False:
  ; Look for highest acceptable tolerance in EIA standard values: [20% 10% 5% 2% 1% 0.5% 0.25% 0.1%]
  val current-tolerance = 0.1
  val goal-r-hi = (v-in[1] - v-out[1]) / current
  val goal-r-lo = v-out[1] / current
  label<VoltageDividerSolution|False> return :
    for tol in std-tols do :
      ; std-tols are in % instead of being unitless
      val tolerance = tol / 100.
      if check-output-voltage-range(v-in,
                                    v-out,
                                    goal-r-lo,
                                    goal-r-hi,
                                    (- tolerance), tolerance, (- tolerance), tolerance,
                                    0., 0., 0., 0.) :
        println("-> Tolerance %_%%" % [tol])
        val solution = make-voltage-divider(v-in, v-out, current, tolerance, current-tolerance)
        if solution is VoltageDividerSolution :
          return(solution)

public defn make-voltage-divider (v-in: [Double, Double, Double],
                                  v-out: [Double,Double,Double],
                                  current: Double,
                                  tolerance: Double,
                                  current-tolerance: Double) -> VoltageDividerSolution|False :
  val goal-r-hi = (v-in[1] - v-out[1]) / current
  val goal-r-lo = v-out[1] / current
  ; Find best resistors matching output requirement
  val hi-res = get-resistance-values(goal-r-hi, tolerance, current-tolerance)
  val lo-res = get-resistance-values(goal-r-lo, tolerance, current-tolerance)

  label<VoltageDividerSolution|False> return :
    for ratio in determine-and-sort-fitting-resistance-pairs(v-in, v-out, tolerance, hi-res, lo-res) do :
      println("    - Querying resistors for R1=%_Ω R2=%_Ω" % [high(ratio), low(ratio)])
      val r-his = query-resistors(high(ratio), tolerance)
      val r-los = query-resistors(low(ratio), tolerance)

      if length(r-his) >= 3 and length(r-los) >= 3 :
        val r-hi-cmp = r-his[0]
        val r-lo-cmp = r-los[0]
        val vo = study-solution(v-in, r-hi-cmp, r-lo-cmp)
        val [vo-min, vo-nom, vo-max] = vo

        if (vo-min >= v-out[0]) and (vo-max <= v-out[2]) :
          println("      Solved: mpn1=%_, mpn2=%_, v-out=(min=%_V, nom=%_V, max=%_V), current=%_A"
                  % [mpn(r-hi-cmp), mpn(r-lo-cmp), vo-min, vo-nom, vo-max, v-out[1] / low(ratio)])
          return(VoltageDividerSolution(r-hi-cmp, r-lo-cmp, vo))
        else :
          println("      Ignoring: not a solution when taking into account TCRs.")
      else :
        println("      Ignoring: there must at least 3 resistors of each type")

    println("   No solution.")

defn get-resistance-values (goal-r: Double, r-tol: Double, current-tol: Double) -> Tuple<Double> :
  query-available-resistance-values(["min-resistance" => (1. - current-tol) * goal-r,
                                     "max-resistance" => (1. + current-tol) * goal-r,
                                     "tolerance" => r-tol,
                                     "minimum_quantity" => 1,
                                     "mounting" => PREFERRED-MOUNTING,
                                     "case" => get-valid-pkg-list(),
                                     "min-stock" => 5 * DESIGN-QUANTITY], ["tcr"])

defn determine-and-sort-fitting-resistance-pairs (v-in: [Double, Double, Double],
                                                  v-out: [Double, Double, Double],
                                                  tol: Double,
                                                  hi-res: Tuple<Double>,
                                                  lo-res: Tuple<Double>) :
  qsort{loss, _} $
    for rh in hi-res seq-cat :
      for rl in lo-res seq? :
        val [vo-min, vo-nom, vo-max] = compute-output-voltage-range(v-in,
                                                                    rl,
                                                                    rh,
                                                                    (- tol), tol, (- tol), tol,
                                                                    0., 0., 0., 0.)
        if v-out[0] <= vo-min and vo-max <= v-out[2] :
          val loss = abs(v-out[1] - vo-nom)
          One(Ratio(rh, rl, loss))
        else :
          None()

public defn compute-output-voltage-range (v-in: [Double, Double, Double],
                                          r-lo-val, r-hi-val,
                                          min-lo-tol, max-lo-tol, min-hi-tol, max-hi-tol,
                                          min-lo-dr, max-lo-dr, min-hi-dr, max-hi-dr) -> [Double, Double, Double] :
  val [v-min v v-max] = v-in
  val vo-min = v-min * (r-lo-val * (1.0 + min-lo-dr)) * (1.0 + min-lo-tol)
                     / ( (r-lo-val * (1.0 + min-lo-dr)) * (1.0 + min-lo-tol)
                      + (r-hi-val  * (1.0 + max-hi-dr)) * (1.0 + max-hi-tol))
  val vo-max = v-max * (r-lo-val * (1.0 + max-lo-dr)) * (1.0 + max-lo-tol)
                     / ( (r-lo-val * (1.0 + max-lo-dr)) * (1.0 + max-lo-tol)
                     + (r-hi-val  * (1.0 + min-hi-dr)) * (1.0 + min-hi-tol))
  val vo-nom = v * (r-lo-val) / ( (r-lo-val) + (r-hi-val) )

  [vo-min, vo-nom, vo-max]

public defn check-output-voltage-range (v-in: [Double, Double, Double],
                                        v-out: [Double, Double, Double],
                                        r-lo-val, r-hi-val,
                                        min-lo-tol, max-lo-tol, min-hi-tol, max-hi-tol,
                                        min-lo-dr, max-lo-dr, min-hi-dr, max-hi-dr) -> True|False :
  val [vo-min, vo-nom, vo-max] = compute-output-voltage-range(v-in,
                                                              r-lo-val, r-hi-val,
                                                              min-lo-tol, max-lo-tol, min-hi-tol, max-hi-tol,
                                                              min-lo-dr, max-lo-dr, min-hi-dr, max-hi-dr)
  v-out[0] <= vo-min and vo-max <= v-out[2]

defn query-resistors (resistance: Double, tol: Double) -> Tuple<Resistor> :
  Resistors(["resistance" => resistance,
             "tolerance" => tol,
             "minimum_quantity" => 1,
             "mounting" => PREFERRED-MOUNTING,
             "case" => get-valid-pkg-list(),
             "min-stock" => 5 * DESIGN-QUANTITY], ["tcr"])

defn study-solution (v-in: [Double, Double, Double], r-hi: Resistor, r-lo: Resistor) -> [Double, Double, Double] :
  val lo-drs = [delta-resistance(r-lo, OPERATING-TEMPERATURE[0]), 0., delta-resistance(r-lo, OPERATING-TEMPERATURE[1])]
  val hi-drs = [delta-resistance(r-hi, OPERATING-TEMPERATURE[0]), 0., delta-resistance(r-hi, OPERATING-TEMPERATURE[1])]

  ; Calculate worst case.
  ; TCR can be + or - here. It's a tolerance, not an absolute change in resistance
  val lo-tolerance = tolerance(r-lo) as MinMaxRange
  val hi-tolerance = tolerance(r-hi) as MinMaxRange
  println("      Checking solution for existing components with: tol1=%_, tol2=%_" % [hi-tolerance, lo-tolerance])

  compute-output-voltage-range(v-in,
                               resistance(r-lo),
                               resistance(r-hi),
                               min(lo-tolerance),
                               max(lo-tolerance),
                               min(hi-tolerance),
                               max(hi-tolerance),
                               minimum(lo-drs),
                               maximum(lo-drs),
                               minimum(hi-drs),
                               maximum(hi-drs))

defn make-voltage-divider-module (in: JITObject, out: JITObject, lo: JITObject,
                                  v-out: [Double, Double, Double],
                                  r-hi-cmp: JITComponentType, r-lo-cmp: JITComponentType,
                                  vo: [Double, Double, Double]) :

  inside pcb-module :
    property(out.voltage) = vo

    ; Instantiate resistors, create netlist
    inst r-hi : r-hi-cmp
    inst r-lo : r-lo-cmp
    net (r-hi.p[1] in)
    net (r-lo.p[1] r-hi.p[2] out)
    net (r-lo.p[2] lo)

    ; [TODO] check. voltage-levels and other check.stanza functions already adapted for JITObjects.
    ; Check true output against requirement
    check voltage-levels(out, v-out)
