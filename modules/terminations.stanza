#use-added-syntax(jitx)
defpackage ocdb/terminations:
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults
  import ocdb/generic-components
  import ocdb/generator-utils
  import ocdb/checks
  import ocdb/design-vars
  import ocdb/bundles

; Adds termination circuitry for a can bus based on method
public defn terminate-can (bus:JITXObject, gnd:JITXObject, method:String, ctrl:JITXObject|False) :
  inside pcb-module:
    switch(method) :
      "parallel":
        res-strap(bus.canh, bus.canl, ["resistance" => 120.0 "min-rated-power" => 1.0])
      "parallel+common-mode-filter":
        inst c : ceramic-cap(4.7e-9)
        res-strap(bus.canh, c.p[1], 60.0)
        res-strap(bus.canl, c.p[1], 60.0)
        net (c.p[2], gnd)
      "switchable":
        ; Controllable termination using two p-channel mosfets. Termination disabled by pulling ctrl pin high.
        val ctrl-pin = ctrl as JITXObject
        inst q : ocdb/on-semiconductor/FDN352AP/component[2]
        res-strap(bus.canh, q[0].s, 56.0)
        res-strap(bus.canl, q[1].s, 56.0)
        ; Add EMI filtering caps
        cap-strap(bus.canl, gnd, 4.7e-12)
        cap-strap(bus.canh, gnd, 4.7e-12)
        net (q[0].g q[1].g)
        net (q[0].d q[1].d)
        res-strap(ctrl-pin, q[0].g, 1.0e3)
        res-strap(q[0].g, gnd,  33.0e3)
        cap-strap(q[0].d, gnd, 4.7e-9)
        res-strap(q[0].d, gnd, 100.0e3)
      "isolated-switchable":
        ; Controllable termination using an opto-isolated relay. Termination enabled by pulling ctrl pin high
        val ctrl-pin = ctrl as JITXObject
        inst ssr : ocdb/toshiba/TLP175A/component
        ; Find voltage on control pin if it's a digital io, else assume 5v
        val v-ctrl = if has-property?(ctrl-pin.digital-io): property(ctrl-pin.voh) else: 5.0
        ; Find a 5% resistor for 5ma current into the led
        val res = closest-std-val((v-ctrl - property(ssr.forward-voltage)) / 5.0e-3, 5.0)
        res-strap(ctrl-pin, ssr.a, res)
        net (ssr.c gnd)
        ; Shoot for 120 ohms total, including rds-on of ssr
        res-strap(bus.canh, ssr.drain[2], 120.0 - property(ssr.rds-on))
        net (bus.canl ssr.drain[1])

public defn terminate-lvds (bus:JITXObject):
  inside pcb-module:
    res-strap(bus.N, bus.P, 100.0)

public defn terminate-can (bus:JITXObject, gnd:JITXObject, method:String) :
  terminate-can(bus, gnd, method, false)

; Adds 22ohm termination resistors, returns terminated bus
public defn terminate-usb-2 (bus:JITXObject) :
  inside pcb-module:
    port terminated-usb-2 : usb-2
    net (terminated-usb-2.vbus, bus.vbus)
    net (terminated-usb-2.id, bus.id)
    res-strap(terminated-usb-2.data.N, bus.data.N, 22.0)
    res-strap(terminated-usb-2.data.P, bus.data.P, 22.0)
    ; Return the terminated bus
    terminated-usb-2