#use-added-syntax(jitx, tests)
defpackage ocdb/tests/connects: 
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/connects
  import ocdb/bundles

defn expect-fatal (body:() -> ?):
  var caught? = false
  execute-with-error-handler(
    body,
    fn (): caught? = true
  )
  #EXPECT(caught?)

deftest(ocdb, connects) test-connect-i2c-isolation:
  pcb-module M1:
    port p: i2c[[1, 2]]
    #EXPECT(connect-i2c-isolation(p[1], p[2]) is-not False)
  
  pcb-module M2:
    port p: i2c[[1, 2]]
    #EXPECT(connect-i2c-isolation(p[1], p[2], M1) is-not False)

  pcb-module M3:
    port x: i2c
    port y: gpio
    var caught? = false
    within expect-fatal():
      connect-i2c-isolation(x, y)
     
  for mod in [M1, M2, M3] do:
    print-def(mod)

deftest(ocdb, connects) test-connect-phy:
  pcb-module M1:
    port x: rgmii
    port y: ethernet-1000
    #EXPECT(connect-phy(x, y) is-not False)
  pcb-module M2:
    port x: rgmii
    port y: gpio
    within expect-fatal():
      connect-phy(x, y)
  pcb-module M3:
    port x: gpio
    port y: ethernet-1000
    within expect-fatal():
      connect-phy(x, y)
  for mod in [M1, M2, M3] do:
    print-def(mod)

deftest(ocdb, connects) test-connect-ft2332:
  pcb-module M1:
    port x: usb-2
    port y: uart([UART-DTR UART-RTS])
    #EXPECT(connect-ft232(x, y) is-not False)

  pcb-module M2:
    port x: usb-2
    port y: gpio
    within expect-fatal():
      connect-ft232(x, y)

  pcb-module M3:
    port x: gpio
    port y: uart([UART-DTR UART-RTS])
    within expect-fatal():
      connect-ft232(x, y)
    
  for mod in [M1, M2, M3] do:
    print-def(mod)
    