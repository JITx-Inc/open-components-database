#use-added-syntax(esir)
defpackage ocdb/class-a :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness
  import esir/repl-lib
  import ocdb/checks

  import ocdb/bundles
  import ocdb/generic-components

pcb-module class-a-amplifier (r-set:Double) :
  port vin:power
  pin in
  pin out
  inst r1 : {chip-resistor(110.0e3)}
  inst r2 : {chip-resistor(10.0e3)}
  inst r3 : {chip-resistor(10.0e3)}
  inst r4 : {chip-resistor(r-set)}

  inst c : {ceramic-cap(0.1e-6)}

  inst bjt : {ocdb/nexperia/PMBT3904/component}

  net (vin.vdd r1.p[1] r3.p[1])
  net (bjt.c r3.p[2])
  net (bjt.b r1.p[2] c.p[2] r2.p[1])
  net (bjt.e r4.p[1])
  net (in c.p[1])
  net (vin.gnd r2.p[2] r4.p[2])

pcb-component power-connector (v-out:Double) :
  port src:power
  spice: 
    "[V] {src.vdd} {src.gnd} {v-out}"

pcb-module demo :

  inst power : {power-connector(20.0)}
  inst amp : {class-a-amplifier(2.0e3)}
  inst j : {pin-header(5)}
  net gnd (power.src.gnd, j.p[1])
  net (power.src, amp.vin)
  net vdd (power.src.vdd, j.p[2])

  spice:
    "[Vi] {amp.in} {power.src.gnd} SIN(0 1 1k 0 0)"

  spice:
    ".temp 80.0"
    ".tran 0.01ms 5m"

pcb-design main-design :
  module = demo

export-netlist(demo.gnd, "ClassA.cir")
