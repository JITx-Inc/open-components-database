; Generated by JITX {JITX-VERSION}
#use-added-syntax(jitx)
defpackage ocdb/designs/tutorial :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/checks
  import ocdb/utils/generic-components

; ==========================================
; Implement other modules used by our design 
; ==========================================
pcb-module lm317a-regulator (output-voltage:Toleranced) :
  pin vin 
  pin vout 
  pin adj 
  pin gnd 

  inst lm317a : ocdb/components/unisonic/LM317A/component
  net (lm317a.input, vin)
  net (lm317a.output, vout)

  cap-strap(vin, gnd, ["capacitance" => 0.1e-6, "min-rated-voltage" => 25.0])
  cap-strap(vout, gnd, ["capacitance" => 1.0e-6, "min-rated-voltage" => 25.0])

  ; How much variability can we expect from the regulator alone?
  val ref-voltage = property(lm317a.reference-voltage)
  val inherent-variance = tol+%(ref-voltage)
  if inherent-variance > tol+%(output-voltage):
    fatal("Target design accuracy infeasible with LM317A %_ > %_" % [inherent-variance tol+%(output-voltage)])

  ; Budget variability for the voltage divider
  val target-variance = tol+%(output-voltage) - inherent-variance
  ; 5mA of voltage-divider current to compensate for regulator adj current
  val divider-current = 5.0e-3
  val adj-current = property(lm317a.adj-current) as Double ; retrieve the property from the component definition
  ; Estimate offset voltage caused by current sourced from adj pin
  val est-v-adj-offset = (typ-value(output-voltage) - typ-value(ref-voltage)) / divider-current * adj-current

  inst output-voltage-divider : ocdb/modules/passive-circuits/voltage-divider(high-voltage, adj-voltage, current) where :
    val high-voltage = tol%(center-value(output-voltage) - est-v-adj-offset, 0.0)
    val adj-voltage = tol%(typ-value(ref-voltage), target-variance)
    val current = divider-current                                                

  net (output-voltage-divider.in, lm317a.output)
  net (output-voltage-divider.out, lm317a.adj adj)
  net (output-voltage-divider.lo, gnd)
  ; inside pcb-module lm317a-regulator 
  schematic-group(self) = lm317a

  ; Calculate the actual offset now that we know the optimized value of the resistors
  val r-lo = property(output-voltage-divider.r-lo.resistance) as Double
  val true-adj-offset = adj-current * r-lo
  val vref = property(output-voltage-divider.output-voltage)

  ; Calculate the possible min and max values for the output voltage
  val out = min-typ-max(min-value(ref-voltage) * typ-value(output-voltage) / max-value(vref) + true-adj-offset,
                        typ-value(ref-voltage) * typ-value(output-voltage) / typ-value(vref) + true-adj-offset,
                        max-value(ref-voltage) * typ-value(output-voltage) / min-value(vref) + true-adj-offset)
  ; Make sure that the voltage specification is met
  check ocdb/utils/checks/within?(output-voltage, out)

; =========================
; Module to run as a design
; ==========================`
val board-shape = RoundedRectangle(35.0, 30.0, 0.5)
pcb-module regulator-fixture :
  val target-voltage = tol%(3.3, 10.0)
  
  inst reg : lm317a-regulator(target-voltage)

  inst source : banana-plug-module() ; shortcut to instantiate and place two `banana-plug`s plus the electrical support
  net gnd (reg.gnd, source.power.gnd)
  net vin (reg.vin, source.power.vdd)
  symbol(gnd) = ocdb/utils/symbols/ground-sym ; has to be placed after the definition of the net itself

  inst measure : sma-connector() ; alias for the instantiation of ocdb/components/johnson/142-0761-881/component
  net vout (reg.vout, measure.sig)
  net (gnd, measure.gnd)

  val test-points = add-testpoint([reg.adj gnd])

  val target-current = 1000.0e-3
  val target-load = closest-std-val(typ-value(target-voltage) / target-current, 5.0)
  inst load : chip-resistor(["resistance" => target-load "min-rated-power" => typ-value(target-voltage) * target-current * 2.0 ])
  
  net (load.p[1], reg.vout)
  net (load.p[2], gnd)

  schematic-group([test-points measure]) = test
  schematic-group(load) = schematic-group(reg)

  property(gnd.net-voltage) = typ(0.0)
  property(vout.net-voltage) = target-voltage
  property(vin.net-voltage) = typ(10.0)

  ocdb/utils/checks/check-design(self)

; ==========================================
; Configure the design, then run or check it
; ==========================================
defn run-design (circuit:Instantiable, run-checks?:True|False) :
  set-design-directory("CAD")
  set-board(ocdb/utils/defaults/default-board(ocdb/manufacturers/stackups/jlcpcb-jlc2313, board-shape))
  set-rules(ocdb/manufacturers/rules/jlcpcb-rules)

  var main-module = circuit
  if run-checks? :
    main-module = ocdb/utils/generator-utils/run-final-passes(circuit) ; Analyze design with a pass
  
  set-main-module(main-module) ; Treat the provided module as a design, and compile it.
  
  if run-checks? :
    run-checks("checks.txt")
  else:  
    view-board()
    view-schematic()

; ====================================
; Compile the design, view the results
; ====================================
defn compile-design (circuit:Instantiable) :
  run-design(circuit, false)

; =====================
; Run the design checks
; =====================
defn check-design (circuit:Instantiable) :
  run-design(circuit, true)

; ====================
; Export design to CAD
; ====================
defn export-design () :
  set-paper(ANSI-A4)
  set-export-backend(`altium)
  export-cad()

; ===================================
; Update CAD, keeping layout progress
; ===================================
defn update-design () :
  set-export-board?(false)
  export-design()

; =================
; Export BOM to csv
; =================
defn export-bill-of-materials () :
  set-bom-vendors(ocdb/utils/design-vars/APPROVED-DISTRIBUTOR-LIST)
  set-bom-design-quantity(ocdb/utils/design-vars/DESIGN-QUANTITY)
  export-bom()

; ============
; Run the code
; ============
check-design(regulator-fixture)