#use-added-syntax(jitx)
defpackage ocdb/designs/can-stm32 :
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/defaults
  import ocdb/utils/generic-components
  import ocdb/utils/bundles
  import ocdb/utils/design-vars
  import ocdb/utils/micro-controllers
  import ocdb/utils/generator-utils
  import ocdb/utils/checks
  import ocdb/modules/terminations

val board-shape = RoundedRectangle(30.0, 25.0, 3.0)

DESIGN-QUANTITY = 0

; Our off-board connector with 5V power and CAN
pcb-module can-connector :
  port power:power
  port can:can
  public inst connector : ocdb/components/phoenix/combicon-mc/component(4)
  net (connector.p[1], can.canl)
  net (connector.p[2], can.canh)
  net (connector.p[3], power.gnd)
  net VIN (connector.p[4], power.vdd)

  ocdb/modules/terminations/terminate-can(can, power.gnd, CANTerminationParallel)
  set-power-source(connector.p[4], connector.p[3], 5.0)

pcb-module first-design :
  inst connector : can-connector

  ; Choose the smallest 3.3V F0 mcu that will work for this design. Spec 30ppm for the main clock.
  inst stm : micro-controller(["mpn" => "STM32F103RBH6"])([`HSE-ppm => 30.0e-6 `boot-from => "flash"])

  ; Add a CAN transceiver
  inst xr : ocdb/components/texas-instruments/TCAN1051/module
  require can-out:can-interface from stm.mcu
  net (can-out, xr.can-interface)
  net CAN (xr.can, connector.can)

  ; Add an accelerometer
  inst xl : ocdb/components/st-microelectronics/LIS3DH/module
  require acc-i2c:i2c from stm.mcu
  net (acc-i2c, xl.i2c)
  add-open-drain-pullups(acc-i2c, stm.power.vdd)
  require tap-detect:gpio from stm.mcu
  net WAKE (tap-detect.gpio, xl.int[1])
  
  ; Connect power, apply symbols
  net (xr.power, connector.power)
  net (xl.power, stm.power)
  net P3V3 (xl.vio, xr.vio, stm.power.vdd)
  net GND (stm.power.gnd)
  symbol(P3V3) = ocdb/utils/symbols/supply-sym
  symbol(GND) = ocdb/utils/symbols/ground-sym

  property(GND.voltage) = typ(0.0)

  ; Mechanicals
  add-mounting-holes(board-shape, "M2", [2 3])
  place(connector.connector) at loc(-4.0, -12.5) on Top

  geom(GND) :
    copper-pour(LayerIndex(1), isolate = 0.1, rank = 1) = board-shape
  geom(P3V3) :
    copper-pour(LayerIndex(2), isolate = 0.1, rank = 1) = board-shape

  ; Run the schematic review
  check-design(self)

set-current-design("output-can-stm32")

val powered-design = run-final-passes(transform-module(generate-power, first-design))
make-default-board(powered-design, 4, board-shape)

view-board()
view-schematic()

; export-cad()
