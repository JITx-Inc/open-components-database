#use-added-syntax(jitx)
defpackage ocdb/diodes-incorporated/AP2112:
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults
  import ocdb/land-patterns
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/property-structs

public pcb-component component :
  pin VEN
  pin GND
  pin VIN
  pin VOUT
  pin nc
  name = "AP2112"
  manufacturer = "Diodes Incorporated"
  description = "600-mA, Low-Dropout Regulator"
  mpn = "AP2112K-3.3TRG1"
  pin-properties :
    [pin:Ref | pads:Int ... | side:Dir | generic-pin:GenericPin]
    [GND     | 2            | Down     | -]
    [VOUT    | 5            | Right    | GenericPin(Interval(0.0, 6.0, false), 4.0e3)]
    [VIN     | 1            | Left     | GenericPin(Interval(0.0, 6.0, false), 4.0e3)]
    [VEN     | 3            | Left     | GenericPin(Interval(0.0, 6.0, false), 4.0e3)]
    [nc      | 4            | Down     | -]
  make-box-symbol()
  assign-landpattern(SOT95P280X145-5N)

  property(VOUT.power-supply-pin) = PowerSupplyPin(3.3, 0.6)
  property(VIN.power-pin) = PowerPin(Interval(3.7, 6.5, false))
  property(self.rated-temperature) = RatedTemperature(Interval(-40.0, 85.0, false))

public pcb-module module : 
  port vin : power
  port vout : power
  pin gnd
  pin en
  public inst ps : ocdb/diodes-incorporated/AP2112/component
  bypass-cap-strap(ps.VIN, gnd, 1.0e-6)
  bypass-cap-strap(ps.VOUT, gnd, 1.0e-6)
  net (vin.vdd, ps.VIN)
  net (ps.VOUT vout.vdd)
  net (gnd, ps.GND vout.gnd vin.gnd)
  net (en, ps.VEN)
