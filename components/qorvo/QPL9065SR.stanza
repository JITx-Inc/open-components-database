#use-added-syntax(jitx)
defpackage ocdb/components/qorvo/QPL9065SR :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components
  import ocdb/utils/bundles
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/property-structs

public pcb-component component :
  name = "QPL9065SR"
  manufacturer = "Qorvo"
  mpn = "QPL9065SR"
  description = "RF Amplifier .45-3.8GHz NF .55dB Gain 37.5dB"
  pin-properties :
    [pin:Ref | pads:Int ...           | side:Dir]
    [rfin    | 2                      | Left    ]
    [rfout   | 11                     | Right   ]
    [gnd     | 1 3 4 7 10 12 14 15 17 | Down    ]
    [nc      | 6                      | Down    ]
    [vpd     | 5                      | Up      ]
    [vbyp    | 8                      | Up      ]
    [vdd1    | 16                     | Up      ]
    [vdd2    | 13                     | Up      ]
    
  make-box-symbol()
  assign-landpattern(qfn-landpattern(0.5, 3.5, 16, 0.25, 0.35, [2.2, 2.2]))

  property(self.vdd1.power-pin) = PowerPin(min-max(3.3, 5.25))
  property(self.vdd2.power-pin) = PowerPin(min-max(3.3, 5.25))

  property(self.rated-temperature) = min-max(-40.0, 105.0)

public pcb-module module :
  pin gnd
  pin vdd
  pin rfin
  pin rfout
  inst lna : ocdb/components/qorvo/QPL9065SR/component

  inst l2 : smd-inductor(2.2e-9)
  net (l2.p[1], lna.vdd1)
  cap-strap(l2.p[2], gnd, 100.0e-6)
  cap-strap(l2.p[2], gnd, 0.1e-6)

  inst l5 : smd-inductor(18.0e-9)
  net (l5.p[1], lna.vdd2)
  cap-strap(l5.p[2], gnd, 100.0e-6)
  cap-strap(l5.p[2], gnd, 0.1e-6)
  cap-strap(vdd, gnd, 4.7e-6)
  net (l5.p[2], l2.p[2], vdd)

  block-cap(lna.rfout, rfout, 100.0e-6)
  inst l1 : smd-inductor(1.5e-9)
  inst l4 : smd-inductor(1.5e-9)
  cap-strap(l1.p[1], gnd, 0.47e-6)
  net (l1.p[1], rfin)
  net (l1.p[2], l4.p[1])
  block-cap(lna.rfin, l4.p[1], 10.0e-6)
  net (gnd, l4.p[2], lna.gnd)
  dip-pull([lna.vbyp, lna.vpd], vdd, gnd)
