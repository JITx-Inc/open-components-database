#use-added-syntax(jitx)
defpackage ocdb/components/power-integrations/LNK304DN :
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/box-symbol
  import ocdb/utils/landpatterns
  import ocdb/utils/bundles
  import ocdb/utils/property-structs

public pcb-landpattern SO-8C :
  make-dual-row-smd-landpattern(
      4,    ; the number of pins along one side of the land pattern
      4,    ; the number of pins along the other side of the land pattern
      1.27, ; the pitch of pins on the primary side of the land pattern
      1.27, ; the pitch of pins on the secondary side of the land pattern
      typ(6.0), ; the overall lead span, across both sides of the land pattern
      typ(4.9), ; the length of the package
      tol(3.9), ; the width of the package
      tol(1.05, 0.15), ; the length of the terminals of the package
      tol(0.25, 0.05), ; the width of the terminals of the package 
      false ; optional parameter to include or remove the polarity marker
    )

  model3d = Model3D("../../3d-models/LNK304DN.stp",
    Vec3D(0.0, 0.0, 0.0),
    Vec3D(1.0, 1.0, 1.0),
    Vec3D(0.0, 0.0, 0.0))

public pcb-component component :
  manufacturer = "Power Integrations"
  mpn = "LNK304DN"
  reference-prefix = "U"
  datasheet = "https://www.power.com/downloads/documents/linkswitch-tn_family_datasheet.pdf"
  description = "Energy-Efficient Off-Line Switcher IC - low cost, low component count." 
  property(self.height) = "1.75"
  property(self.rated-temperature) = 142.0

  pin-properties :
    [pin:Ref | pads:Ref ... | side:Dir | electrical-type:String]
    [BP | p[1] | Left | "Passive"]
    [FB | p[2] | Left | "Passive"]
    [D | p[4] | Left | "Passive"]
    [S_1 | p[5] | Right | "Passive"]
    [S_2 | p[6] | Right | "Passive"]
    [S_3 | p[7] | Right | "Passive"]
    [S_4 | p[8] | Right | "Passive"]

  assign-landpattern(SO-8C)
  make-box-symbol()

  property(self.D.power-pin) = PowerPin(min-max(70.0, 700.0))
  property(self.FB.power-pin) = PowerPin(min-max(-0.3, 9.0))
  property(self.BP.power-pin) = PowerPin(min-typ-max(5.55, 5.8, 6.10))