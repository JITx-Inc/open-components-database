#use-added-syntax(esir)
defpackage ocdb/texas-instruments/TL5209:
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

;SOIC-8
unique pcb-package footprint :
  val smd-pad = smd-pad(1.55, 0.6)
  val x0 = 2.7
  pad p[1] : {smd-pad} at loc((- x0), 1.905)
  pad p[2] : {smd-pad} at loc((- x0), 0.635)
  pad p[3] : {smd-pad} at loc((- x0), -0.635)
  pad p[4] : {smd-pad} at loc((- x0), -1.905)
  pad p[5] : {smd-pad} at loc(x0, -1.905)
  pad p[6] : {smd-pad} at loc(x0, -0.635)
  pad p[7] : {smd-pad} at loc(x0, 0.635)
  pad p[8] : {smd-pad} at loc(x0, 1.905)
  layer(Courtyard(Top)) = Rectangle(7.15, 5.0)
  layer(Silkscreen("values", Top)) = Text(">REF", 0.7, C, loc(0.0, 0.0))
  ;height = 1.75mm

public unique pcb-component component :
  manufacturer = "Texas Instruments"
  mpn = "TL5209DR"
  description = "Linear Voltage Regulator IC 1 Output 500mA 8-SOIC"
  val ps = PinSpec $ #TABLE :
    [Ref | Int ...   | Dir  ]
    [IN        | 2   | Left  ]
    [EN        | 1   | Left  ]
    [GND1      | 5   | Left  ]
    [GND2      | 6   | Left  ]
    [OUT       | 3   | Right ]
    [ADJ/BYP   | 4   | Right ]
    [GND3      | 7   | Right ]
    [GND4      | 8   | Right ]
  gen-symbol-map(ps, footprint) 

public pcb-module ldo_module (v-out:Double):
  pin VIN
  pin VOUT
  pin GND
  pin EN
  pin FB

  val r2 = 470.0e3                      ;r2 should be <= 470k for optimal performance
  val r1 = closest-std-val(r2 / ((v-out / 1.242) - 1.0), 1.0) ;maximum v-out = 6.75V +/- 10% -- error: want this to round down vs. up

  inst ldo : {ocdb/texas-instruments/TL5209/component}
  net (GND ldo.GND1 ldo.GND2 ldo.GND3 ldo.GND4)
  net (VIN ldo.IN)
  cap-strap(ldo.IN, GND, 1.0e-6)        ;datasheet recommends 1.0uF minimum input cap
  net (EN ldo.EN)
  net (VOUT ldo.OUT)
  net (FB ldo.ADJ/BYP)
  res-strap(VOUT, FB, r1)
  res-strap(FB, GND, r2)
  cap-strap(VOUT, GND, 4.7e-6)          ;datasheet recommends 4.7uF output cap (<100uA start-up load), 10uF for higher start-up loads
  cap-strap(FB, GND, 470.0e-12)         ;datasheet recommends 470pF bypass cap