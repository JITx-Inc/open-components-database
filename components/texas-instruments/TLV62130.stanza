#use-added-syntax(esir)
defpackage ocdb/texas-instruments/TLV62130:
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

public unique pcb-component component :
  manufacturer = "Texas Instruments"
  mpn = "TLV62130RGTR"
  description = "TLV62130x 3-V to 17-V 3-A Step-Down Converter In 3x3 QFN"
  val ps = PinSpec $ #TABLE :
    [Ref | Int ...   | Dir  ]
    [AGND | 6     | Down ]
    [AVIN | 10    | Left ]
    [DEF  | 8     | Left ]
    [EN   | 13    | Left ]
    [FB   | 5     | Left ]
    [FSW  | 7     | Left ]
    [PG   | 4     | Right]
    [PGND | 15 16 | Down ]
    [PVIN | 11 12 | Left ]
    [SS   | 9     | Left ]
    [SW   | 1 2 3 | Right]
    [VOS  | 14    | Left ]
    [PAD  | 17    | Down ]
  gen-symbol-map(ps, qfn-package(0.5, 3.0, 16, 0.24, 0.5, [1.68, 1.68]))



public pcb-module module (v-out:Double):
  pin vin
  pin vout
  pin gnd
  pin en
  pin pg
  pin ss
  val r2 = 180.0e3
  val r1 = r2 * (v-out / 0.8 - 1.0)

  inst buck : {ocdb/texas-instruments/TLV62130/component}
  inst l    : {ocdb/abracon/ASPI-0530HI/component(2.2)}

  net (gnd buck.AGND buck.PGND buck.PAD buck.DEF buck.FSW)
  net (vin buck.AVIN buck.PVIN)
  cap-strap(buck.PVIN, gnd, 10.0e-6)
  cap-strap(buck.PVIN, gnd, 1.0e-6)

  net (ss buck.SS)
  cap-strap(buck.SS, gnd, 3.3e-9)

  net sw (l.p[1] buck.SW)
  net (vout l.p[2] buck.VOS)
  net (pg buck.PG)
  res-strap(l.p[2], buck.PG, 100.0e3)
  res-strap(l.p[2], buck.FB, r1)
  res-strap(gnd,    buck.FB, r2)

  cap-strap(vout, gnd, 22.0e-6)

  properties(buck.EN):
    apply-enable-pin-params(#R(AVIN))