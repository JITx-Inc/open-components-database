#use-added-syntax(jitx)
defpackage ocdb/components/texas-instruments/CC2640 :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

pcb-bundle analog :
  pin analog

pcb-bundle high-drive :
  pin high-drive

pcb-bundle sensor-ctrl : 
  pin sensor-ctrl

;<test>skip<test>
public pcb-component component :
  manufacturer = "Texas Instruments"
  mpn = "CC2640F128Rxx"
  description = "SimpleLink™ 32-bit Arm Cortex-M3 Bluetooth® Low Energy wireless MCU with 128kB Flash"
  
  pin-properties :
    [pin:Ref   | pads:Int ... | side:Dir | bank:Ref]
    [VDDR      | 45           | Right    | base   ]
    [VDDR_RF   | 48           | Right    | base   ]
    [VDDS      | 44           | Right    | base   ]
    [VDDS2     | 13           | Right    | base   ]
    [VDDS3     | 22           | Right    | base   ]
    [VDDS_DCDC | 34           | Right    | base   ]
    [DCDC_SW   | 33           | Right    | base   ]
    [DCOUPL    | 23           | Right    | base   ]
    [EGP       | 49           | Right    | base   ]
    for i in 0 to 8 do :        
      [DIO_[i] | i + 5        | Left     | gpio ]
    for i in 8 to 16 do :   
      [DIO_[i] | i + 6        | Left     | gpio ]
    for i in 16 to 23 do :   
      [DIO_[i] | i + 10       | Right    | gpio ]
    for i in 23 to 31 do :   
      [DIO_[i] | i + 13       | Right    | gpio ]
    [JTAG_TMSC | 24           | Left     | base   ]
    [JTAG_TCKC | 25           | Left     | base   ]
    [RESET_N   | 35           | Left     | base   ]
    [RF_P      | 1            | Left     | base   ]
    [RF_N      | 2            | Left     | base   ]
    [X32K_Q1   | 3            | Left     | base   ]
    [X32K_Q2   | 4            | Left     | base   ]
    [X24M_N    | 46           | Left     | base   ]
    [X24M_P    | 47           | Left     | base   ]
    
  make-box-symbol()
  assign-landpattern(qfn-landpattern(0.5, 7.0, 48, 0.25, 0.4, [5.15 5.15]))

  pcb-bundle io-pin : (pin p)
  for i in 0 to 31 do :
    supports io-pin :
      io-pin.p => self.DIO_[i]

  for i in 23 to 31 do :
    supports analog :
      analog.analog => self.DIO_[i]
    supports sensor-ctrl :
      sensor-ctrl.sensor-ctrl => self.DIO_[i]

  for i in 0 to 8 do :
    supports sensor-ctrl :
      sensor-ctrl.sensor-ctrl => self.DIO_[i]

  ; Use io-pin to map bundles to peripherals
  ; There are 2 SPI peripherals

  ; for i in 0 to 2 do:
  ;   supports spi:
  ;     require pins:{io-pin}[4]
  ;     sdo  =>  pins[0].p
  ;     sdi  =>  pins[1].p
  ;     sck   =>  pins[2].p
  ;     ss    =>  pins[3].p

  ; supports i2c:
  ;   require pins:{io-pin}[2]
  ;   sda   =>    pins[0].p
  ;   scl   =>    pins[1].p

  val uart = uart()
  supports uart:
    require pins:io-pin[2]
    uart.rx    =>    pins[0].p
    uart.tx    =>    pins[1].p

  val jtag = jtag()
  supports jtag:
    require pins:io-pin[3]
    jtag.tms => self.JTAG_TMSC
    jtag.tck => self.JTAG_TCKC
    jtag.tdi => pins[0].p
    jtag.tdo => pins[1].p
    jtag.trstn => pins[2].p

  for i in 0 to 31 do:
    supports gpio:
      require pins:io-pin
      gpio.gpio => pins.p

  for i in 5 to 8 do :
    supports high-drive :
      high-drive.high-drive => self.DIO_[i]

  supports high-drive :
    high-drive.high-drive => self.DIO_[16]
  supports high-drive :
    high-drive.high-drive => self.DIO_[17]

public pcb-module module:
  ; define ports
  port i2c-node : i2c
  port spi: spi-controller()
  pin gnd
  pin vdds
  pin vddr
  pin vcc

  ; start with the CC2640 mcu
  inst mcu : ocdb/components/texas-instruments/CC2640/component
  ; set up primary power nets
  net (gnd, mcu.EGP)
  net (vdds, mcu.VDDS, mcu.VDDS2, mcu.VDDS3, mcu.VDDS_DCDC)
  net (vddr, mcu.VDDR, mcu.VDDR_RF)
  
  ; add decoupling caps to VDDS
  for i in 0 to 4 do :
    cap-strap(mcu.VDDS, gnd, 0.1e-6)
  cap-strap(mcu.VDDS, gnd, 10.0e-6)

  ; add decoupling caps and inductor to VDDR
  cap-strap(vddr, gnd, 10.0e-6)
  cap-strap(vddr, gnd, 0.1e-6)
  cap-strap(vddr, gnd, 0.1e-6)
  inst l1 : ocdb/components/murata/BLM18HE152SN1D/component
  net (vcc, l1.p[1])
  net (l1.p[2], vdds)
  inst l2 : smd-inductor(10.0e-6)
  net (mcu.DCDC_SW, l2.p[1])
  net (mcu.VDDR, l2.p[2])

  cap-strap(mcu.DCOUPL, gnd, 1.0e-6)

  ; reset pin circuitry
  res-strap(mcu.RESET_N, vdds, 100.0e3)
  cap-strap(mcu.RESET_N, gnd, 0.1e-6)

  ; add RF section
  inst balun : ocdb/components/johanson/2450BM14G0011/component
  inst ant : ocdb/components/johanson/2450AT18D0100/component
  inst jsc : ocdb/components/murata/MM5829-2700R/component
  net (mcu.RF_N, balun.BAL1)
  net (mcu.RF_P, balun.BAL2)
  net (gnd, balun.GND)
  ; add tuning network
  inst rfr1 : chip-resistor(0.0)
  inst rfr2 : chip-resistor(0.0)
  inst rfrjsc : chip-resistor(0.0)
  net (rfrjsc.p[1], balun.UBAL, rfr1.p[1])
  net (jsc.g[1], gnd)
  net (jsc.p[1], rfrjsc.p[2])
  net (rfr1.p[2], rfr2.p[1])
  cap-strap(rfr1.p[2], gnd, 1.0e-6)
  cap-strap(rfr2.p[2], gnd, 1.0e-6)
  net (gnd, ant.p[2], ant.p[3], ant.p[4])
  net (rfr2.p[2], ant.p[1])

  ; add XTALS
  inst x1 : ocdb/components/epson/FC-135/component
  inst x2 : ocdb/components/epson/TSX-3225/component(16.0e6)

  net (x1.p[1], mcu.X32K_Q1)
  net (x1.p[2], mcu.X32K_Q2)
  cap-strap(mcu.X32K_Q1, gnd, 12.0e-12)
  cap-strap(mcu.X32K_Q2, gnd, 12.0e-12)

  net (x2.p[1], mcu.X24M_P)
  net (x2.p[3], mcu.X24M_N)
  net (gnd, x2.p[2], x2.p[4])
