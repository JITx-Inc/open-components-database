#use-added-syntax(jitx)
defpackage ocdb/texas-instruments/TLV743P :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults
  import ocdb/land-patterns
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
  import ocdb/symbols
  import ocdb/box-symbol

;<test>
v-out: 1.5
<test>
public pcb-component component (v-out:Double) :
  pin en
  pin gnd
  pin in
  pin out
  pin nc
  name = "TLV743P"
  manufacturer = "Texas Instruments"
  description = "TLV743P 300-mA, Low-Dropout Regulator"
  val [code spice-code] = switch(v-out) :
    1.5 : ["15" "TLV74315P"]
    2.5 : ["25" "TLV74325P"]
    3.3 : ["33" "TLV74333P"]
  mpn = to-string("TLV743%_P" % [code])
  pin-properties :
    [pin:Ref | pads:Int ... | side:Dir]
    [gnd     | 2            | Down    ]
    [in      | 1            | Left    ]
    [en      | 3            | Left    ]
    [out     | 5            | Right   ]
    [nc      | 4            | Down    ]
  make-box-symbol()
  assign-landpattern(SOT95P280X145-5N)

  property(in.power-pin) = true
  property(in.rated-voltage) = [1.4 5.5]
  property(out.supply-pin) = true
  property(out.voltage) = v-out

  spice:
    "[X] {en} {gnd} {in} {out} {nc} {spice-code}"

public pcb-module module (v-out:Double) :
  pin vin
  pin vout
  pin gnd
  pin en
  inst ps : ocdb/texas-instruments/TLV743P/component(v-out)
  cap-strap(gnd, ps.in, 1.0e-6)
  cap-strap(gnd, ps.out, 1.0e-6)
  net (vin, ps.in)
  net (vout, ps.out)
  net (gnd, ps.gnd)
  net (en, ps.en)

public pcb-module mod :
  inst m : module(3.3)
