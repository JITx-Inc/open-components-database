#use-added-syntax(jitx)
defpackage ocdb/components/pulse-electronics/J0G-0009NL :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults

  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/landpatterns
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

public pcb-component component :
  manufacturer = "Pulse Electronics Network"
  mpn = "J0G-0009NL"  
  port trd:trd[4]
  pin shield
  pin-properties :
    [pin:Ref       | pads:Int ... | side:Dir]
    [trd[0].trd.P  | 11           | Left    ]
    [trd[0].trd.N  | 10           | Left    ]
    [trd[0].common | 12           | Left    ]
    [trd[1].trd.P  | 4            | Left    ]
    [trd[1].trd.N  | 5            | Left    ]
    [trd[1].common | 6            | Left    ]
    [trd[2].trd.P  | 3            | Left    ]
    [trd[2].trd.N  | 2            | Left    ]
    [trd[2].common | 1            | Left    ]
    [trd[3].trd.P  | 8            | Left    ]
    [trd[3].trd.N  | 9            | Left    ]
    [trd[3].common | 7            | Left    ]
    [shield        | 18 19        | Down    ]

  supports ethernet-1000 :
    ethernet-1000.mdi[0].P => self.trd[0].trd.P
    ethernet-1000.mdi[0].N => self.trd[0].trd.N
    ethernet-1000.mdi[1].P => self.trd[1].trd.P
    ethernet-1000.mdi[1].N => self.trd[1].trd.N
    ethernet-1000.mdi[2].P => self.trd[2].trd.P
    ethernet-1000.mdi[2].N => self.trd[2].trd.N
    ethernet-1000.mdi[3].P => self.trd[3].trd.P
    ethernet-1000.mdi[3].N => self.trd[3].trd.N
  make-box-symbol()
  assign-landpattern(pulse-J0G-0009NL-pkg)

pcb-landpattern pulse-J0G-0009NL-pkg :
  val pin-row = row-locs(6, 2.03)
  for (i in 1 through 6, l in pin-row) do :
    pad p[i] : pth-pad(0.89 / 2.0) at loc(0.0, -6.6) * l
  val three-row = row-locs(3, 2.03)
  for (i in 7 through 9, l in three-row) do :
    pad p[i] : pth-pad(0.89 / 2.0) at loc(-4.0 * 1.02, -9.14) * l
  for (i in 10 through 12, l in pin-row) do :
    pad p[i] : pth-pad(0.89 / 2.0) at loc(1.0 * 1.02, -9.14, 180.0) * l

  pad p[13] : pth-pad(1.27 / 2.0) at loc(-15.04 / 2.0, 8.89 - 2.54)  
  pad p[14] : pth-pad(1.27 / 2.0) at loc(-15.04 / 2.0, 8.89)  
  pad p[15] : pth-pad(1.27 / 2.0) at loc(15.04 / 2.0, 8.89)  
  pad p[16] : pth-pad(1.27 / 2.0) at loc(15.04 / 2.0, 8.89 - 2.54)  
  pad p[17] : pth-pad(1.27 / 2.0) at loc(15.04 / 2.0, 8.89 - 2.54 * 2.0)  
  pad p[18] : pth-pad(1.57 / 2.0) at loc(17.58 / 2.0, 0.0)  
  pad p[19] : pth-pad(1.57 / 2.0) at loc(-17.58 / 2.0, 0.0)

  layer(Cutout()) = Circle(9.65 / 2.0, 0.0, 1.6)
  layer(Cutout()) =   Circle(-9.65 / 2.0, 0.0, 1.6)
  layer(Courtyard(Top)) = Rectangle(17.58, 24.13, loc(0.0, 1.5))

public pcb-module module :
  inst c : component
