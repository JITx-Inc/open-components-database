#use-added-syntax(jitx)
defpackage ocdb/components/windbond/W25Q128JVSIQ :
  import core
  import collections
  import math
  import jitx
  import jitx/commands

  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/symbol-utils
  import ocdb/utils/generic-components

  import ocdb/utils/box-symbol
  import ocdb/utils/bundles


public pcb-component component :
  port spi:spi-peripheral()
  pin-properties :
    [pin:Ref   | pads:Int ...  | side:Dir]
    [nWP       | 3             | Left    ]
    [nHOLD     | 7             | Left    ]
    [spi.cs    | 1             | Left    ]
    [spi.sdo   | 2             | Left    ]
    [spi.sdi   | 5             | Left    ]
    [GND       | 4             | Down    ]
    [spi.sck   | 6             | Left    ]
    [VCC       | 8             | Up      ]
  
  make-box-symbol()
  assign-landpattern(soic127p-landpattern(8))

  mpn = "W25Q128JVSIQ"
  manufacturer = "Winbond Electronics"
  description = "FLASH - NOR Memory IC  SPI - Quad I/O, QPI, DTR 133MHz  8-SOIC"

public pcb-module module:
  port power : power
  port spi: spi-peripheral()
  port wp
  port hold

  inst flash : ocdb/components/windbond/W25Q128JVSIQ/component
  cap-strap(flash.VCC, flash.GND, 0.1e-6)
  net (power.vdd, flash.VCC)
  net (power.gnd, flash.GND)
  net (spi, flash.spi)
  net (wp, flash.nWP)
  net (hold, flash.nHOLD)
