#use-added-syntax(jitx)
defpackage ocdb/components/fine-made/SC1117-3-3V :
  import core
  import collections
  import math
  import jitx
  import jitx/commands

  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/symbol-utils
  import ocdb/utils/generic-components
  import ocdb/utils/bundles
  import ocdb/utils/box-symbol
  import ocdb/utils/property-structs

pcb-landpattern SOT223-lp :
  pad p[1] : smd-pad(1.2, 1.6) at loc(-2.3, -3.2)
  pad p[2] : smd-pad(1.2, 1.6) at loc(0.0, -3.2)
  pad p[3] : smd-pad(1.2, 1.6) at loc(2.3, -3.2)
  pad p[4] : smd-pad(3.3, 1.6) at loc(0.0, 3.2)
  layer(Courtyard(Top)) = Rectangle(6.8, 8.2)
  layer(Silkscreen("f-silk", Top)) = Line(0.12, [ Point(-3.4, -3.2), Point(-3.4, -4.0) ])
  ref-label()

public pcb-component component :
  pin IN
  pin GND
  pin OUT
  val pkg = SOT223-lp
  val generic-props = GenericPin(min-max(-0.3, 3.6), 1500.0)
  val power-props = PowerPin(min-max(3.1, 3.5)) ; best interpretation
  val power-in = PowerPin(min-max(4.0, 7.0))
  pin-properties :
    [pin:Ref | pads:Int ... | side:Dir| generic-pin:GenericPin | power-pin:PowerPin]
    [IN      | 3    | Left    | -             | power-in ]
    [GND     | 1    | Down    | generic-props | - ]
    [OUT     | 2 4  | Right   | -             | power-props ]
  name = "3.3V LDO"
  reference-prefix = "U"
  manufacturer = "Fine Made"
  description = "3.3V LDO"
  datasheet = "https://datasheet.lcsc.com/lcsc/2105181504_Shenzhen-Fuman-Elec-SC1117-3.3V_C173386.pdf"
  mpn = "SC1117-3.3V / AMS1117"
  supports power:
    power.vdd => self.OUT
    power.gnd => self.GND
  make-box-symbol()
  assign-landpattern(pkg)

  property(self.rated-temperature) = min-max(-40.0, 85.0)
