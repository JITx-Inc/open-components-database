#use-added-syntax(jitx)
defpackage ocdb/tag-connect/TC2030-IDC :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

pcb-landpattern footprint :
  ; landing pads
  for (l in grid-locs(2, 3, 1.27, 1.27), i in [2 4 6 1 3 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  
  ; alignment holes
  layer(Cutout()) = Circle((- 2.54), 0.0, 0.4955)
  layer(Cutout()) = Circle(2.54, 1.016, 0.4955)
  layer(Cutout()) = Circle(2.54, (- 1.016), 0.4955)

  ; legs
  layer(Cutout()) = Circle((-2.54), 2.54, 1.1875)
  layer(Cutout()) = Circle((-2.54), (-2.54), 1.1875)
  layer(Cutout()) = Circle(0.635, 2.54, 1.1875)
  layer(Cutout()) = Circle(0.635, (-2.54), 1.1875)

  layer(Courtyard(Top)) = Rectangle(7.75, 8.5, loc((-0.692), 0.0))
  layer(Courtyard(Bottom)) = Rectangle(7.75, 11.0, loc((-0.692), 0.0))
  layer(ForbidCopper(LayerIndex(0), LayerIndex(0, Bottom))) = Rectangle(2.54, 1.27)
  layer(Silkscreen("f-silk", Top)) = Rectangle(7.75, 11.0, loc((-0.692), 0.0))
  ref-label()

public pcb-component component :
  pin VCC
  pin SWDIO
  pin nRESET
  pin SWDCK
  pin GND
  pin SWO

  val sym = header-symbol(6,2)
  symbol = sym( VCC => sym.p[1], SWDIO => sym.p[2], 
                nRESET => sym.p[3], SWDCK => sym.p[4],
                GND => sym.p[5], SWO => sym.p[6])

  val land = footprint
  landpattern = land( VCC => land.p[1], SWDIO => land.p[2], 
                      nRESET => land.p[3], SWDCK => land.p[4],
                      GND => land.p[5], SWO => land.p[6])
  reference-prefix = "J"