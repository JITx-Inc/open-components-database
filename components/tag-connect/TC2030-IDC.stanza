#use-added-syntax(jitx)
defpackage ocdb/components/tag-connect/TC2030-IDC :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

  import ocdb/utils/property-structs
  import ocdb/utils/checks

; NOTE PINOUT ORDER DIFFERENT THAN 10-PIN
; Datasheet showing pinout: https://www.tag-connect.com/wp-content/uploads/bsk-pdf-manager/2019/12/TC2030-IDC-Datasheet-Rev-B.pdf

pcb-landpattern TC2030-IDC-footprint :
  ; landing pads
  for (l in grid-locs(2, 3, 1.27, 1.27), i in [2 4 6 1 3 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  
  ; alignment holes
  layer(Cutout()) = Circle((- 2.54), 0.0, 0.4955)
  layer(Cutout()) = Circle(2.54, 1.016, 0.4955)
  layer(Cutout()) = Circle(2.54, (- 1.016), 0.4955)

  ; legs
  layer(Cutout()) = Circle((-2.54), 2.54, 1.1875)
  layer(Cutout()) = Circle((-2.54), (-2.54), 1.1875)
  layer(Cutout()) = Circle(0.635, 2.54, 1.1875)
  layer(Cutout()) = Circle(0.635, (-2.54), 1.1875)

  layer(Courtyard(Top)) = Rectangle(7.75, 8.5, loc((-0.692), 0.0))
  ref-label()

public pcb-component component :
  description = "Insertion point for TC-2030 connector"
  datasheet = "https://www.tag-connect.com/wp-content/uploads/bsk-pdf-manager/2019/12/TC2030-IDC-Datasheet-Rev-B.pdf"
  manufacturer = "Tag-Connect"
  mpn = "TC2030-IDC"
  property(self.trust) = "built"
  port p: pin[1 through 6]

  val sym = header-symbol(6, 2)
  symbol = sym(for i in 1 through 6 do : p[i] => sym.p[i])
  landpattern = TC2030-IDC-footprint(for i in 1 through 6 do : p[i] => TC2030-IDC-footprint.p[i])
  reference-prefix = "J"

  property(self.rated-temperature) = false

  supports swd([SWD-SWO, SWD-TRACESWO]) :
    swd([SWD-SWO, SWD-TRACESWO]).swdio  => self.p[2]
    swd([SWD-SWO, SWD-TRACESWO]).swdclk => self.p[4]
    swd([SWD-SWO, SWD-TRACESWO]).swo => self.p[6]
    swd([SWD-SWO, SWD-TRACESWO]).traceswo => self.p[1]

  supports power:
    power.vdd => self.p[1]
    power.gnd => self.p[5]

  supports reset:
    reset.reset => self.p[3]

