#use-added-syntax(jitx)
defpackage ocdb/tag-connect/TC2050-IDC :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/box-symbol
  import ocdb/module-utils
  import ocdb/tolerance

  import ocdb/property-structs
  import ocdb/checks

; NOTE UNUSUAL PINOUT
; Datasheet showing pinout: https://www.tag-connect.com/wp-content/uploads/bsk-pdf-manager/TC2050-IDC_Datasheet_7.pdf

pcb-landpattern TC2050-IDC-footprint :
  for (l in grid-locs(2, 5, 1.27, 1.27), i in [10 9 8 7 6 1 2 3 4 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  layer(Cutout()) = Circle((- 3.81), 0.0, 0.5)
  layer(Cutout()) = Circle(3.81, 1.015, 0.5)
  layer(Cutout()) = Circle(3.81, (- 1.015), 0.5)

; Leg Latch Holes  
  layer(Cutout()) = Circle((- 3.81), 2.54, 1.1875)
  layer(Cutout()) = Circle((- 3.81), -2.54, 1.1875)
  layer(Cutout()) = Circle(1.905, ( 2.54), 1.1875)
  layer(Cutout()) = Circle(1.905, (- 2.54), 1.1875)
  layer(Courtyard(Top)) = Rectangle(10.16, 7.62)
  ref-label()

public pcb-component component :
  description = "Insertion point for TC-2050 connector"
  manufacturer = "Tag-Connect"
  mpn = "TC2050-IDC"
  port p: pin[1 through 10]
  pin-properties :
    [pin:Ref     | pads:Int ... | side:Dir]
    [p[1]          | 1             | Left     ]
    [p[2]          | 2             | Left     ]
    [p[3]          | 3             | Left     ]
    [p[4]          | 4             | Left     ]
    [p[5]          | 5             | Left     ]
    [p[10]         | 10            | Right    ]
    [p[9]          | 9             | Right    ]
    [p[8]          | 8             | Right    ]
    [p[7]          | 7             | Right    ]
    [p[6]          | 6             | Right    ]
  landpattern = TC2050-IDC-footprint(for i in 1 through 10 do : p[i] => TC2050-IDC-footprint.p[i])
  reference-prefix = "J"
  make-box-symbol()
  property(self.rated-temperature) = RatedTemperature(min-max(-40.0, 125.0))

  supports jtag() :
    jtag().tms => self.p[2]
    jtag().tck => self.p[4]
    jtag().tdo => self.p[6]
    jtag().tdi => self.p[8]
    jtag().trstn => self.p[10]

  supports swd([SWD-SWO, SWD-TRACESWO]) :
    swd([SWD-SWO, SWD-TRACESWO]).swdio  => self.p[2]
    swd([SWD-SWO, SWD-TRACESWO]).swdclk => self.p[4]
    swd([SWD-SWO, SWD-TRACESWO]).swo => self.p[6]
    swd([SWD-SWO, SWD-TRACESWO]).traceswo => self.p[8]

  supports power:
    power.vdd => self.p[1]
    power.gnd => self.p[3]

public pcb-module module : 
  make-10-pin-debug-connector-module(component)
