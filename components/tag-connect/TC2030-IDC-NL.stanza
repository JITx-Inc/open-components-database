#use-added-syntax(jitx)
defpackage ocdb/tag-connect/TC2030-IDC-NL :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/tolerance

  import ocdb/property-structs
  import ocdb/checks

; NOTE PINOUT ORDER DIFFERENT THAN 10-PIN
; Datasheet showing pinout: https://www.tag-connect.com/wp-content/uploads/bsk-pdf-manager/2019/12/TC2030-IDC-NL-Datasheet-Rev-B.pdf

pcb-landpattern TC2030-IDC-NL-footprint :
  ; landing pads
  for (l in grid-locs(2, 3, 1.27, 1.27), i in [2 4 6 1 3 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  
  ; alignment holes
  layer(Cutout()) = Circle((- 2.54), 0.0, 0.4955)
  layer(Cutout()) = Circle(2.54, 1.016, 0.4955)
  layer(Cutout()) = Circle(2.54, (- 1.016), 0.4955)

  layer(Courtyard(Top)) = Rectangle(6.08, 3.032 )
  ref-label()

public pcb-component component :
  description = "Insertion point for TC-2030-NL connector"
  manufacturer = "Tag-Connect"
  mpn = "TC2030-IDC-NL"
  port p: pin[1 through 6]

  val sym = header-symbol(6,2)
  symbol = sym(for i in 1 through 6 do : p[i] => sym.p[i])
  landpattern = TC2030-IDC-NL-footprint(for i in 1 through 6 do : p[i] => TC2030-IDC-NL-footprint.p[i])
  reference-prefix = "J"
  property(self.rated-temperature) = RatedTemperature(min-max(-40.0, 125.0))

  supports swd([SWD-SWO, SWD-TRACESWO]) :
    swd([SWD-SWO, SWD-TRACESWO]).swdio  => self.p[2]
    swd([SWD-SWO, SWD-TRACESWO]).swdclk => self.p[4]
    swd([SWD-SWO, SWD-TRACESWO]).swo => self.p[6]
    swd([SWD-SWO, SWD-TRACESWO]).traceswo => self.p[1]