#use-added-syntax(jitx)
defpackage ocdb/tag-connect/TC2050-IDC-NL :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

pcb-landpattern footprint :
  for (l in grid-locs(2, 5, 1.27, 1.27), i in [1 3 5 7 9 2 4 6 8 10]) do :
    pad p[i] : testpoint-pad(0.787) at l
  
  layer(Cutout()) = Circle((- 3.81), 0.0, 0.5)
  layer(Cutout()) = Circle(3.81, 1.015, 0.5)
  layer(Cutout()) = Circle(3.81, (- 1.015), 0.5)
  layer(Courtyard(Top)) = Rectangle(8.62, 3.03)
  layer(Silkscreen("f-silk", Top)) = LineRectangle(8.62, 3.03)
  ref-label()

public pcb-component component :
  description = "Insertion point for TC-2050 connector"
  port p: pin[1 through 10]
  val sym = header-symbol(10,2)
  symbol = sym(for i in 1 through 10 do : p[i] => sym.p[i])
  landpattern = footprint(for i in 1 through 10 do : p[i] => footprint.p[i])

public pcb-module module :
  port j : jtag 
  port s : swd
  port power : power
  public inst con : ocdb/tag-connect/TC2050-IDC-NL/component
  net (j.trstn, con.p[10])
  net (j.tdi, con.p[8])
  net (j.tdo, con.p[6])
  net (j.tck, swd.swdclk, con.p[4])
  net (j.tms, swd.swdio, con.p[2])
  net (power.vdd, con.p[1])
  net (power.gnd, con.p[9], con.p[5], con.p[3])

  supports jtag:
    jtag.trstn => con.p[10]
    jtag.tdi => con.p[8]
    jtag.tdo => con.p[6]
    jtag.tck => con.p[4]
    jtag.tms => con.p[2]

  supports swd:
    swd.swdclk => con.p[4]
    swd.swdio => con.p[2]