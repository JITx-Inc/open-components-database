#use-added-syntax(esir)
defpackage ocdb/tag-connect/TC2050-IDC-NL :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

  import jitpcb/visualizer

pcb-package footprint :
  for (l in grid-locs(2, 5, 1.27, 1.27), i in [1 2 3 4 5 10 9 8 7 6]) do :
    pad p[i] : {testpoint-pad(0.787)} at l
  
  layer(Cutout()) = Circle((- 3.81), 0.0, 0.5)
  layer(Cutout()) = Circle(3.81, 1.015, 0.5)
  layer(Cutout()) = Circle(3.81, (- 1.015), 0.5)
  layer(Courtyard(Top)) = Rectangle(8.62, 3.03)
  layer(Silkscreen("f-silk", Top)) = LineRectangle(8.62, 3.03)
  ref-label()

public unique pcb-component component :
  description = "Insertion point for TC-2050 connector"
  for i in 1 to 11 do :
    pin p[i]
  symbol = {header-symbol(10,2)}(for i in 1 to 11 do : p[i] => p[i])
  package = footprint(for i in 1 to 11 do : p[i] => p[i])

public unique pcb-module module :
  port j : jtag 
  port swd : swd
  port pwr : power
  inst jtag : {ocdb/tag-connect/TC2050-IDC-NL/component}
  net (j.trstn, jtag.p[10])
  net (j.tdi, jtag.p[8])
  net (j.tdo, jtag.p[6])
  net (j.tck, swd.swdclk, jtag.p[4])
  net (j.tms, swd.swdio, jtag.p[2])
  net (pwr.vdd, jtag.p[1])
  net (pwr.gnd, jtag.p[9], jtag.p[5], jtag.p[3])
