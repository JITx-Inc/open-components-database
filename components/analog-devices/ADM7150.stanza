#use-added-syntax(jitx)
defpackage ocdb/components/analog-devices/ADM7150:
  import core
  import collections
  import math
  import jitx
  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components
  import ocdb/utils/bundles
  import ocdb/utils/symbols
  import ocdb/utils/property-structs
  import ocdb/utils/box-symbol
  import jitx/commands

doc: \<s>
800 mA Ultralow Noise, High PSRR, RF Linear Regulator

# Component
`v-out` is the specified output voltage of the voltage regulator in volts. Valid values are `3.0`, `3.3`, and `5.0`.
<s>

public pcb-component component (v-out:Double) :
  name = "ADM7150"
  manufacturer = "Analog Devices"
  description = "800 mA Ultralow Noise, High PSRR, RF Linear Regulator"
  datasheet = "https://www.analog.com/media/en/technical-documentation/data-sheets/ADM7150.pdf"
  val a-code = switch(v-out) :
    3.0 : "3.0"
    3.3 : "3.3"
    5.0 : "5.0"
    else : fatal("Invalid output voltage for ADM7150 %_" % [v-out])
  mpn = to-string("ADM7150ARDZ-%_-R7" % [a-code])
  pin-properties :
    [pin:Ref | pads:Int ...   | side:Dir]
    [vin  | 8 | Left]
    [en   | 7 | Left]
    [vreg | 1 | Left]
    [byp  | 3 | Left]
    [vout      | 2 | Right] 
    [ref       | 6 | Right] 
    [ref-sense | 5 | Right]
    [gnd | 4 9 | Down]

  make-box-symbol()

  assign-landpattern(soic127p-landpattern(8, [2.3 3.0]))
  property(self.rated-temperature) = min-max(-40.0, 125.0)


doc: \<s>
Example Usage:

```stanza
inst ldo : {ocdb/components/analog-devices/ADM7150/component(3.0)}
```
<img src=FIG:(gen-ocdb-app, svg, view(URL, ocdb/components/analog-devices/ADM7150/component(3.0))) height="400">

"MPN : ADM7150ARDZ-3.0-R7 Package: SOIC127P-8"
<s>

doc: \<s>
# Module
The module is an implementation of the circuit from the datasheet, with Cbyp = 100uF. `v-out` is the specified output voltage of the voltage regulator in volts. Valid values are `3.0`, `3.3`, and `5.0`.
<s>

public pcb-module module (v-out:Double) :
  port dst : power
  port src : power
  pin vin
  pin vout
  pin gnd
  pin en
  net (dst.gnd gnd)
  net (dst.vdd vout)
  net (src.gnd gnd)
  net (src.vdd vout)
  inst ps : ocdb/components/analog-devices/ADM7150/component(v-out)
  cap-strap(ps.vin,  gnd, 10.0e-6)
  cap-strap(ps.vout, gnd, 10.0e-6)
  cap-strap(ps.vreg, gnd, 10.0e-6)
  cap-strap(ps.byp,  gnd, 1.0e-6)
  cap-strap(ps.byp,  gnd, 47.0e-6)
  cap-strap(ps.ref,  gnd, 1.0e-6)
  net (ps.ref, ps.ref-sense)
  net (ps.en, en)
  net (ps.vin, vin)
  net (ps.vout, vout)
  net (ps.gnd gnd)

doc: \<s>
Example usage:
```stanza
inst ldo : {ocdb/components/analog-devices/ADM7150/module(3.0)}
```
<img src=FIG:(gen-ocdb-app, svg, view(URL, single-module-design(ocdb/components/analog-devices/ADM7150/module(3.0), default-board(4, 10.0, 10.0)))) height="400">
<s>

public pcb-module module () :
  inst i : module(3.0)
