#use-added-syntax(jitx)
defpackage ocdb/components/bosch/BMX160 :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

public pcb-component component :
  manufacturer = "Bosch"
  mpn = "BMX160"
  description = "Small, low-power, 9-axis IMU (accel, gyro, magnetometer)"
  pin-properties :
    [pin:Ref | pads:Int ... | side:Dir ]
    [SDO    | 1     | Right ]
    [GND    | 2,3,7 | Down  ]
    [INT1   | 4     | Left  ]
    [VDDIO  | 5     | Left  ]
    [GNDIO  | 6     | Down  ]
    [VDD    | 8     | Left  ]
    [INT2   | 9     | Left  ]
    [NC0    | 10    | Down  ]
    [NC1    | 11    | Down  ]
    [CSB    | 12    | Right ]
    [SCx    | 13    | Right ]
    [SDx    | 14    | Right ]

  make-box-symbol()
  assign-landpattern(BMX160-pkg)

  supports i2c :
    i2c.scl => self.SCx
    i2c.sda => self.SDx

  supports spi-peripheral() :
    spi-peripheral().sck => self.SCx
    spi-peripheral().cs => self.CSB
    spi-peripheral().sdi => self.SDx
    spi-peripheral().sdo => self.SDO

pcb-landpattern BMX160-pkg :
  val dx = 0.675 / 2.0 + 0.925
  val dy = 0.675 / 2.0 + 0.675
  val pitch = 0.5
  pad p[1] : smd-pad(0.675, 0.25) at loc((- dx), (1.5 * pitch))
  pad p[2] : smd-pad(0.675, 0.25) at loc((- dx), (0.5 * pitch))
  pad p[3] : smd-pad(0.675, 0.25) at loc((- dx), ((-0.5) * pitch))
  pad p[4] : smd-pad(0.675, 0.25) at loc((- dx), ((-1.5) * pitch))

  pad p[7] : smd-pad(0.25, 0.675) at loc(pitch, (- dy))
  pad p[6] : smd-pad(0.25, 0.675) at loc(0.0, (- dy))
  pad p[5] : smd-pad(0.25, 0.675) at loc((- pitch), (- dy))

  pad p[11] : smd-pad(0.675, 0.25) at loc(dx, (1.5 * pitch))
  pad p[10] : smd-pad(0.675, 0.25) at loc(dx, (0.5 * pitch))
  pad p[9] : smd-pad(0.675, 0.25) at loc(dx, ((-0.5) * pitch))
  pad p[8] : smd-pad(0.675, 0.25) at loc(dx, ((-1.5) * pitch))

  pad p[12] : smd-pad(0.25, 0.675) at loc(pitch, dy)
  pad p[13] : smd-pad(0.25, 0.675) at loc(0.0, dy)
  pad p[14] : smd-pad(0.25, 0.675) at loc((- pitch), dy)

  layer(Courtyard(Top)) = Rectangle(3.0, 2.5)
  layer(Silkscreen("pol", Top)) = Circle((- dx), dy + 0.5, 0.254)

  ref-label()

public pcb-module module :
  pin gnd
  pin vcc
  port spi : spi-peripheral()
  pin int1
  pin int2

  inst imu : ocdb/components/bosch/BMX160/component

  require some-spi-port : spi-peripheral() from imu
  net (spi, some-spi-port)

  net (gnd, imu.GND)
  net (gnd, imu.GNDIO)
  net (vcc, imu.VDD)
  net (vcc, imu.VDDIO)
  net (int1, imu.INT1)
  net (int2, imu.INT2)
  
  cap-strap(vcc, gnd, 1.0e-6)
  cap-strap(vcc, gnd, 0.1e-6)