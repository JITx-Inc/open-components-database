#use-added-syntax(esir)
defpackage ocdb/amphenol/87520-0010BLF:
  import core
  import collections
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components
  import jitpcb/visualizer

;DESIGN NOTE: PTH Annular Ring Calculation (pth-pad)
;Minimum Hole Size = Maximum Lead Diameter + 0.25mm (for Level A of IPC-2222)
;Minimum Hole Size = Maximum Lead Diameter + 0.20mm (for Level B of IPC-2222)
;Minimum Hole Size = Maximum Lead Diameter + 0.15mm (for Level C of IPC-2222)

;Pad Diameter = Minimum Hole Size + 0.1mm + 0.60mm (for Level A of IPC-2221)
;Pad Diameter = Minimum Hole Size + 0.1mm + 0.50mm (for Level B of IPC-2221)
;Pad Diameter = Minimum Hole Size + 0.1mm + 0.40mm (for Level C of IPC-2221)

;error: Circle needs to be Oval drill-shape
pcb-pad plated-slot-pad (d:Double) :
  type = TH
  shape = Rectangle(d + 0.6, d + 0.6)
  layer(Cutout()) = Circle(d / 2.0)

;Design for IPC-222x Level B
pcb-package usb-87520-0010BLF-package :
  ;calculated 1.2mm min hole size, 1.8mm pad diameter
  pad p[1] : {pth-pad(0.6, 0.9)} at loc(-6.0, -3.5)
  pad p[2] : {pth-pad(0.6, 0.9)} at loc(-6.0, -1.0) 
  pad p[3] : {pth-pad(0.6, 0.9)} at loc(-6.0, 1.0)
  pad p[4] : {pth-pad(0.6, 0.9)} at loc(-6.0, 3.5) 
  ;calculated 2.58mm min hole size
  pad p[5] : {plated-slot-pad(2.58)} at loc(-3.29, -6.55)
  pad p[6] : {plated-slot-pad(2.58)} at loc(-3.29, 6.55) 

  layer(Courtyard(Top)) = Rectangle(14.0, 13.1)
  layer(Silkscreen("values", Top)) = Text(">REF", 0.7, C, loc(0.0, 0.0))
  ;height = 7.1mm

public unique pcb-component component :
  manufacturer = "Amphenol ICC (FCI)"
  mpn = "87520-0010BLF"
  description = "USB-A (USB TYPE-A) USB 2.0 Receptacle Connector 4 Position Through Hole, Right Angle"
  reference-prefix = "J"
  val ps = PinSpec $ #TABLE :
    [Ref | Int ...   | Dir  ]
    [vbus     | 1    | Left  ]
    [dm       | 2    | Left  ]
    [dp       | 3    | Left  ]
    [gnd      | 4    | Left  ]
    [shield1  | 5    | Right ]
    [shield2  | 6    | Right ]
  gen-symbol-map(ps, usb-87520-0010BLF-package)
  view(usb-87520-0010BLF-package)

public pcb-module usb_conn_typea_module :

  inst usb_conn_typea : {ocdb/amphenol/87520-0010BLF/component}

  net VBUS (usb_conn_typea.vbus)
  net DM (usb_conn_typea.dm)
  net DP (usb_conn_typea.dp)
  net GND (usb_conn_typea.gnd)
  net SHIELD (usb_conn_typea.shield1, usb_conn_typea.shield2)
  