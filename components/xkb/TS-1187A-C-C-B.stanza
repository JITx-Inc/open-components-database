#use-added-syntax(jitx)
defpackage ocdb/components/xkb/TS-1187A-C-C-B :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults
  import ocdb/utils/bundles
  import ocdb/utils/landpatterns
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol

pcb-landpattern TS-1187A-C-C-B-pkg :
  pad p[1] : smd-pad(1.0, 0.7) at loc(-3.0, 1.85)
  pad p[2] : smd-pad(1.0, 0.7) at loc(3.0, 1.85)
  pad p[3] : smd-pad(1.0, 0.7) at loc(-3.0, -1.85)
  pad p[4] : smd-pad(1.0, 0.7) at loc(3.0, -1.85)
  layer(Courtyard(Top)) = Rectangle(7.0, 5.1)
  layer(Silkscreen("pol", Top)) = Circle(-3.75, 1.85, 0.1)
  layer(Silkscreen("f-silk", Top)) =  Line(0.127, [ Point(-2.55, 2.55), Point(2.55, 2.55) ])
  layer(Silkscreen("f-silk", Top)) =  Line(0.127, [ Point(-2.55, -2.55), Point(2.55, -2.55) ])
  ref-label()

public pcb-component xkb-TS-1187A-C-C-B :
  pin-properties :
    [pin:Ref | pads:Int ... ]
    [p | 1 2 ]
    [t | 3 4 ]
  make-box-symbol()
  assign-landpattern(TS-1187A-C-C-B-pkg)

  manufacturer = "XKB Enterprise"
  mpn = "TS-1187A-C-C-B"
  description = "SPST Vertical Round Button 260gf SMD,5.1x5.1x1.7mm Tactile Switches"


public pcb-module xkb-TS-1187A-C-C-B-debounced-module :
  pin vdd
  pin gnd
  pin signal

  ;create the button
  public inst button : xkb-TS-1187A-C-C-B

  ;set values for the debounce RC
  val r1-val = 10.0e3
  val r2-val = 10.0e3
  val c1-val = 1.0e-6

  inst r1 : database-part(["category" => "resistor", "resistance" => r1-val, "case" => ["0402"], "_exist" => ["vendor_part_numbers.lcsc"]])
  inst r2 : database-part(["category" => "resistor", "resistance" => r2-val, "case" => ["0402"], "_exist" => ["vendor_part_numbers.lcsc"]])
  inst c1 : database-part(["category" => "capacitor", "capacitance" => 1.0e-6, "_exist" => ["vendor_part_numbers.lcsc"]]) 

  val rc-time-constant = ((r1-val + r2-val) * c1-val)
  println("rc-time-constant is %_" % [rc-time-constant])

  net (vdd r1.p[1])
  net (button.t r1.p[2] r2.p[1])
  net (r2.p[2] c1.p[1] signal)
  net (c1.p[2] gnd)
  net (button.p gnd)