#use-added-syntax(jitx)
defpackage ocdb/components/st-microelectronics/generate-ioc:
  import core
  import collections
  import jitx
  import jitx/commands

  import ocdb/utils/checks
  import ocdb/utils/generic-components

  import jitx/powergen/gen

  import ocdb/utils/design-vars
  import ocdb/utils/property-structs
  import ocdb/utils/bundles

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMGPIOModes :
  STMGPIOOutput   ; GPIO_Output
  STMGPIOInput    ; GPIO_Input
  STMGPIOAnalog   ; GPIO_Analog
  STMGPIOEventOut ; GPIO_EVENTOUT
  STMGPIOExtI     ; GPXTI

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMGPIOSpeed :
  STMGPIOSpeedLow      ; nothing needs to be added to the ioc file?
  STMGPIOSpeedMedium   ; GPIO_SPEED_FREQ_MEDIUM
  STMGPIOSpeedHigh     ; GPIO_SPEED_FREQ_HIGH
  STMGPIOSpeedVeryHigh ; GPIO_SPEED_FREQ_VERY_HIGH

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMGPIOPuPd :
  STMGPIOPullUp        ; Pull up
  STMGPIOPullDown      ; Pull down (not for open drain IOs)
  STMGPIONoPull        ; No pull up or pull down

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMUSARTMode :
  STMUSARTSynchronous
  STMUSARTAsynchronous
  STMUSARTSingleWire
  STMUSARTMultiprocessor
  STMUSARTIRDA
  STMUSARTSmartcard
  STMUSARTSmartcardWClk
  STMUSARTLIN

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMSPIType :
  STMSPIFullDuplexController
  STMSPIFullDuplexPeripheral
  STMSPIHalfDuplexController
  STMSPIHalfDuplexPeripheral
  STMSPIRxController
  STMSPIRxPeripheral
  STMSPITxController
  STMSPITxPeripheral

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STMPinMode :
  STMI2CMode
  STMSPIMode
  STMSerialWireMode
  STMSerialWireDebugMode
  STMHSEExternalMode
  STMLSEExternalMode

public pcb-enum ocdb/components/st-microelectronics/generate-ioc/STM32IPs :
  STMI2C1IP   ; I2C1
  STMI2C2IP   ; I2C2
  STMCAN1IP   ; CAN1
  STMCAN2IP   ; CAN2
  STMLPUART1IP; LPUART1
  STMUSART1IP ; USART1
  STMUSART2IP ; USART2
  STMUSART3IP ; USART3
  STMUART4IP  ; UART4
  STMUART5IP  ; UART5
  STMUSART6IP ; USART6
  STMRCCIP    ; RCC - default
  STMRTCIP    ; RTC - when adding 32kHz xtal?
  STMSPI1IP   ; SPI1
  STMSPI2IP   ; SPI2
  STMSPI3IP   ; SPI3
  STMSYSIP    ; SYS - default
  STMNVICIP   ; NVIC - default
  STMADCIP    ; ADC
  STMADC1IP   ; ADC1
  STMADC2IP   ; ADC2
  STMADC3IP   ; ADC3
  STMDACIP    ; DAC

defn ioc-file-header () -> String :
  \<header-body>#MicroXplorer Configuration settings - do not modify
File.Version=6
board=custom
<header-body>

defn default-nvic-settings () -> String :
  \<nvic-body>NVIC.ForceEnableDMAVector=true
NVIC.HardFault_IRQn=true\:0\:0\:false\:false\:true\:false\:false
NVIC.NonMaskableInt_IRQn=true\:0\:0\:false\:false\:true\:false\:false
NVIC.PendSV_IRQn=true\:0\:0\:false\:false\:true\:false\:false
NVIC.SVC_IRQn=true\:0\:0\:false\:false\:true\:false\:false
NVIC.SysTick_IRQn=true\:0\:0\:false\:false\:true\:false\:true
<nvic-body>

defn valid-instance (i : JITXObject) :
  match(mpn?(i): String) :
    if prefix?(mpn?(i) as String, "STM32") :
      true
  else :
    false

defn stm32-ip-name (ip : STM32IPs) -> String :
  switch(ip) :
    STMI2C1IP   : "I2C1" ; I2C1
    STMI2C2IP   : "I2C2" ; I2C2
    STMCAN1IP   : "CAN1" ; CAN1
    STMCAN2IP   : "CAN2" ; CAN2
    STMLPUART1IP: "LPUART1" ; LPUART1
    STMUSART1IP : "USART1" ; USART1
    STMUSART2IP : "USART2" ; USART2
    STMUSART3IP : "USART3" ; USART3
    STMUART4IP  : "UART4" ; UART4
    STMUART5IP  : "UART5" ; UART5
    STMUSART6IP : "USART6" ; USART6
    STMRCCIP    : "RCC" ; RCC - default
    STMRTCIP    : "RTC" ; RTC - when adding 32kHz xtal?
    STMSPI1IP   : "SPI1" ; SPI1
    STMSPI2IP   : "SPI2" ; SPI2
    STMSPI3IP   : "SPI3" ; SPI3
    STMSYSIP    : "SYS" ; SYS - default
    STMNVICIP   : "NVIC" ; NVIC - default
    STMADCIP    : "ADC" ; ADC
    STMADC1IP   : "ADC1" ; ADC1
    STMADC2IP   : "ADC2" ; ADC2
    STMADC3IP   : "ADC3" ; ADC3
    STMDACIP    : "DAC" ; DAC


defn stm32-spi-type (mode : STMSPIType) -> String :
  switch(mode) :
    STMSPIFullDuplexController : "Full_Duplex_Master"
    STMSPIFullDuplexPeripheral : "Full_Duplex_Slave"
    STMSPIHalfDuplexController : "Simplex_Bidirectional_Master"
    STMSPIHalfDuplexPeripheral : "Simplex_Bidirectional_Slave"
    STMSPIRxController :         "RX_Only_Simplex_Unidirect_Master"
    STMSPIRxPeripheral :         "RX_Only_Simplex_Unidirect_Slave"
    STMSPITxController :         "TX_Only_Simplex_Unidirect_Master"
    STMSPITxPeripheral :         "TX_Only_Simplex_Unidirect_Slave"

defn stm32-usart-mode (mode : STMUSARTMode) -> String :
  switch(mode) :
    STMUSARTSynchronous :    "Synchronous"
    STMUSARTAsynchronous :   "Asynchronous"
    STMUSARTSingleWire :     "Half_duplex(single_wire_mode)"
    STMUSARTMultiprocessor : "Multiprocessor_communication"
    STMUSARTIRDA :           "IrDA"
    STMUSARTSmartcard :      "SmartCard"
    STMUSARTSmartcardWClk :  "SmartCard_With_Clock"
    STMUSARTLIN :            "LIN"

defn stm32-gpio-mode (mode : STMGPIOModes) -> String :
  switch(mode) :
    STMGPIOOutput :   "GPIO_Output"
    STMGPIOInput :    "GPIO_Input"
    STMGPIOAnalog :   "GPIO_Analog"
    STMGPIOEventOut : "EVENTOUT"
    STMGPIOExtI :     "GPXTI\n# can't currently write EXTI pins correctly."

defn stm32-io-speed (speed : STMGPIOSpeed) -> String :
  switch(speed) :
    STMGPIOSpeedLow :      "GPIO_SPEED_FREQ_LOW"
    STMGPIOSpeedMedium :   "GPIO_SPEED_FREQ_MEDIUM"
    STMGPIOSpeedHigh :     "GPIO_SPEED_FREQ_HIGH"
    STMGPIOSpeedVeryHigh : "GPIO_SPEED_FREQ_VERY_HIGH"

defn stm32-pupd (mode : STMGPIOPuPd) -> String :
  switch(mode) :
    STMGPIOPullUp :   "GPIO_PULLUP"
    STMGPIOPullDown : "GPIO_PULLDOWN"
    STMGPIONoPull :   "GPIO_NOPULL"

defn compose-ioc-contents (i : JITXObject) :
  val ip-template = "Mcu.IP%_=%_\n"
  val pin-template = "Mcu.Pin%_=%_\n"
  var content : String = ""
  var pin-content : String = ""
  var pin-params : String = ""
  var ip-content : String = ""
  val ip-names = HashSet<STM32IPs>()
  var exist-params? = false
  inside pcb-module :
    if has-property?(i.stm32-series) :
      content = append(content, to-string("Mcu.Family=%_\n" % [property(i.stm32-series)]))
    if has-property?(i.stm32-refname) :
      content = append(content, to-string("Mcu.Name=%_\n" % [property(i.stm32-refname)]))
    if has-property?(i.stm32-mfg-package) :
      content = append(content, to-string("Mcu.Package=%_\n" % [property(i.stm32-mfg-package)]))
    var index:Int = 0
    add(ip-names, STMRCCIP)        
    add(ip-names, STMSYSIP)        
    add(ip-names, STMNVICIP)        
    ; let's list IPs now
    for p in pins(i) do :
      if has-property?(p.stm32-ip-name) :
        add(ip-names, property(p.stm32-ip-name))
    for (ip in ip-names, i in 0 to false) do :
      ip-content = append(ip-content, to-string("Mcu.IP%_=%_\n" % [i, stm32-ip-name(ip)]))
    ip-content = append(ip-content, to-string("Mcu.IPNb=%_\n" % [length(ip-names)]))
    content = append(content, ip-content)
    content = append(content, default-nvic-settings())
    for p in pins(i) do :
      if not has-property?(p.power-pin) :
        if has-property?(p.stm32-pin-name) :
          if prefix?(property(p.stm32-pin-name), "P") :
            if connected?(p) :
              content = append(content, to-string(pin-template % [index, property(p.stm32-pin-name)]))
              index = index + 1
    ; if not using a timer for the timebase source then the following is included
    content = append(content, to-string("Mcu.Pin%_=VP_SYS_VS_Systick\n" % [index])) ; this is some kind of default "pin" and always defined
    index = index + 1
    content = append(content, to-string("Mcu.PinsNb=%_\n" % [index]))
    ; if using a timer for the timebase then the statement should be changed to VP_SYS_VS_timX where X is the timer being utilized
    ; don't know how to capture that yet so we default to the SysTick source
    ; some extra pins if we are using the RTC + Calendar include the following:
    ; Mcu.PinXX=VP_RTC_VS_RTC_Activate
    ; Mcu.PinXX=VP_RTC_VS_RTC_Calendar
    ; not sure how to include them either
    for p in pins(i) do :
      if not has-property?(p.power-pin) :
        if has-property?(p.stm32-pin-name) :
          if prefix?(property(p.stm32-pin-name), "P") :
            if connected?(p) :
              pin-content = ""
              pin-params = ""
              exist-params? = false
              pin-params = append(pin-params, to-string("%_.GPIOParameters=" % [property(p.stm32-pin-name)]))
              if has-property?(p.stm32-gpio-iomode) :
                pin-content = append(pin-content, to-string("%_.Signal=%_\n" % [property(p.stm32-pin-name), stm32-gpio-mode(property(p.stm32-gpio-iomode))]))
              else if has-property?(p.stm32-signal-name) :
                pin-content = append(pin-content, to-string("%_.Signal=%_\n" % [property(p.stm32-pin-name), property(p.stm32-signal-name)]))
                ; special case of signals with names and also used as ADC
                if has-property?(p.stm32-adc-mode) :
                  pin-content = append(pin-content, to-string("%_.Mode=%_\n" % [property(p.stm32-pin-name), property(p.stm32-adc-mode)]))
              if has-property?(p.stm32-gpio-speed) :
                if exist-params? :
                  pin-params = append(pin-params, ",")
                else :
                  exist-params? = true
                pin-params = append(pin-params, "GPIO_Speed")
                pin-content = append(pin-content, to-string("%_.GPIO_Speed=%_\n" % [property(p.stm32-pin-name), stm32-io-speed(property(p.stm32-gpio-speed))]))
              if has-property?(p.stm32-gpio-pupd) :
                if exist-params? :
                  pin-params = append(pin-params, ",")
                else :
                  exist-params? = true
                pin-params = append(pin-params, "GPIO_PuPd")
                pin-content = append(pin-content, to-string("%_.GPIO_PuPd=%_\n" % [property(p.stm32-pin-name), stm32-pupd(property(p.stm32-gpio-pupd))]))
                pin-content = append(pin-content, to-string("%_.Locked=true\n" % [property(p.stm32-pin-name)]))
              if has-property?(p.stm32-gpio-label) :
                if exist-params? :
                  pin-params = append(pin-params, ",")
                else :
                  exist-params? = true
                pin-params = append(pin-params, "GPIO_Label")
                pin-content = append(pin-content, to-string("%_.GPIO_Label=%_\n" % [property(p.stm32-pin-name), property(p.stm32-gpio-label)]))
              if has-property?(p.stm32-pin-mode) :
                switch(property(p.stm32-pin-mode)) :
                  STMI2CMode :
                    pin-content = append(pin-content, to-string("%_.Mode=I2C\n" % [property(p.stm32-pin-name)]))
                  STMSPIMode :
                    if has-property?(p.stm32-spi-mode) :
                      pin-content = append(pin-content, to-string("%_.Mode=%_\n" % [property(p.stm32-pin-name), stm32-spi-type(property(p.stm32-spi-mode))]))
                  STMSerialWireMode :
                    if has-property?(p.stm32-uxart-mode) :
                      pin-content = append(pin-content, to-string("%_.Mode=%_\n" % [property(p.stm32-pin-name), stm32-usart-mode(property(p.stm32-uxart-mode))]))
                    else :
                      pin-content = append(pin-content, to-string("# No SerialWire mode defined on %_\n" % [property(p.stm32-pin-name)]))
                  STMSerialWireDebugMode :
                    pin-content = append(pin-content, to-string("%_.Mode=SerialWire\n" % [property(p.stm32-pin-name)]))
                  STMHSEExternalMode :
                    pin-content = append(pin-content, to-string("%_.Mode=HSE-External-Oscillator\n" % [property(p.stm32-pin-name)]))
                  STMLSEExternalMode :
                    pin-content = append(pin-content, to-string("%_.Mode=LSE-External-Oscillator\n" % [property(p.stm32-pin-name)]))
              if has-property?(p.stm32-pin-locked) :
                if property(p.stm32-pin-locked) :
                  pin-content = append(pin-content, to-string("%_.Locked=true\n" % [property(p.stm32-pin-name)]))

              ; output data for a pin now
              if exist-params? :
                content = append(content, to-string("%_\n" % [pin-params]))
              if not empty?(pin-content) :
                content = append(content, pin-content)
    content

public defn generate-ioc-file (filename : String):
  inside pcb-module :
    for i in component-instances(self) do :
      if valid-instance(i) :
        if file-exists?(filename) :
          println("Overwriting file %_" % [filename])
        val fos = FileOutputStream(filename, false) ; overwrite file
        print(fos, ioc-file-header())
        print(fos, compose-ioc-contents(i))
        close(fos)

public defn generate-ioc-file ():
  generate-ioc-file("default-project-output.ioc")


; PG15.GPIOParameters=GPIO_PuPd
; PG15.GPIO_PuPd=GPIO_PULLDOWN
; PG15.Locked=true
; PG15.Signal=GPIO_Input

; PG14.GPIOParameters=GPIO_Speed,PinState,GPIO_PuPd,GPIO_ModeDefaultOutputPP
; PG14.GPIO_ModeDefaultOutputPP=GPIO_MODE_OUTPUT_OD
; PG14.GPIO_PuPd=GPIO_PULLUP
; PG14.GPIO_Speed=GPIO_SPEED_FREQ_VERY_HIGH
; PG14.Locked=true
; PG14.PinState=GPIO_PIN_SET
; PG14.Signal=GPIO_Output

; PF1.GPIOParameters=GPIO_PuPd,GPIO_Label,GPIO_ModeDefaultOutputPP
; PF1.GPIO_Label=OutPut3
; PF1.GPIO_ModeDefaultOutputPP=GPIO_MODE_OUTPUT_OD
; PF1.GPIO_PuPd=GPIO_NOPULL
; PF1.Signal=GPIO_Output

; PD1.GPIOParameters=GPIO_PuPd
; PD1.GPIO_PuPd=GPIO_PULLUP
; PD1.Locked=true
; PD1.Signal=GPIO_Input

; PC2.Mode=Full_Duplex_Slave
; PC2.Signal=SPI2_MISO
; PC3.Mode=Full_Duplex_Slave
; PC3.Signal=SPI2_MOSI
; PB12.Mode=NSS_Signal_Hard_Input
; PB12.Signal=SPI2_NSS
; PB13.Mode=Full_Duplex_Slave
; PB13.Signal=SPI2_SCK

; PC10.Mode=TX_Only_Simplex_Unidirect_Master
; PC10.Signal=SPI3_SCK
; PC12.Mode=TX_Only_Simplex_Unidirect_Master
; PC12.Signal=SPI3_MOSI

; PA4.Mode=NSS_Signal_Hard_Output
; PA4.Signal=SPI1_NSS
; PA5.Mode=Full_Duplex_Master
; PA5.Signal=SPI1_SCK
; PA6.Mode=Full_Duplex_Master
; PA6.Signal=SPI1_MISO
; PA7.Mode=Full_Duplex_Master
; PA7.Signal=SPI1_MOSI

; PC14-OSC32_IN.Locked=true
; PC14-OSC32_IN.Mode=LSE-External-Oscillator
; PC14-OSC32_IN.Signal=RCC_OSC32_IN
; PC15-OSC32_OUT.Locked=true
; PC15-OSC32_OUT.Mode=LSE-External-Oscillator
; PC15-OSC32_OUT.Signal=RCC_OSC32_OUT
