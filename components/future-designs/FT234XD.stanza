#use-added-syntax(jitx)
defpackage ocdb/components/future-designs/FT234XD :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/utils/defaults

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

pcb-landpattern ft234xd-dfn:
  make-dfn-land-pattern(12
                        0.45
                        tol(3.0, 0.0)
                        tol(3.0, 0.0)
                        min-typ-max(0.3, 0.40, 0.5)
                        min-typ-max(0.18, 0.25, 0.28)
                        Rectangle(1.65, 2.55))

public pcb-component component :
  manufacturer = "FTDI"
  mpn = "FT234XD"
  description = "USB Bridge, USB to UART USB 2.0 UART Interface"

  port uart : uart([UART-TX UART-RX UART-CTS UART-RTS])
  port usb : usb-2-data
  pin-properties :
    [pin:Ref | pads:Int ... | side:Dir ]
    [usb.N    | 1           | Left     ]
    [nRESET   | 2           | Left     ]
    [VOUT3V3  | 3           | Right    ]
    [VCC      | 4           | Right    ]
    [GND      | 5, 13       | Right    ]
    [CBUS0    | 6           | Left     ]
    [uart.tx  | 7           | Right    ]
    [uart.rts | 8           | Right    ]
    [VCCIO    | 9           | Right    ]
    [uart.rx  | 10          | Left     ]
    [uart.cts | 11          | Left     ]
    [usb.P    | 12          | Left     ]


  make-box-symbol()
  assign-landpattern(ft234xd-dfn)

public pcb-module module :
  inst bridge : ocdb/components/future-designs/FT234XD/component
  pin vcc
  pin gnd
  pin v-usb
  pin reset
  pin cts
  pin rts
  pin cbus0
  
  port uart-node : uart([UART-TX UART-RX UART-CTS UART-RTS])
  port usb-data : usb-2-data

  inst ferrite : ocdb/components/tdk/MPZ1608/component(470.0)

  net (v-usb, ferrite.p[1])
  net (ferrite.p[2], bridge.VCC)
  net (vcc, bridge.VCCIO)
  net (gnd, bridge.GND)
  net (reset, bridge.nRESET)
  net (cbus0, bridge.CBUS0)
  net (usb-data, bridge.usb)
  net (uart-node, bridge.uart)

  bypass-cap-strap(bridge.VCC, gnd, 4.7e-6)
  bypass-cap-strap(bridge.VCC, gnd, 100.0e-9)
  bypass-cap-strap(bridge.VCC, gnd, 100.0e-9)
  bypass-cap-strap(bridge.VOUT3V3, gnd, 100.0e-9)
