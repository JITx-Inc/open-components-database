#use-added-syntax(jitx)
defpackage ocdb/defaults :
  import core
  import collections
  import jitx
  import jitx/commands

;============================================================
;=================== Default Materials ======================
;============================================================

public pcb-material copper-default :
  type = Conductor
  name = "Copper"
  
public pcb-material core-default :
  type = Dielectric
  dielectric-coefficient = 4.26
  name = "PR4 Core"

public pcb-material prepreg-default :
  type = Dielectric
  dielectric-coefficient = 4.26
  name = "PR4 Prepreg 2113/2116"

public pcb-material soldermask-default :
  type = Dielectric
  dielectric-coefficient = 3.9
  name = "Taiyo BSN4000"

;============================================================
;============== Default Stackup, Rules, Board ===============
;============================================================

public pcb-stackup default-stackup (num-layers:Int) :
  name = "Default stackup"
  layer(2.03e-2, soldermask-default)
  for i in 0 to num-layers do :
    layer(1.498e-1, prepreg-default) when i > 0
    layer(5.08e-2, copper-default)  
  layer(2.03e-2, soldermask-default)   

public pcb-rules default-rules :
  min-copper-width = 0.13 ; 5mil
  min-copper-copper-space = 0.2 ; 
  min-copper-hole-space = 0.2032 ; 8mil
  min-copper-edge-space = 0.381 ; 10mil outer, but 15mil inner
  min-annular-ring = 0.1524 ; 6mil
  min-drill-diameter = 0.254 ; 10mil
  min-silkscreen-width = 0.0762 ; 3mil
  min-pitch-leaded = 0.35 ; guess
  min-pitch-bga = 0.35 ; guess
  max-board-width = 457.2 ; 18in
  max-board-height = 609.6 ; 24in

public pcb-board default-board (num-layers:Int, outline:Rectangle) :
  stackup = default-stackup(num-layers)
  boundary = outline
  signal-boundary = outline

public defn make-default-board (module:Instantiable, num-layers:Int, outline:Rectangle) :
  val brd-outline = outline 

  pcb-board B :
    stackup = default-stackup(num-layers)
    boundary = brd-outline 
    signal-boundary = brd-outline

  set-main-module(module)
  set-board(B)
  set-rules(ocdb/rules/bay-area-circuits-conservative-rules)
  run-checks("checks.txt")

;============================================================
;================= Single Module Design =====================
;============================================================

public defn make-single-module-design (module:Instantiable, board:Board) :
  pcb-module main-module :
    inst instance : module
    place(instance) at loc(0.0,0.0) on Top

  set-main-module(module)
  set-board(board)
  set-rules(default-rules)

public defn make-single-module-design (module:Instantiable) :
  make-single-module-design(module, default-board(4, Rectangle(10.0, 10.0)))
