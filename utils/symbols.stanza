#use-added-syntax(esir)
defpackage ocdb/symbols :
  import core
  import collections
  import math
  import esir
  import esir/gen
  import esir/utils

  import ocdb/symbol-utils

; ====== Supply and Ground =====================================================

pcb-symbol supply-sym :
  pin p[0] at unit-point(0.0, 0.0)

  unit-line([[0.0, 0.0], [0.0, 0.25]])
  unit-triangle([0.0, 0.25], [0.0, 0.75], 0.5)

  unit-val([-1.0, 1.5])

pcb-symbol ground-sym :
  pin p[0] at unit-point(0.0, 0.0)
  
  unit-line([[0.0, 0.0], [0.0, -0.5]])
  unit-line(0.1, [[-0.5, -0.5], [0.5, -0.5]])
  unit-line(0.1, [[-0.3, -0.7], [0.3, -0.7]])
  unit-line(0.1, [[-0.1, -0.9], [0.1, -0.9]])

  unit-val([-1.0, -1.5])

; ====== Discrete Passives (Resistors, Capacitors, Inductors) ==================

public pcb-symbol resistor-sym (r-type:String|False) :
  pin p[1] at unit-point(0.0, -1.0)
  pin p[2] at unit-point(0.0, 1.0)
  
  unit-line([[0.0, -1.0], [0.0, -0.625]])
  unit-line(0.1, [
    [0.0, -0.625]
    [-0.3, -0.5]
    [0.3, -0.25]
    [-0.3, 0.0]
    [0.3, 0.25]
    [-0.3, 0.5]
    [0.0, 0.625]
  ])
  unit-line([[0.0, 0.625], [0.0, 1.0]])

  defn var-arrow () :
    unit-line([[-0.6, -0.8], [0.6,0.8]])
    unit-triangle([0.45, 0.6], [0.6, 0.8], 0.3)

  match(r-type) :
    (s:String) :
      switch(s) :
        "variable" :
          var-arrow()
        "pot" :
          pin p[3] at unit-point(-1.0, 0.0)
          unit-line([[-1.0, 0.0], [-0.6, 0.0]])
          unit-triangle([-0.6, 0.0], [-0.3, 0.0], 0.3)
        "photo" :
          draw-opto("detector")
        "therm" :
          var-arrow()
          unit-text([-0.75, 0.0],"T")
        else : fatal("Incorrect resistor type %_." % [s])
    (o) : false

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public pcb-symbol capacitor-sym (pol?:True|False) :
  pin p[1] at unit-point(0.0, 1.0)
  pin p[2] at unit-point(0.0, -1.0)
  
  unit-line([[0.0, 1.0], [0.0, 0.2]])
  unit-line(0.1, [[-0.5, 0.2], [0.5, 0.2]])
  unit-line([[0.0, -0.2], [0.0, -1.0]])

  if pol? :
    unit-text([0.7, 0.7], "+")
    val a = asin(0.5 / 0.8)
    unit-approx-arc([0.0, -1.0], 0.8, PI / 2.0 + a, PI / 2.0 - a, 0.1)
  else :
    unit-line(0.1, [[-0.5, -0.2], [0.5, -0.2]])
    
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])
  
public defn capacitor-sym () :
  capacitor-sym(false)

public pcb-symbol inductor-sym :
  pin p[1] at unit-point(0.0, -1.0)
  pin p[2] at unit-point(0.0, 1.0)

  unit-line([[0.0, -1.0], [0.0, -0.75]])
  unit-line([[0.0, 0.75], [0.0, 1.0]])
  
  for i in 0 to 3 do :
    unit-approx-arc([0.0, 0.5 - 0.5 * to-double(i)], 0.25, 3.0 * PI / 2.0, PI / 2.0, 0.1)
  
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

; ====== Fuses =================================================================

pcb-symbol fuse-sym :
  pin p[0] at unit-point(0.0, 1.0)
  pin p[1] at unit-point(0.0, -1.0)

  unit-line([[0.0, 1.0], [0.0, 0.75]])
  unit-line([[0.0, -1.0], [0.0, -0.75]])

  unit-rectangle(0.8, 1.5)

  unit-line([[-0.4, 0.5], [0.4, 0.5]])
  unit-line([[-0.4, -0.5], [0.4, -0.5]])

  unit-approx-arc([0.0, 0.25], 0.25, 3.0 * PI / 2.0, PI / 2.0)
  unit-approx-arc([0.0, -0.25], 0.25, (- PI) / 2.0, PI / 2.0)

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

; ====== Crystals, Oscillators =================================================

public pcb-symbol crystal-sym (n-gnd:Int) :
  pin p[1] at unit-point(-1.0, 0.0)
  pin p[2] at unit-point(1.0, 0.0)

  unit-line([[-1.0, 0.0], [-0.5, 0.0]])
  unit-line(0.1, [[-0.5, -0.5], [-0.5, 0.5]])
  unit-rectangle(0.5, 1.0)
  unit-line(0.1, [[0.5, -0.5], [0.5, 0.5]])
  unit-line([[0.5, 0.0], [1.0, 0.0]])

  val gnd-locs = [[-1.0, 0.0], [-1.0, -1.0]]

  if n-gnd > 0 :
    unit-line(0.1, [[0.5, -1.0], [-0.5, -1.0]])
    unit-line([[0.0, -1.0], [0.0, -2.0]])
    pin gnd at unit-point(0.0, -2.0)

  unit-ref([1.0, 0.5])
  unit-val([1.2, 0.2])

; ====== Discrete Semiconductors (Diodes, Transistors) =========================

; TODO: Factor diode shape to allow scaled translation and reuse

public pcb-symbol diode-sym (d-type:String|False) :
  pin a at unit-point(0.0, 1.0)
  pin c at unit-point(0.0, -1.0)

  unit-line([[0.0, 1.0], [0.0, 0.4]])
  unit-triangle([0.0, 0.4], [0.0, -0.4], 1.0)
  unit-line([[0.0, -0.4], [0.0, -1.0]])
  unit-line([[-0.5, -0.4], [0.5, -0.4]])  
  
  match(d-type) :
    (s:String) :
      switch(s) :
        "led" : draw-opto("emitter")
        "photo" : draw-opto("detector")
        "zener" :
          unit-line([[-0.7, -0.2], [-0.5, -0.4]])
          unit-line([[0.5, -0.4], [0.7, -0.6]])
        "schottky" :
          unit-line([[-0.5, -0.4], [-0.5, -0.15], [-0.3, -0.15]])
          unit-line([[0.5, -0.4], [0.5, -0.65], [0.3, -0.65]])
        else : fatal("Incorrect diode symbol type %_." % [d-type])
    (o) : false

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public pcb-symbol diode-tvs :
  pin a at unit-point(0.0, 1.5)
  pin c at unit-point(0.0, -1.5)
  val ytop = 0.3
  val ybot = (- ytop)
  
  ; draw lower diode first
  unit-triangle([0.0, ybot], [0.0, (ybot - 0.8)], 1.0)
  unit-line([[0.0,(ybot - 0.8)], [0.0, -1.5]])
  unit-line([[-0.5, (ybot - 0.8)], [0.5, (ybot - 0.8)]])
  unit-line([[-0.7, (ybot - 1.0)], [-0.5, (ybot - 0.8)]])
  unit-line([[0.5, (ybot - 0.8)], [0.7, (ybot - 0.6)]])  

  ; now draw second one flipped above
  unit-triangle([0.0, ytop], [0.0, (ytop + 0.8)], 1.0)
  unit-line([[0.0,(ytop + 0.8)], [0.0, 1.5]])
  unit-line([[-0.5, (ytop + 0.8)], [0.5, (ytop + 0.8)]])
  unit-line([[-0.7, (ytop + 0.6)], [-0.5, (ytop + 0.8)]])
  unit-line([[0.5, (ytop + 0.8)], [0.7, (ytop + 1.0)]]) 

  ; connecting line
  unit-line([[0.0, ybot], [0.0, ytop]])

  unit-ref([1.0, 1.0])
  unit-val([1.0, -1.0])

public pcb-symbol diode-rgb-led :
  val xr = (- 2.0)
  val xg = 0.0
  val xb = 2.0
  val ybot = 0.4
  
  pin a at unit-point(0.0, 2.0)
  pin r at unit-point(xr, -1.5)
  pin g at unit-point(xg, -1.5)
  pin b at unit-point(xb, -1.5)

  ; draw three diode symbols next to each other along the bottom
  ; red
  unit-triangle([xr, ybot], [xr, (ybot - 0.8)], 1.0)
  unit-line([[xr,(ybot - 0.8)], [xr, -1.5]])
  unit-line([[xr - 0.5, (ybot - 0.8)], [xr + 0.5, (ybot - 0.8)]])
  draw-opto("emitter", [xr, 0.0])

  ; green 
  unit-triangle([xg, ybot], [xg, (ybot - 0.8)], 1.0)
  unit-line([[xg,(ybot - 0.8)], [xg, -1.5]])
  unit-line([[xg - 0.5, (ybot - 0.8)], [xg + 0.5, (ybot - 0.8)]])
  draw-opto("emitter", [xg, 0.0])

  ; blue 
  unit-triangle([xb, ybot], [xb, (ybot - 0.8)], 1.0)
  unit-line([[xb,(ybot - 0.8)], [xb, -1.5]])
  unit-line([[xb - 0.5, (ybot - 0.8)], [xb + 0.5, (ybot - 0.8)]])
  draw-opto("emitter", [xb, 0.0])

  ; add remaining lines
  unit-line([[xr, ybot], [xr, ybot + 0.5]])
  unit-line([[xg, ybot], [xg, 2.0]])
  unit-line([[xb, ybot], [xb, ybot + 0.5]])
  unit-line([[xr, ybot + 0.5], [xb, ybot + 0.5]])

  unit-ref([1.0, 2.0])
  unit-val([1.0, 1.5])
  
public pcb-symbol bjt-sym (bjt-type:String) :
  pin e at unit-point(1.0, -1.0)
  pin c at unit-point(1.0, 1.0)
  pin b at unit-point(-1.0, 0.0)

  unit-line([[-1.0, 0.0],[-0.75, 0.0]])
  unit-line(0.1, [[-0.75, 0.0], [-0.25, 0.0]])

  unit-line(0.1, [[-0.25, 0.75], [-0.25, -0.75]])
  
  unit-line(0.1, [[-0.25, 0.375], [0.75, 0.875]])
  unit-line([[0.75, 0.875], [1.0, 1.0]])
  
  unit-line(0.1, [[-0.25, -0.375], [0.75, -0.875]])
  unit-line([[0.75, -0.875], [1.0, -1.0]])

  switch(bjt-type) :
    "npn" :
      unit-triangle([0.0, -0.5], [0.5, -0.75], 0.3)
    "pnp" :
      unit-triangle([0.5, 0.75], [0.0, 0.5], 0.3)
    else:
      fatal("Incorrect BJT type %_.", % [bjt-type])

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn bjt-sym () :
  bjt-sym("npn")

public pcb-symbol fet-sym (type:String, mode:String, diode?:True|False, bulk?:True|False) :
  
  val n-type? = switch(type) :
    "n-type" :
      unit-line([[0.0, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [-0.5, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, -0.5]])
      true
    "p-type" :
      unit-line([[-0.5, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [0.0, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, 0.5]])
      false
    else :
      fatal("Incorrect FET channel type %_." % [type])

  pin g at unit-point(-1.0, 0.0) with:
    direction = Left
    length = 0.0
    number-size = 1.0
  
  val [s-y, s-dir, d-y, d-dir] = if n-type? :
    [-1.0, Down, 1.0, Up]
  else :
    [1.0, Up, -1.0, Down]

  pin s at unit-point(0.0, s-y) with:
    direction = s-dir
    length = 0.0
    number-size = 1.0
  
  pin d at unit-point(0.0, d-y) with:
    direction = d-dir
    length = 0.0
    number-size = 1.0

  if bulk? :
    pin bulk at unit-point(1.0, 0.0)
    unit-line([[0.0, 0.0], [1.0, 0.0]])

  unit-line([[0.0, 1.0], [0.0, 0.5], [-0.5, 0.5]])
  unit-line([[0.0, -1.0], [0.0, -0.5], [-0.5, -0.5]])
  unit-line([[-1.0, 0.0], [-0.75, 0.0]])

  unit-line(0.1, [[-0.75, 0.65], [-0.75, -0.65]])
  
  switch(mode) :
    "enhancement" :
      for i in 0 to 3 do :
        val dy = to-double(i) * 0.5
        unit-line(0.1, [[-0.5, 0.65 - dy], [-0.5, 0.35 - dy]])
    "depletion" :
      unit-line(0.1, [[-0.5, 0.65], [-0.5, -0.65]])
    else :
      fatal("Incorrect FET mode %_." % [mode])

  if diode? :
    unit-line([[0.0, -0.5], [0.5, -0.5], [0.5, -0.2]])
    unit-triangle([0.5, -0.2], [0.5, 0.2], 0.5)
    unit-line([[0.25, 0.2], [0.75, 0.2]])  
    unit-line([[0.5, 0.2], [0.5, 0.5], [0.0, 0.5]])
    
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn fet-sym (type:String, mode:String, diode?:True|False) :
  fet-sym(type, mode, diode?, false)

public defn fet-sym (type:String, mode:String) :
  fet-sym(type, mode, true)

public defn fet-sym (type:String) :
  fet-sym(type, "enhancement")
  
public defn fet-sym () :
  fet-sym("n-type")

; ====== Op-Amps, Logic Gates ==================================================

pcb-symbol power-supply-sym :
  pin vs+ at unit-point(0.0, 2.0) with:
    direction = Down
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.3 * UNIT-TO-MM

  pin vs- at unit-point(0.0, -2.0) with:
    direction = Up
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.3 * UNIT-TO-MM
  
  unit-rectangle("foreground", 1.0, 3.0)
  
  unit-ref([0.5, 0.5])
  unit-val([0.5, -0.5])

pcb-symbol multi-op-amp-sym :
  pin vi- at unit-point(-2.0, -1.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
 
  pin vi+ at unit-point(-2.0, 1.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
  
  pin vo at unit-point(2.0, 0.0) with:
    direction = Left
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)
  
  unit-text([-1.1, 0.8], "+")
  unit-text([-1.1, -0.8], "-")

  unit-ref([0.0, 1.0])
  unit-val([0.0, -1.0]) 

; DEPRECATED
public pcb-symbol op-amp-sym :
  pin v- at unit-point(0.0, -2.0)
  pin v+ at unit-point(0.0, 2.0)
  pin in- at unit-point(-2.0, -1.0)
  pin in+ at unit-point(-2.0, 1.0)
  pin out at unit-point(2.0, 0.0)
  
  unit-line([[0.0, -2.0], [0.0, -0.75]])
  unit-line([[0.0, 2.0], [0.0, 0.75]])
  unit-line([[-2.0, -1.0], [-1.5, -1.0]])
  unit-line([[-2.0, 1.0], [-1.5, 1.0]])
  unit-line([[2.0, 0.0], [1.5, 0.0]])

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)

  unit-text([-1.1, 0.8], "+")
  unit-text([-1.1, -0.8], "-")

  unit-ref([0.0, 1.5])
  unit-val([0.0, -1.0])

public pcb-symbol multi-digital-buffer-sym (open-drain?:True|False) :
  pin a at unit-point(-2.0, 0.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
  
  pin y at unit-point(2.0, 0.0) with:
    direction = Left
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
 
  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)

  if open-drain? :  
    unit-line([[-0.5, -0.5], [-1.0, 0.0], [-0.5, 0.5], [0.0, 0.0], [-0.5, -0.5]])
    unit-line([[-1.0, -0.5], [0.0, -0.5]])

  unit-ref([0.0, 1.0])
  unit-val([0.0, -1.0])

public defn multi-digital-buffer-sym () :
  multi-digital-buffer-sym(false)

; DEPRECATED
public pcb-symbol digital-buffer-sym :
  pin v- at unit-point(0.0, -2.0)
  pin v+ at unit-point(0.0, 2.0)
  pin a at unit-point(-2.0, 0.0)
  pin y at unit-point(2.0, 0.0)

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)
  
  unit-line([[0.0, -2.0], [0.0, -0.75]])
  unit-line([[0.0, 2.0], [0.0, 0.75]])
  unit-line([[-2.0, 0.0], [-1.5, 0.0]])
  unit-line([[2.0, 0.0], [1.5, 0.0]])
  
  unit-ref([0.0, 1.5])
  unit-val([0.0, -1.0])

; ====== Connectors, Test Points, Holes ========================================

public pcb-symbol test-point-sym :
  pin p[0] at unit-point(1.0, 0.0)

  unit-circle([-0.5, 0.0], 0.25)
  unit-line(0.1, [[-0.25, 0.0], [1.0, 0.0]])
  
  unit-ref([-1.0, 0.75])

public pcb-symbol hole-sym :
  pin p[0] at unit-point(1.0, 0.0)

  unit-circle("background", [-0.5, 0.0], 0.25)
  unit-circle([-0.5, 0.0], 0.45)
  
  unit-line(0.1, [[-0.1, 0.0], [1.0, 0.0]])
  
  unit-ref([-1.0, 0.75])

public pcb-symbol unplated-hole-sym :

  unit-circle("background", [-0.5, 0.0], 0.25)
  unit-circle([-0.5, 0.0], 0.45)
  
  unit-ref([-1.0, 0.75])

public pcb-symbol header-symbol (n-pins:Int, sides:Int) :
  if sides > 2 :
    fatal("Header cannot have more than 2 sides!")
  if n-pins % sides != 0 :
    fatal("Need an even number of pins for a 2 sided header!")
  
  val ytop = to-double(n-pins / sides)/2.0
  val ypin = to-double(n-pins / sides)/2.0 - 0.5
  val nside = n-pins / sides
  unit-line([[(-1.0), ytop], [(-1.0), (- ytop)]])
  unit-line([[1.0, ytop], [1.0, (- ytop)]])
  unit-line([[(-1.0), ytop], [1.0, ytop]])
  unit-line([[(-1.0), (- ytop)], [1.0, (- ytop)]])
  for i in 0 to nside do :
    pin p[(i + 1)] at unit-point(1.0, (ypin - (to-double(i) * 1.0))) with :
      direction = Right
      length = 2.54
      number-size = 0.762
      name-size = 0.762
    if sides > 1 :
      pin p[(i + nside + 1)] at unit-point((- 1.0), (ypin - (to-double(i) * 1.0))) with :
        direction = Left
        length = 2.54
        number-size = 0.762
        name-size = 0.762
    
public defn header-symbol (n-pins:Int) :
  header-symbol(n-pins, 1)

; ====== Switches, Relays ======================================================

; TODO: Switch symbol parametric in connections

public pcb-symbol spst (n-pside:Int, n-tside:Int) :
  if n-pside < 1 :
    fatal("not enough pole pins!")
  if n-tside < 1 :
    fatal("not enough throw pins!")

  val xpoff = (to-double(n-pside) - 1.0)/2.0
  val xtoff = (to-double(n-tside) - 1.0)/2.0

  unit-line([[0.0, 0.0], [0.0, (- 1.0)]])
  unit-line([[0.0, 1.0], [0.0, 2.0]])
  unit-line([[0.0, 1.0], [1.0, 0.0]])

  for i in 0 to n-tside do :
    unit-line([[(to-double(i) - xtoff), 2.0], [(to-double(i) - xtoff), 3.0]])
    pin t[i + 1] at unit-point((to-double(i) - xtoff), 3.0) with : 
      direction = Up
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  unit-line([[( - xtoff), 2.0], [xtoff, 2.0]])

  for i in 0 to n-pside do :
    unit-line([[(to-double(i) - xpoff), (- 1.0)], [(to-double(i) - xpoff), (- 2.0)]])
    pin p[i + 1] at unit-point((to-double(i) - xpoff), (- 2.0)) with :
      direction = Down
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  unit-line([[( - xpoff), (- 1.0)], [xpoff, (- 1.0)]])

; TODO: Relay symbol parametric in connections

; ====== RF ====================================================================

public pcb-symbol antenna-symbol (n-feeds:Int, n-gnd:Int) :
  unit-circle([0.0, 0.0], 1.0)
  unit-line([[0.0, 0.707], [0.0, (- 1.0)]])
  unit-line([[0.0, 0.0], [0.50, 0.50]])
  unit-line([[0.0, 0.0], [(- 0.50), 0.50]])

  unit-line([[0.0, (- 1.0)], [(- 1.0), (- 1.0)]])
  pin p[1] at unit-point((- 1.0), (- 1.0)) with :
    direction = Left
    length = 2.54
    number-size = 0.762
    name-size = 0.762
  for i in 1 to n-feeds do :
    unit-line([[0.0, to-double(i + 1)*(- 1.0)], [(- 1.0), to-double(i + 1)*(- 1.0)]])
    unit-line([[0.0, to-double(i)*(- 1.0)], [0.0, to-double(i + 1)*(- 1.0)]])
    pin p[i + 1] at unit-point((- 1.0), to-double(i + 1)*(- 1.0)) with :
      direction = Left
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  val yoff = (to-double(n-gnd) - 1.0)/2.0
  unit-line([[1.0, yoff], [2.0, yoff]])
  pin p[n-feeds + 1] at unit-point(2.0, yoff) with : 
    direction = Right
    length = 2.54
    number-size = 0.762
    name-size = 0.762
  for i in 1 to n-gnd do :
    unit-line([[1.0, to-double(i)*(- 1.0) + yoff], [2.0, to-double(i)*(- 1.0) + yoff]])
    unit-line([[1.0, to-double(i - 1)*(- 1.0) + yoff], [1.0, to-double(i)*(- 1.0) + yoff]])
    pin p[n-feeds + 1 + i] at unit-point(2.0, to-double(i)*(- 1.0) + yoff) with :
      direction = Right
      length = 2.54
      number-size = 0.762
      name-size = 0.762

  unit-ref([(-1.5), 2.0])
  unit-val([(-1.5), 1.5])

; ====== Audio =================================================================

public pcb-symbol speaker-symbol :
  val xbodl = (- 0.5)
  val xbodr = 1.0
  val ybod = 1.0
  val xcone = 2.0
  val ycone = 2.0
  val ypin = 2.0
  
  pin high at unit-point(0.0, ypin)
  pin low at unit-point(0.0, (- ypin))

  ; draw body
  unit-line([[xbodl, ybod], [xbodr, ybod]])
  unit-line([[xbodl, (- ybod)], [xbodr, (- ybod)]])
  unit-line([[xbodl, (- ybod)], [xbodl, ybod]])
  unit-line([[xbodr, (- ybod)], [xbodr, ybod]])

  ; draw cone
  unit-line([[xbodr, ybod], [xcone, ycone]])
  unit-line([[xbodr, (- ybod)], [xcone, (- ycone)]])
  unit-line([[xcone, (- ycone)], [xcone, ycone]])

  ; draw leads
  unit-line([[0.0, ybod], [0.0, ypin]])
  unit-line([[0.0, (- ybod)], [0.0, (- ypin)]])

  unit-ref([1.0, 3.0])
  unit-val([1.0, 2.5])
