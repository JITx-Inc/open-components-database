#use-added-syntax(jitx)
defpackage ocdb/symbols :
  import core
  import collections
  import math
  import jitx
  import jitx/commands

  import ocdb/symbol-utils

; ====== Symbol enumerated types =======
; The following enumerated types replace strings in order to make the usage consistent.
; 

public pcb-enum ocdb/utils/ResistorSymbolType :
  ResistorStd
  ResistorVariable
  ResistorPot
  ResistorPhoto
  ResistorTherm

public pcb-enum ocdb/utils/Polarized :
  CapacitorPolarized
  CapacitorNonPolarized

public pcb-enum ocdb/utils/InductorSymbolType :
  InductorStd
  InductorIronCore
  InductorFerriteCore
  InductorVariable
  InductorPreset

public pcb-enum ocdb/utils/DiodeSymbolType :
  DiodeStd
  DiodeLED
  DiodePhoto
  DiodeZener
  DiodeSchottky

public pcb-enum ocdb/utils/OptoSymbolType :
  OptoEmitter
  OptoDetector

public pcb-enum ocdb/utils/BJTSymbolType :
  BJTNpn
  BJTPnp

public pcb-enum ocdb/utils/FETSymbolType :
  FETNType
  FETNmos
  FETPType
  FETPmos

public pcb-enum ocdb/utils/FETSymbolMode :
  FETEnhancement
  FETDepletion

; ======= Altium symbols =======================================================

public pcb-symbol altium-power-arrow-sym :
  name = "ALTIUM-POWER-ARROW"
  pin p[0] at unit-point(0.0, 0.0)

  unit-line([[0.0, 0.0], [1.27, 0.0]])
  unit-triangle([1.27, 0.0], [2.794, 0.0], 1.524)
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-power-circle-sym :
  name = "ALTIUM-POWER-CIRCLE"
  pin p[0] at unit-point(0.0, 0.0)

  unit-line([[0.0, 0.0], [1.27, 0.0]])
  unit-circle([3.0 / 2.0 * 1.27, 0.0], 1.27 / 2.0)
  unit-val([3.5, -2.5])
  
  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-power-bar-sym : 
  name = "ALTIUM-POWER-BAR"
  pin p[0] at unit-point(0.0, 0.0)

  val ex = 3.0 / 2.0 * 1.27
  unit-line([[0.0, 0.0], [ex, 0.0]])
  unit-line([[ex, -1.27 / 2.0], [ex, 1.27 / 2.0]])
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-power-wave-sym :
  name = "ALTIUM-POWER-WAVE"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 0.5 * 1.27
  val ex = 4.0 * u
  unit-line([[0.0, 0.0], [ex, 0.0]])
  unit-line([[ex - u, u], [ex, 0.5 * u]])
  unit-line([[ex, 0.5 * u], [ex, -0.5 * u]])
  unit-line([[ex + u, (- u)], [ex, -0.5 * u]])
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-power-gnd-power-sym :
  name = "ALTIUM-POWER-GND-POWER"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 1.27
  unit-line([[0.0, 0.0], [2.0 * u, 0.0]])
  unit-line(0.1, [[2.00 * u, -1.40], [2.00 * u, 1.40]])
  unit-line(0.1, [[2.67 * u, -1.08], [2.67 * u, 1.08]])
  unit-line(0.1, [[3.33 * u, -0.77], [3.33 * u, 0.77]])
  unit-line(0.1, [[4.00 * u, -0.46], [4.00 * u, 0.46]])
  unit-val([7.5, -2.5])

  preferred-orientation = PreferRotation([3])

public pcb-symbol altium-power-gnd-signal-sym :
  name = "ALTIUM-POWER-GND-SIGNAL"
  pin p[0] at unit-point(0.0, 0.0)

  unit-line([[0.0, 0.0], [1.27, 0.0]])
  unit-triangle([1.27, 0.0], [1.5 * 2.794, 0.0], 1.5 * 1.524)
  unit-val([1.5 * 3.5, 1.5 * -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-power-gnd-earth-sym :
  name = "ALTIUM-POWER-GND-EARTH"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 0.5 * 1.27
  val ex = 4.0 * u
  unit-line([[0.0, 0.0], [ex, 0.0]])
  unit-line([[ex, u], [ex, -1.0 * u]])

  unit-line([[ex, u], [ex + 0.5 * u, u - 0.5 * u]])
  unit-line([[ex, 0.0], [ex + 0.5 * u, -0.5 * u]])
  unit-line([[ex, -1.0 * u], [ex + 0.5 * u, -1.5 * u]])
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-gost-power-arrow-sym :
  name = "ALTIUM-GOST-POWER-ARROW"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 0.5 * 1.27
  val ex = 4.0 * u
  unit-line([[0.0, 0.0], [ex + u, 0.0]])
  unit-line([[ex, u], [ex + u, 0.0]])
  unit-line([[ex, -1.0 * u], [ex + u, 0.0]])
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

public pcb-symbol altium-gost-gnd-power-sym :
  name = "ALTIUM-GOST-GND-POWER"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 1.27
  unit-line([[0.0, 0.0], [2.0 * u, 0.0]])
  unit-line(0.1, [[2.00 * u, -1.40], [2.00 * u, 1.40]])
  unit-line(0.1, [[2.67 * u, -0.93], [2.67 * u, 0.93]])
  unit-line(0.1, [[3.33 * u, -0.46], [3.33 * u, 0.46]])

  preferred-orientation = PreferRotation([3])

public pcb-symbol altium-gost-gnd-earth-sym :
  name = "ALTIUM-GOST-GND-EARTH"
  pin p[0] at unit-point(0.0, 0.0)

  val u = 1.27
  unit-line([[0.0, 0.0], [2.0 * u, 0.0]])
  unit-line(0.1, [[2.00 * u, -1.40], [2.00 * u, 1.40]])
  unit-line(0.1, [[2.67 * u, -0.93], [2.67 * u, 0.93]])
  unit-line(0.1, [[3.33 * u, -0.46], [3.33 * u, 0.46]])
  unit-circle([2.0 * u, 0.0], 1.50 * u)

  preferred-orientation = PreferRotation([3])

public pcb-symbol altium-gost-bar-sym : 
  name = "ALTIUM-GOST-BAR"
  pin p[0] at unit-point(0.0, 0.0)

  val ex = 3.0 / 2.0 * 1.27
  unit-line([[0.0, 0.0], [ex, 0.0]])
  unit-line([[ex, -1.27 / 2.0], [ex, 1.27 / 2.0]])
  unit-val([3.5, -2.5])

  preferred-orientation = PreferRotation([1])

; ====== Supply and Ground =====================================================

public pcb-symbol supply-sym :
  pin p[0] at unit-point(0.0, 0.0)

  unit-line([[0.0, 0.0], [0.0, 0.25]])
  unit-triangle([0.0, 0.25], [0.0, 0.75], 0.5)

  unit-val([-1.0, 1.5])
  preferred-orientation = PreferRotation([0])
  backend-substitution("Altium") = altium-power-arrow-sym

public pcb-symbol ground-sym :
  pin p[0] at unit-point(0.0, 0.0)
  
  unit-line([[0.0, 0.0], [0.0, -0.5]])
  unit-line(0.1, [[-0.5, -0.5], [0.5, -0.5]])
  unit-line(0.1, [[-0.3, -0.7], [0.3, -0.7]])
  unit-line(0.1, [[-0.1, -0.9], [0.1, -0.9]])

  unit-val([-1.0, -1.5])
  preferred-orientation = PreferRotation([0])
  backend-substitution("Altium") = altium-power-gnd-power-sym

; ====== Discrete Passives (Resistors, Capacitors, Inductors) ==================

; Resistor symbol generator
; 
public pcb-symbol resistor-sym (r-type:ResistorSymbolType) :
  pin p[1] at unit-point(0.0, -1.0)
  pin p[2] at unit-point(0.0, 1.0)
  
  unit-line([[0.0, -1.0], [0.0, -0.625]])
  unit-line(0.1, [
    [0.0, -0.625]
    [-0.3, -0.5]
    [0.3, -0.25]
    [-0.3, 0.0]
    [0.3, 0.25]
    [-0.3, 0.5]
    [0.0, 0.625]
  ])
  unit-line([[0.0, 0.625], [0.0, 1.0]])

  defn var-arrow () :
    unit-line([[-0.6, -0.8], [0.6,0.8]])
    unit-triangle([0.45, 0.6], [0.6, 0.8], 0.3)

  switch(r-type) :
    ResistorStd :
      false  ; do nothing special here
    ResistorVariable :
      var-arrow()
    ResistorPot :
      pin p[3] at unit-point(-1.0, 0.0)
      unit-line([[-1.0, 0.0], [-0.6, 0.0]])
      unit-triangle([-0.6, 0.0], [-0.3, 0.0], 0.3)
    ResistorPhoto :
      draw-opto(OptoDetector)
    ResistorTherm :
      var-arrow()
      unit-text([-0.75, 0.0],"T")

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn resistor-sym () :
  resistor-sym(ResistorStd)

public pcb-symbol capacitor-sym (pol:Polarized) :
  pin p[1] at unit-point(0.0, 1.0)
  pin p[2] at unit-point(0.0, -1.0)
  
  unit-line([[0.0, 1.0], [0.0, 0.2]])
  unit-line(0.1, [[-0.5, 0.2], [0.5, 0.2]])
  unit-line([[0.0, -0.2], [0.0, -1.0]])

  if pol == CapacitorPolarized :
    unit-text([0.7, 0.7], "+")
    val a = asin(0.5 / 0.8)
    unit-approx-arc([0.0, -1.0], 0.8, PI / 2.0 + a, PI / 2.0 - a, 0.1)
  else if pol == CapacitorNonPolarized:
    unit-line(0.1, [[-0.5, -0.2], [0.5, -0.2]])
    
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])
  
public defn capacitor-sym () :
  capacitor-sym(CapacitorNonPolarized)

public pcb-symbol inductor-sym (l-type:InductorSymbolType) :
  pin p[1] at unit-point(0.0, -1.0)
  pin p[2] at unit-point(0.0, 1.0)

  unit-line([[0.0, -1.0], [0.0, -0.75]])
  unit-line([[0.0, 0.75], [0.0, 1.0]])
  
  for i in 0 to 3 do :
    unit-approx-arc([0.0, 0.5 - 0.5 * to-double(i)], 0.25, 3.0 * PI / 2.0, PI / 2.0, 0.1)
  defn var-arrow () :
    unit-line([[-0.6, -0.8], [0.6,0.8]])
    unit-triangle([0.45, 0.6], [0.6, 0.8], 0.3)
  
  ; https://electricalvoice.com/inductor-and-its-types/
  switch(l-type) :
    InductorStd :
      false  ; do nothing special here
    InductorIronCore :
      unit-line([[-0.55, -0.75], [-0.55, 0.75]])
      unit-line([[-0.6, -0.75], [-0.6, 0.75]])
    InductorFerriteCore :
      for i in 0 to 3 do :
        val dy = to-double(i) * 0.375
        unit-line(0.1, [[-0.55, 0.75 - dy], [-0.55, 0.45 - dy]])
        unit-line(0.1, [[-0.60, 0.75 - dy], [-0.60, 0.45 - dy]])
    InductorVariable :
      var-arrow()
    InductorPreset :
      var-arrow()
      unit-text([-0.75, 0.0],"F")
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

; ====== Fuses =================================================================
; not currently tested or used?
pcb-symbol fuse-sym :
  pin p[0] at unit-point(0.0, 1.0)
  pin p[1] at unit-point(0.0, -1.0)

  unit-line([[0.0, 1.0], [0.0, 0.75]])
  unit-line([[0.0, -1.0], [0.0, -0.75]])

  unit-rectangle(0.8, 1.5)

  unit-line([[-0.4, 0.5], [0.4, 0.5]])
  unit-line([[-0.4, -0.5], [0.4, -0.5]])

  unit-approx-arc([0.0, 0.25], 0.25, 3.0 * PI / 2.0, PI / 2.0)
  unit-approx-arc([0.0, -0.25], 0.25, (- PI) / 2.0, PI / 2.0)

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

; ====== Crystals, Oscillators =================================================

public pcb-symbol crystal-sym (n-gnd:Int) :
  pin p[1] at unit-point(-1.0, 0.0)
  pin p[2] at unit-point(1.0, 0.0)

  unit-line([[-1.0, 0.0], [-0.5, 0.0]])
  unit-line(0.1, [[-0.5, -0.5], [-0.5, 0.5]])
  unit-rectangle(0.5, 1.0)
  unit-line(0.1, [[0.5, -0.5], [0.5, 0.5]])
  unit-line([[0.5, 0.0], [1.0, 0.0]])

  val gnd-locs = [[-1.0, 0.0], [-1.0, -1.0]]

  if n-gnd > 0 :
    unit-line(0.1, [[0.5, -1.0], [-0.5, -1.0]])
    unit-line([[0.0, -1.0], [0.0, -2.0]])
    pin gnd at unit-point(0.0, -2.0)

  unit-ref([1.0, 0.5])
  unit-val([1.2, 0.2])


public defn crystal-sym () :
  crystal-sym(0)

; ====== Discrete Semiconductors (Diodes, Transistors) =========================

; TODO: Factor diode shape to allow scaled translation and reuse

public pcb-symbol diode-sym (d-type:DiodeSymbolType) :
  pin a at unit-point(0.0, 1.0)
  pin c at unit-point(0.0, -1.0)

  unit-line([[0.0, 1.0], [0.0, 0.4]])
  unit-triangle([0.0, 0.4], [0.0, -0.4], 1.0)
  unit-line([[0.0, -0.4], [0.0, -1.0]])
  unit-line([[-0.5, -0.4], [0.5, -0.4]])  
  
  switch(d-type) :
    DiodeStd      : false ; do nothing for now
    DiodeLED      : draw-opto(OptoEmitter)
    DiodePhoto    : draw-opto(OptoDetector)
    DiodeZener    :
      unit-line([[-0.7, -0.2], [-0.5, -0.4]])
      unit-line([[0.5, -0.4], [0.7, -0.6]])
    DiodeSchottky :
      unit-line([[-0.5, -0.4], [-0.5, -0.15], [-0.3, -0.15]])
      unit-line([[0.5, -0.4], [0.5, -0.65], [0.3, -0.65]])
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn diode-sym () :
  diode-sym(DiodeStd)

public pcb-symbol diode-tvs :
  pin a at unit-point(0.0, 1.5)
  pin c at unit-point(0.0, -1.5)
  val ytop = 0.3
  val ybot = (- ytop)
  
  ; draw lower diode first
  unit-triangle([0.0, ybot], [0.0, (ybot - 0.8)], 1.0)
  unit-line([[0.0,(ybot - 0.8)], [0.0, -1.5]])
  unit-line([[-0.5, (ybot - 0.8)], [0.5, (ybot - 0.8)]])
  unit-line([[-0.7, (ybot - 1.0)], [-0.5, (ybot - 0.8)]])
  unit-line([[0.5, (ybot - 0.8)], [0.7, (ybot - 0.6)]])  

  ; now draw second one flipped above
  unit-triangle([0.0, ytop], [0.0, (ytop + 0.8)], 1.0)
  unit-line([[0.0,(ytop + 0.8)], [0.0, 1.5]])
  unit-line([[-0.5, (ytop + 0.8)], [0.5, (ytop + 0.8)]])
  unit-line([[-0.7, (ytop + 0.6)], [-0.5, (ytop + 0.8)]])
  unit-line([[0.5, (ytop + 0.8)], [0.7, (ytop + 1.0)]]) 

  ; connecting line
  unit-line([[0.0, ybot], [0.0, ytop]])

  unit-ref([1.0, 1.0])
  unit-val([1.0, -1.0])

public pcb-symbol diode-rgb-led :
  val xr = (- 2.0)
  val xg = 0.0
  val xb = 2.0
  val ybot = 0.4
  
  pin a at unit-point(0.0, 2.0)
  pin r at unit-point(xr, -1.5)
  pin g at unit-point(xg, -1.5)
  pin b at unit-point(xb, -1.5)

  ; draw three diode symbols next to each other along the bottom
  ; red
  unit-triangle([xr, ybot], [xr, (ybot - 0.8)], 1.0)
  unit-line([[xr,(ybot - 0.8)], [xr, -1.5]])
  unit-line([[xr - 0.5, (ybot - 0.8)], [xr + 0.5, (ybot - 0.8)]])
  draw-opto(OptoEmitter, [xr, 0.0])
  unit-text([(xr - 0.3) -1.2], "R")

  ; green 
  unit-triangle([xg, ybot], [xg, (ybot - 0.8)], 1.0)
  unit-line([[xg,(ybot - 0.8)], [xg, -1.5]])
  unit-line([[xg - 0.5, (ybot - 0.8)], [xg + 0.5, (ybot - 0.8)]])
  draw-opto(OptoEmitter, [xg, 0.0])
  unit-text([(xg - 0.3) -1.2], "G")

  ; blue 
  unit-triangle([xb, ybot], [xb, (ybot - 0.8)], 1.0)
  unit-line([[xb,(ybot - 0.8)], [xb, -1.5]])
  unit-line([[xb - 0.5, (ybot - 0.8)], [xb + 0.5, (ybot - 0.8)]])
  draw-opto(OptoEmitter, [xb, 0.0])
  unit-text([(xb - 0.3) -1.2], "B")

  ; add remaining lines
  unit-line([[xr, ybot], [xr, ybot + 0.5]])
  unit-line([[xg, ybot], [xg, 2.0]])
  unit-line([[xb, ybot], [xb, ybot + 0.5]])
  unit-line([[xr, ybot + 0.5], [xb, ybot + 0.5]])

  unit-ref([1.0, 2.0])
  unit-val([1.0, 1.5])

public defn draw-opto (dir:OptoSymbolType, offset:Point|[Double, Double]) :
  val [xoff, yoff] = point-coords(offset)
  unit-line([[xoff + 0.5, yoff + 0.1], [xoff + 0.9, (yoff - 0.3)]])
  unit-line([[xoff + 0.5, (yoff - 0.2)], [xoff + 0.9, (yoff - 0.6)]])
  switch(dir) :
    OptoEmitter :
      unit-line([[xoff + 0.75, (yoff - 0.3)], [xoff + 0.9, (yoff - 0.3)], [xoff + 0.9, (yoff - 0.15)]])
      unit-line([[xoff + 0.75, (yoff - 0.6)], [xoff + 0.9, (yoff - 0.6)], [xoff + 0.9, (yoff - 0.45)]])
    OptoDetector :
      unit-line([[xoff + 0.5, (yoff - 0.05)], [xoff + 0.5, yoff + 0.1], [xoff + 0.65, yoff + 0.1]])
      unit-line([[xoff + 0.5, (yoff - 0.35)], [xoff + 0.5, (yoff - 0.2)], [xoff + 0.65, (yoff - 0.2)]])
  unit-circle(offset, 0.75)

public defn draw-opto (dir:OptoSymbolType) :
  draw-opto(dir, [0.0,0.0])

public pcb-symbol bjt-sym (bjt-type:BJTSymbolType) :
  pin e at unit-point(1.0, -1.0)
  pin c at unit-point(1.0, 1.0)
  pin b at unit-point(-1.0, 0.0)

  unit-line([[-1.0, 0.0],[-0.75, 0.0]])
  unit-line(0.1, [[-0.75, 0.0], [-0.25, 0.0]])

  unit-line(0.1, [[-0.25, 0.75], [-0.25, -0.75]])
  
  unit-line(0.1, [[-0.25, 0.375], [0.75, 0.875]])
  unit-line([[0.75, 0.875], [1.0, 1.0]])
  
  unit-line(0.1, [[-0.25, -0.375], [0.75, -0.875]])
  unit-line([[0.75, -0.875], [1.0, -1.0]])

  switch(bjt-type) :
    BJTNpn :
      unit-triangle([0.0, -0.5], [0.5, -0.75], 0.3)
    BJTPnp :
      unit-triangle([0.5, 0.75], [0.0, 0.5], 0.3)

  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn bjt-sym () :
  bjt-sym(BJTNpn)

public pcb-symbol fet-sym (type:FETSymbolType, mode:FETSymbolMode, diode?:True|False, bulk?:True|False) :
  
  ; Stanza does not support a fall-through behavior for its switch statement.
  ; https://stackoverflow.com/questions/66498304/how-to-write-a-fall-through-switch-statement-in-stanza
  val n-type? = switch(type) :
    FETNType :
      unit-line([[0.0, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [-0.5, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, -0.5]])
      true
    FETNmos :
      unit-line([[0.0, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [-0.5, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, -0.5]])
      true
    FETPType :
      unit-line([[-0.5, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [0.0, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, 0.5]])
      false
    FETPmos :
      unit-line([[-0.5, 0.0], [-0.25, 0.0]])
      unit-triangle([-0.25, 0.0], [0.0, 0.0], 0.3)
      if not bulk? : unit-line([[0.0, 0.0], [0.0, 0.5]])
      false

  pin g at unit-point(-1.0, 0.0) with:
    direction = Left
    length = 0.0
    number-size = 1.0
  
  val [s-y, s-dir, d-y, d-dir] = if n-type? :
    [-1.0, Down, 1.0, Up]
  else :
    [1.0, Up, -1.0, Down]

  pin s at unit-point(0.0, s-y) with:
    direction = s-dir
    length = 0.0
    number-size = 1.0
  
  pin d at unit-point(0.0, d-y) with:
    direction = d-dir
    length = 0.0
    number-size = 1.0

  if bulk? :
    pin bulk at unit-point(1.0, 0.0)
    unit-line([[0.0, 0.0], [1.0, 0.0]])

  unit-line([[0.0, 1.0], [0.0, 0.5], [-0.5, 0.5]])
  unit-line([[0.0, -1.0], [0.0, -0.5], [-0.5, -0.5]])
  unit-line([[-1.0, 0.0], [-0.75, 0.0]])

  unit-line(0.1, [[-0.75, 0.65], [-0.75, -0.65]])
  
  switch(mode) :
    FETEnhancement :
      for i in 0 to 3 do :
        val dy = to-double(i) * 0.5
        unit-line(0.1, [[-0.5, 0.65 - dy], [-0.5, 0.35 - dy]])
    FETDepletion :
      unit-line(0.1, [[-0.5, 0.65], [-0.5, -0.65]])

  if diode? :
    unit-line([[0.0, -0.5], [0.5, -0.5], [0.5, -0.2]])
    unit-triangle([0.5, -0.2], [0.5, 0.2], 0.5)
    unit-line([[0.25, 0.2], [0.75, 0.2]])  
    unit-line([[0.5, 0.2], [0.5, 0.5], [0.0, 0.5]])
    
  unit-ref([1.0, 0.5])
  unit-val([1.0, -0.5])

public defn fet-sym (type:FETSymbolType, mode:FETSymbolMode, diode?:True|False) :
  fet-sym(type, mode, diode?, false)

public defn fet-sym (type:FETSymbolType, mode:FETSymbolMode) :
  fet-sym(type, mode, true)

public defn fet-sym (type:FETSymbolType) :
  fet-sym(type, FETEnhancement)
  
public defn fet-sym () :
  fet-sym(FETNType)

; ====== Op-Amps, Logic Gates ==================================================

pcb-symbol power-supply-sym :
  pin vs+ at unit-point(0.0, 2.0) with:
    direction = Down
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.3 * UNIT-TO-MM

  pin vs- at unit-point(0.0, -2.0) with:
    direction = Up
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.3 * UNIT-TO-MM
  
  unit-rectangle("foreground", 1.0, 3.0)
  
  unit-ref([0.5, 0.5])
  unit-val([0.5, -0.5])

pcb-symbol multi-op-amp-sym :
  pin vi- at unit-point(-2.0, -1.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
 
  pin vi+ at unit-point(-2.0, 1.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
  
  pin vo at unit-point(2.0, 0.0) with:
    direction = Left
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)
  
  unit-text([-1.1, 0.8], "+")
  unit-text([-1.1, -0.8], "-")

  unit-ref([0.0, 1.0])
  unit-val([0.0, -1.0]) 

; DEPRECATED
public pcb-symbol op-amp-sym :
  pin v- at unit-point(0.0, -2.0)
  pin v+ at unit-point(0.0, 2.0)
  pin in- at unit-point(-2.0, -1.0)
  pin in+ at unit-point(-2.0, 1.0)
  pin out at unit-point(2.0, 0.0)
  
  unit-line([[0.0, -2.0], [0.0, -0.75]])
  unit-line([[0.0, 2.0], [0.0, 0.75]])
  unit-line([[-2.0, -1.0], [-1.5, -1.0]])
  unit-line([[-2.0, 1.0], [-1.5, 1.0]])
  unit-line([[2.0, 0.0], [1.5, 0.0]])

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)

  unit-text([-1.1, 0.8], "+")
  unit-text([-1.1, -0.8], "-")

  unit-ref([0.0, 1.5])
  unit-val([0.0, -1.0])

public pcb-symbol multi-digital-buffer-sym (open-drain?:True|False) :
  pin a at unit-point(-2.0, 0.0) with:
    direction = Right
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
  
  pin y at unit-point(2.0, 0.0) with:
    direction = Left
    length = 0.5 * UNIT-TO-MM
    number-size = 0.3 * UNIT-TO-MM
    name-size = 0.0
 
  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)

  if open-drain? :  
    unit-line([[-0.5, -0.5], [-1.0, 0.0], [-0.5, 0.5], [0.0, 0.0], [-0.5, -0.5]])
    unit-line([[-1.0, -0.5], [0.0, -0.5]])

  unit-ref([0.0, 1.0])
  unit-val([0.0, -1.0])

public defn multi-digital-buffer-sym () :
  multi-digital-buffer-sym(false)

; DEPRECATED
public pcb-symbol digital-buffer-sym :
  pin v- at unit-point(0.0, -2.0)
  pin v+ at unit-point(0.0, 2.0)
  pin a at unit-point(-2.0, 0.0)
  pin y at unit-point(2.0, 0.0)

  unit-triangle("foreground", [-1.5, 0.0], [1.5, 0.0], 3.0)
  
  unit-line([[0.0, -2.0], [0.0, -0.75]])
  unit-line([[0.0, 2.0], [0.0, 0.75]])
  unit-line([[-2.0, 0.0], [-1.5, 0.0]])
  unit-line([[2.0, 0.0], [1.5, 0.0]])
  
  unit-ref([0.0, 1.5])
  unit-val([0.0, -1.0])

; ====== Connectors, Test Points, Holes ========================================

public pcb-symbol test-point-sym :
  pin p at unit-point(1.0, 0.0)

  unit-circle([-0.5, 0.0], 0.25)
  unit-line(0.1, [[-0.25, 0.0], [1.0, 0.0]])
  
  unit-ref([-1.0, 0.75])

public pcb-symbol hole-sym :
  pin p[0] at unit-point(1.0, 0.0)

  unit-circle("background", [-0.5, 0.0], 0.25)
  unit-circle([-0.5, 0.0], 0.45)
  
  unit-line(0.1, [[-0.1, 0.0], [1.0, 0.0]])
  
  unit-ref([-1.0, 0.75])

public pcb-symbol unplated-hole-sym :

  unit-circle("background", [-0.5, 0.0], 0.25)
  unit-circle([-0.5, 0.0], 0.45)
  
  unit-ref([-1.0, 0.75])

public pcb-symbol header-symbol (n-pins:Int, sides:Int) :
  if sides > 2 :
    fatal("Header cannot have more than 2 sides!")
  if n-pins % sides != 0 :
    fatal("Need an even number of pins for a 2 sided header!")
  
  val ytop = to-double(n-pins / sides)/2.0
  val ypin = to-double(n-pins / sides)/2.0 - 0.5
  val nside = n-pins / sides
  unit-val([-1.0, ytop + 0.25])
  unit-ref([-1.0, ytop + 0.5])
  if sides == 2 :
    unit-rectangle("foreground", to-double(sides), ytop * 2.0)
  else if sides == 1 :
    unit-rectangle("foreground", to-double(sides), ytop * 2.0, unit-loc([-0.2, 0.0]))

  for i in 0 to nside do :
    pin p[(i * sides + 1)] at unit-point(-1.0, (ypin - (to-double(i) * 1.0))) with :
      direction = Left
      length = 2.54
      number-size = 0.762
      name-size = 0.762
    if sides > 1 :
      pin p[(i * sides + 2)] at unit-point(1.0, (ypin - (to-double(i) * 1.0))) with :
        direction = Right
        length = 2.54
        number-size = 0.762
        name-size = 0.762
  preferred-orientation = PreferRotation([0])
  
    
public defn header-symbol (n-pins:Int) :
  header-symbol(n-pins, 1)

; ====== Switches, Relays ======================================================

; TODO: Switch symbol parametric in connections

public pcb-symbol spst (n-pside:Int, n-tside:Int) :
  if n-pside < 1 :
    fatal("not enough pole pins!")
  if n-tside < 1 :
    fatal("not enough throw pins!")

  val xpoff = (to-double(n-pside) - 1.0)/2.0
  val xtoff = (to-double(n-tside) - 1.0)/2.0

  unit-line([[0.0, 0.0], [0.0, (- 1.0)]])
  unit-line([[0.0, 1.0], [0.0, 2.0]])
  unit-line([[0.0, 1.0], [1.0, 0.0]])

  for i in 0 to n-tside do :
    unit-line([[(to-double(i) - xtoff), 2.0], [(to-double(i) - xtoff), 3.0]])
    pin t[i + 1] at unit-point((to-double(i) - xtoff), 3.0) with : 
      direction = Up
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  unit-line([[( - xtoff), 2.0], [xtoff, 2.0]])

  for i in 0 to n-pside do :
    unit-line([[(to-double(i) - xpoff), (- 1.0)], [(to-double(i) - xpoff), (- 2.0)]])
    pin p[i + 1] at unit-point((to-double(i) - xpoff), (- 2.0)) with :
      direction = Down
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  unit-line([[( - xpoff), (- 1.0)], [xpoff, (- 1.0)]])

; TODO: Relay symbol parametric in connections

; ====== RF ====================================================================

public pcb-symbol antenna-symbol (n-feeds:Int, n-gnd:Int) :
  unit-circle([0.0, 0.0], 1.0)
  unit-line([[0.0, 0.707], [0.0, (- 1.0)]])
  unit-line([[0.0, 0.0], [0.50, 0.50]])
  unit-line([[0.0, 0.0], [(- 0.50), 0.50]])
  preferred-orientation = PreferRotation([0])

  unit-line([[0.0, (- 1.0)], [(- 1.0), (- 1.0)]])
  pin p[1] at unit-point((- 1.0), (- 1.0)) with :
    direction = Left
    length = 2.54
    number-size = 0.762
    name-size = 0.762
  for i in 1 to n-feeds do :
    unit-line([[0.0, to-double(i + 1)*(- 1.0)], [(- 1.0), to-double(i + 1)*(- 1.0)]])
    unit-line([[0.0, to-double(i)*(- 1.0)], [0.0, to-double(i + 1)*(- 1.0)]])
    pin p[i + 1] at unit-point((- 1.0), to-double(i + 1)*(- 1.0)) with :
      direction = Left
      length = 2.54
      number-size = 0.762
      name-size = 0.762
  val yoff = (to-double(n-gnd) - 1.0)/2.0
  unit-line([[1.0, yoff], [2.0, yoff]])
  pin p[n-feeds + 1] at unit-point(2.0, yoff) with : 
    direction = Right
    length = 2.54
    number-size = 0.762
    name-size = 0.762
  for i in 1 to n-gnd do :
    unit-line([[1.0, to-double(i)*(- 1.0) + yoff], [2.0, to-double(i)*(- 1.0) + yoff]])
    unit-line([[1.0, to-double(i - 1)*(- 1.0) + yoff], [1.0, to-double(i)*(- 1.0) + yoff]])
    pin p[n-feeds + 1 + i] at unit-point(2.0, to-double(i)*(- 1.0) + yoff) with :
      direction = Right
      length = 2.54
      number-size = 0.762
      name-size = 0.762

  unit-ref([(-1.5), 2.0])
  unit-val([(-1.5), 1.5])

; ====== Audio =================================================================

public pcb-symbol speaker-symbol :
  val xbodl = (- 0.5)
  val xbodr = 1.0
  val ybod = 1.0
  val xcone = 2.0
  val ycone = 2.0
  val ypin = 2.0
  
  pin high at unit-point(0.0, ypin)
  pin low at unit-point(0.0, (- ypin))

  ; draw body
  unit-line([[xbodl, ybod], [xbodr, ybod]])
  unit-line([[xbodl, (- ybod)], [xbodr, (- ybod)]])
  unit-line([[xbodl, (- ybod)], [xbodl, ybod]])
  unit-line([[xbodr, (- ybod)], [xbodr, ybod]])

  ; draw cone
  unit-line([[xbodr, ybod], [xcone, ycone]])
  unit-line([[xbodr, (- ybod)], [xcone, (- ycone)]])
  unit-line([[xcone, (- ycone)], [xcone, ycone]])

  ; draw leads
  unit-line([[0.0, ybod], [0.0, ypin]])
  unit-line([[0.0, (- ybod)], [0.0, (- ypin)]])

  unit-ref([1.0, 3.0])
  unit-val([1.0, 2.5])
