#use-added-syntax(jitx)
defpackage ocdb/generator-utils:
  import core
  import collections
  import jitx
  import jitx/commands

  import ocdb/checks
  import ocdb/generic-components

  import jitx/powergen/gen

  import ocdb/design-vars
  import ocdb/property-structs
  import ocdb/power-regulators
  import ocdb/bundles
  import ocdb/tolerance

; =======================================
; Ciruit convenience functions
; =======================================

; Add load balancing caps to crystal oscillator
public defn add-xtal-caps (xtal:JITXObject, gnd:JITXObject, stray-capacitance:Double) :
  inside pcb-module:
    val c-load = load-capacitance(property(xtal.crystal-resonator))
    val c-bal = closest-std-val(2.0 * (c-load - stray-capacitance), 5.0)
    cap-strap(xtal.p[1], gnd, ["capacitance" => c-bal "temperature-coefficient.code" => "C0G" ])
    val c = cap-strap(xtal.p[2], gnd, ["capacitance" => c-bal "temperature-coefficient.code" => "C0G" ])
    c 
; Default stray capacitance assumed 5pf. 
public defn add-xtal-caps (xtal:JITXObject, gnd:JITXObject) :
  add-xtal-caps(xtal, gnd, 5.0e-12)

; Add pullup resistors to an open-drain bus (like i2c) to vio pin. Specced for minimum power draw meeting speed requirements [TI app note:slva689].
public defn add-open-drain-pullups (bus:JITXObject, vio:JITXObject, clk-rate:Double, bus-capacitance:Double) :
  inside pcb-module:
    val rise-time = 0.35 / (clk-rate * 5.0) ; Signal bandwidth~ 5x clock rate for fairly sharp edge
    val r = closest-std-val(rise-time / (0.8473 * bus-capacitance), 10.0)
    pcb-module collector (r-val:Double, count:Int):
      public inst r : chip-resistor(r-val)[count]
    inst rs:collector(r, length(ports(bus)))
    for (p in ports(bus), i in 0 to false) do :
      net (rs.r[i].p[1] p)
      net (rs.r[i].p[2] vio)
    rs

; Default is 100kHz clock rate, 100pF bus capacitance.
public defn add-open-drain-pullups (bus:JITXObject, vio:JITXObject) :
  add-open-drain-pullups(bus, vio, 100.0e3, 100.0e-12)

; =======================================
; Piecewise linear functions
; Example call : PWL([[x0 y0] [x1 y1] ... [xn yn])[x]. Returns interpolated value of y at x
; =======================================
public deftype PWL
public defmulti get (f:PWL, x:Double) -> Double
public defn PWL (ps:Tuple<[Double,Double]>) -> PWL :
  new PWL :
    defmethod get (this, x:Double) :
      val n = length(ps)
      if x < ps[0][0] :
        ; println("Warning: PWL is extrapolating with zero-order hold")
        ps[0][1]
      else if x >= ps[0][0] and x <= ps[n - 1][0] :
        val i = for i in 0 to n find! :
          ps[i][0] >= x
        val j = for i in reverse(0 to n) find! :
          ps[i][0] <= x
        if i == j :
          ps[i][1]
        else :
          val dydx = (ps[i][1] - ps[j][1]) / (ps[i][0] - ps[j][0])
          dydx * (x - ps[j][0]) + ps[j][1]
      else if x > ps[n - 1][0] :
        ; println("Warning: PWL is extrapolating with zero-order hold")
        ps[n - 1][1]
      else  :
        fatal("Something is wrong in PWL with x = %_" % [x])

; =======================================
; Assign schematic and layout groups
; =======================================
public defn group-modules (mods:Tuple<Tuple<JITXObject>>) :
  inside pcb-module :
    val g-count = to-seq(0 to false)  
    for m in mods do :
      val g-name = Ref("g-sg-%_" % [next(g-count)])
      for r in m do :
        schematic-group(r) = (g-name)
        layout-group(r) = (g-name)

public defn connect-floating-inputs-to-gnd ():
  inside pcb-module :
    for i in component-instances(self) do :
      for p in pins(i) do :
        ; println("Is %_ connected? %_" % [p, connected?(p)])
        if not connected?(p) and
           has-property?(p.digital-io) and
           property(p.type) == "i" :
          println("Connecting floating input %_ to gnd." % [p])
          net (p, self.gnd)

public defn connect-gnd-pins ():
  inside pcb-module :
    for i in instances(self) do :
      for p in ports(i) do :
        val port-name = tail(ref(p), ref-length(ref(p)) - 1)
        if port-name == Ref("gnd") :
          net (self.gnd, p)

public defn calculate-operating-points ()  :
  defn get-other-pin (comp:JITXObject,  p:JITXObject) :
    ; Used on 2-pin components to get the single other pin
    for ps in pins(comp) find! :
      ps != p

  defn resistor-operating-point (v: Double, r: Double) :
    OperatingPoint(min-max(0.0, v), min-max(0.0, v / r))

  defn capacitor-operating-point (v: Double) :
    OperatingPoint(min-max(0.0, v), min-max(0.0, 0.0))

  defn inductor-operating-point (v: Double, i: Double) :
    OperatingPoint(min-max(0.0, v), min-max(0.0, i))

  defn is-resistor? (i: JITXObject) :
    has-property?(i.resistance)

  defn is-capacitor? (i: JITXObject) :
    has-property?(i.capacitance)

  defn is-inductor? (i: JITXObject) :
    has-property?(i.inductance)

  defn resistance! (i: JITXObject) :
    property(i.resistance)

  defn dc-resistance! (i: JITXObject) :
    property(i.dc-resistance)

  defn check-dual-pin-count! (i: JITXObject) : 
    val pin-count = length(pins(i)) 
    fatal("Resistor, capacitor or inductor %_ has %_ pins instead of 2." % [ref(i), pin-count]) when pin-count != 2

  inside pcb-module :
    for i in component-instances(self) do :
      if is-resistor?(i) or is-capacitor?(i) or is-inductor?(i):
        if check-dual-pin-count!(i) :
          if has-property?(pins(i)[0].net-voltage) and has-property?(pins(i)[1].net-voltage) :
            val p1 = property(pins(i)[0].net-voltage)
            val p2 = property(pins(i)[1].net-voltage)
            val voltage = abs(max((max-value(p1) - min-value(p2)), (max-value(p2) - min-value(p1))))
            if is-resistor?(i) :
              property(i.operating-point) = resistor-operating-point(voltage, resistance!(i))
            if is-capacitor?(i) :
              property(i.operating-point) = capacitor-operating-point(voltage)
            if is-inductor?(i) :
              property(i.operating-point) = inductor-operating-point(voltage, voltage / dc-resistance!(i))

; - jitx/commands/connected-pins(p) result does not contain p.
; - connected-group(p) contains p if it is a component pin (not net or module pin)
defn connected-group (obj: Pin|Net) :
  defn is-component-pin? (pin: Pin) :
    containing-instance(pin) is Instance

  val extra-pins =
    match(obj) :
      (pin: Pin) : [pin] when is-component-pin?(pin) else []
      (net: Net) : []

  to-tuple $ cat(extra-pins, connected-pins(obj))


; =======================================
; Attach the DNP (do not populate) to objects (components, array of instances, or module)
; =======================================
public defn do-not-populate (objs:Seqable<JITXObject>) :
  inside pcb-module :
    for obj in objs do :
      match(instance-type(obj): SingleComponent) : property(obj.DNP) = true
      else : do-not-populate $ component-instances(obj)

public defn dnp (objs:Seqable<JITXObject>) :
  do-not-populate(objs)

; =======================================
; Attach the power generator to designs and loads.
; =======================================
public defn generate-power () :
  println("JITX: Generating power system.")
  inside pcb-module :
    for i in component-instances(self) do :
      for p in pins(i) do : ; [TODO] public-pins or pins?
        if has-property?(p.power-request) :
          val [v a n] = property(p.power-request)
          within (rail) = make-load("Load", v, a, n) :
            net (p, pos(rail))
            net (property(p.gnd-ref), neg(rail))

    for i in component-instances(self) do :
      for p in pins(i) do : ; [TODO] public-pins or pins?
        if has-property?(p.power-input) :
          generate-power-system(property(p.power-input), OPERATING-TEMPERATURE[1], p, property(p.gnd-ref))

public defn set-power-source (pos:JITXObject, neg:JITXObject, v-in:Double):
  inside pcb-module:
    property(pos.power-input) = v-in
    property(pos.gnd-ref) = neg
    property(pos.power-supply-pin) = PowerSupplyPin(v-in, 0.3) 

public defn propagate-net-voltages () :
  inside pcb-module:
    for item in all-connected-items(self) do :
      
      ;First gather all the voltages on all connected nets.
      val voltages = to-tuple $ seq?({property?(_.net-voltage)}, nets(item))
      ; val voltages = Vector<Toleranced>()
      ; for n in nets(item) do :
      ;   if has-property?(n.net-voltage) :      
      ;     add(voltages, property(n.net-voltage))
      ;If at least one net has a voltage property.
      if not empty?(voltages) :
        ;Let's check that all the voltages happen to be the same.
        ;If they are different, then something has gone wrong.

        ;Gather all the net names (some nets don't have names.).
        val net-names = Vector<String|Ref>()
        for (net in nets(item), i in 0 to false) do :
          match(ref?(net)) :
            (f:False) : add(net-names, "unnamed-net-%_" % [i])
            (r:Ref) : add(net-names, r)

        for i in 1 to length(nets(item)) do :
          if voltages[i] != voltages[0] :
            fatal("Mismatched net-voltage on net(s) %," % [net-names])

        ;Now assign the voltage to all of the component pins in 
        ;the module.
        for p in component-pins(item) do :
          property(p.net-voltage) = voltages[0]
        
; =======================================
; Run transformations to propagate voltages from sources, and run delayed evaluations
; =======================================
public defn run-final-passes (module:Instantiable) -> Instantiable :
  val result-module = transform-module{calculate-operating-points, _} $
    let loop (design: Instantiable = module, i: Int = 0) :
      defn run-eval-loop (design: Instantiable) :
        val design* = run-evals?(design)
        match(design*: Instantiable) : loop(design*, i + 1)
        else : design

      if i == 5 :
        throw $ Exception("A loop executing eval-whens, propagating rail voltages and assigning pins has been \
                           encountered: 5 iterations reached. Check that no eval-when generate other eval-whens in an \
                           infinite loop, or that there is no infinite loop of 2nd order between eval-when execution \
                           and rail voltage propagation or pin assignment.")

      ;TODO: Review the order and necessity of those algos
      design $> transform-module{propagate-net-voltages, _}
             $> assign-pins
             $> run-eval-loop

  ; If we did not set the main module, a user forgetting to set it himself on the new module would be exporting
  ; the module before adding operating points (the main module is set in the loop to be able to assign pins).
  ; Such bug would be invisible.
  set-main-module(result-module)
  result-module

; FIXME: assign-pins was obviously not meant to be used like this, it depends on the flattening
; Is run-evals? actually accessing the information resulting from the pin assignment?
defn assign-pins (design: Instantiable) -> Instantiable :
  set-main-module(design)
  assign-pins()
