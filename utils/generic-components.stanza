#use-added-syntax(jitx)
defpackage ocdb/generic-components:
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/bundles
  import ocdb/land-patterns
  import ocdb/generator-utils

  import ocdb/db-parts
  import ocdb/design-vars
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/property-structs

; ========================================================
; ====== Standard packages for the passive solver ========
; ========================================================
val PKGS = ["01005", "0201", "02016", "0202", "0303", "0402", "0404", "0503", "0505", "0603", "0612", "0805", "1206",
            "1210", "1218", "1812", "2010", "2512", "2525", "2615", "2616", "3920", "4122", "4823", "5329", "6030"]

public defn get-valid-pkg-list () :
  val min-pkg-idx = find({PKGS[_] == MIN-PKG}, 0 to length(PKGS))
  match(min-pkg-idx: Int) :
    PKGS[min-pkg-idx to false]
  else :
    throw(Exception("Invalid MIN-PKG."))

; ========================================================
; ==== EIA Standard Tolerances and Logarithmic Values ====
; ========================================================
public val std-tols = to-list([20.0, 10.0, 5.0, 2.0, 1.0, 0.5, 0.25, 0.1])
public val std-vals = HashTable<Double, List<Double>>()

std-vals[20.0] = to-list([
  1.00,1.50,2.20,3.30,4.70,6.80
])

std-vals[10.0] = to-list([
  1.20,1.80,2.70,3.90,5.60,8.20
])

std-vals[5.0] = to-list([
  1.10,1.30,1.60,2.00,2.40,3.00,
  3.60,4.30,5.10,6.20,7.50,9.10
])

std-vals[2.0] = to-list([
  1.05,1.15,1.21,1.27,1.33,1.40,1.47,1.54,1.62,
  1.69,1.78,1.87,1.96,2.05,2.15,2.26,2.37,2.49,
  2.61,2.74,2.87,3.01,3.16,3.32,3.48,3.65,3.83,
  4.02,4.22,4.42,4.64,4.87,5.11,5.36,5.62,5.90,
  6.19,6.49,6.81,7.15,7.87,8.25,8.66,9.09,9.53
])

std-vals[1.0] = to-list([
  1.02,1.07,1.13,1.18,1.24,1.37,1.43,1.58,1.65,
  1.74,1.82,1.91,2.10,2.21,2.32,2.43,2.55,2.67,
  2.80,2.94,3.09,3.24,3.40,3.57,3.74,3.92,4.12,
  4.32,4.53,4.75,4.99,5.23,5.49,5.76,6.04,6.34,
  6.65,6.98,7.32,7.68,8.06,8.45,8.87,9.31,9.76
])

;E192 tolerance series of resistors contains 0.5, 0.25, 0.1
val e192-list = to-list([
  1.00 1.01 1.02 1.04 1.05 1.06 1.07 1.09 1.10 1.11 1.13 1.14
  1.15 1.17 1.18 1.20 1.21 1.23 1.24 1.26 1.27 1.29 1.30 1.32
  1.33 1.35 1.37 1.38 1.40 1.42 1.43 1.45 1.47 1.49 1.50 1.52
  1.54 1.56 1.58 1.60 1.62 1.64 1.65 1.67 1.69 1.72 1.74 1.76
  1.78 1.80 1.82 1.84 1.87 1.89 1.91 1.93 1.96 1.98 2.00 2.03
  2.05 2.08 2.10 2.13 2.15 2.18 2.21 2.23 2.26 2.29 2.32 2.34
  2.37 2.40 2.43 2.46 2.49 2.52 2.55 2.58 2.61 2.64 2.67 2.71
  2.74 2.77 2.80 2.84 2.87 2.91 2.94 2.98 3.01 3.05 3.09 3.12
  3.16 3.20 3.24 3.28 3.32 3.36 3.40 3.44 3.48 3.52 3.57 3.61
  3.65 3.70 3.74 3.79 3.83 3.88 3.92 3.97 4.02 4.07 4.12 4.17
  4.22 4.27 4.32 4.37 4.42 4.48 4.53 4.59 4.64 4.70 4.75 4.81
  4.87 4.93 4.99 5.05 5.11 5.17 5.23 5.30 5.36 5.42 5.49 5.56
  5.62 5.69 5.76 5.83 5.90 5.97 6.04 6.12 6.19 6.26 6.34 6.42
  6.49 6.57 6.65 6.73 6.81 6.90 6.98 7.06 7.15 7.23 7.32 7.41
  7.50 7.59 7.68 7.77 7.87 7.96 8.06 8.16 8.25 8.35 8.45 8.56
  8.66 8.76 8.87 8.98 9.09 9.20 9.31 9.42 9.53 9.65 9.76 9.88
])
std-vals[0.5]  = e192-list
std-vals[0.25] = e192-list
std-vals[0.1]  = e192-list

; Read more http://www.resistorguide.com/resistor-values/
defn union-std-vals (tol:Double) :
  val union = Vector<Double>()
  for std-tol in std-tols do :
    if std-tol >= tol :
      add-all(union, std-vals[std-tol])
  to-list(qsort(union))

val cumulative-std-vals = HashTable<Double, List<Double>>()

for tol in std-tols do :
  cumulative-std-vals[tol] = union-std-vals(tol)


; Returns the closest value to v in the tolerance range specified by tol
public defn closest-std-val (v:Double, tol:Double) :
  val expon = floor(log10(v))
  val signif = v / (pow(10.0,expon))
  
  var match-diff = 100.0
  var match-val = -1.0

  if key?(cumulative-std-vals, tol) :
    for std-val in cumulative-std-vals[tol] do :
      val diff = abs(signif - std-val)
      if diff < match-diff :
        match-diff = diff
        match-val = std-val
  else :
    fatal("Tolerance %_ not found in standard tolerances." % [tol])

  match-val * pow(10.0, expon)


; ========================================================
; ========== Generic passive components ==================
; ========================================================
; Generic ceramic capacitor from database (of 600k options)
public defn ceramic-cap (params:Tuple<KeyValue>) :
  val default-params = ["type" => "ceramic" 
                        "mounting" => "smd" 
                        "case" => get-valid-pkg-list()
                        "minimum_quantity" => 1
                        "min-rated-voltage" => MIN-CAP-VOLTAGE
                        "min-stock" => 5 * DESIGN-QUANTITY]
  to-esir(Capacitor(to-tuple $ to-hashtable<String, ?> $ cat(default-params params)))

public defn ceramic-cap (cap:Double) :
  ceramic-cap(["capacitance" => cap])

; Generic chip resistor from database (of 600k options)
public defn chip-resistor (params:Tuple<KeyValue>) :
  val default-params = ["mounting" => "smd" 
                        "case" => get-valid-pkg-list()
                        "minimum_quantity" => 1
                        "min-stock" => 5 * DESIGN-QUANTITY]
  to-esir(Resistor(to-tuple $ to-hashtable<String, ?> $ cat(default-params params)))

public defn chip-resistor (resistance:Double, tolerance:Double) :
  chip-resistor(["resistance" => resistance "tolerance" => tolerance])

public defn chip-resistor (resistance:Double) :
  chip-resistor(["resistance" => resistance])

; Generic inductors from database of standard packages (0402, 0603... 10k options)
public defn smd-inductor (params:Tuple<KeyValue>) :
  val default-params = ["mounting" => "smd" 
                        "case" => get-valid-pkg-list()
                        "minimum_quantity" => 1
                        "min-stock" => 5 * DESIGN-QUANTITY]
  to-esir(Inductor(to-tuple $ to-hashtable<String, ?> $ cat(default-params params)))

public defn smd-inductor (ind:Double) :
  smd-inductor(["inductance" => ind])

public pcb-component gen-cap-cmp (cap:Double, tol:Double, max-v:Double, pkg-name:String) :
  port p : pin[1 through 2]
  val sym = capacitor-sym(false)
  val pkg = ipc-two-pin-landpattern(pkg-name)
  symbol = sym(p[1] => sym.p[1], p[2] => sym.p[2])
  landpattern = pkg(p[1] => pkg.p[1], p[2] => pkg.p[2])
  emodel = Capacitor(cap, tol, max-v)
  reference-prefix = "C"
  spice :
    "[C] {p[1]} tmp {cap}"
    "[R] tmp {p[2]} 0.01"
  property(self.capacitor) = true
  property(self.type) = "ceramic"
  property(self.capacitance) = cap
  property(self.tolerance) = tol
  property(self.rated-voltage) = max-v
  property(self.rated-temperature) = [-55.0 125.0]

public defn gen-cap-cmp (cap:Double, max-v:Double) :
  gen-cap-cmp(cap, 0.2, max-v, "0402")

public defn gen-cap-cmp (cap:Double, pkg:String) :
  gen-cap-cmp(cap, 0.2, 10.0, pkg)

public defn gen-cap-cmp (cap:Double) :
  gen-cap-cmp(cap, 10.0)

;Generic Ceramic surface mount capacitors
public pcb-component gen-tant-cap-cmp (cap:Double, tol:Double, max-v:Double, pkg:String) :
  pin a 
  pin c
  val sym = capacitor-sym(true)
  symbol = sym(a => sym.p[1], c => sym.p[2])
  emodel = Capacitor(cap, tol, max-v)
  switch(pkg) :
    "0402" : landpattern = CAPT1005X55(a => CAPT1005X55.p[1] c => CAPT1005X55.p[2])
    "0603" : landpattern = CAPT1608X80(a => CAPT1608X80.p[1] c => CAPT1608X80.p[2])
    "0805" : landpattern = CAPT2012X125(a => CAPT2012X125.p[1] c => CAPT2012X125.p[2])
    "1206" : landpattern = CAPT3216X160(a => CAPT3216X160.p[1] c => CAPT3216X160.p[2])
    else : fatal("Incorrect capacitor package %_." % [pkg])
  reference-prefix = "C"
  spice :
    "[C] {a} tmp {cap}"
    "[R] tmp {c} 2.0"
  property(self.capacitor) = true
  property(self.type) = "electrolytic"
  property(self.anode) = "tantalum"
  property(self.electrolyte) = "polymer"
  property(self.capacitance) = cap
  property(self.rated-voltage) = max-v
  property(self.tolerance) = (tol * 0.01)
  property(self.rated-current-pk) = 3.7
  property(self.rated-current-rms) = [[25.0 0.298] [85.0 0.268] [125.0 0.119]]
  property(self.rated-temperature) = [-55.0 125.0]

public defn gen-tant-cap-cmp (cap:Double, tol:Double, max-v:Double) :
  gen-tant-cap-cmp(cap, tol, max-v, "0402")

public defn gen-tant-cap-cmp (cap:Double, pkg:String) :
  gen-tant-cap-cmp(cap, 20.0, 10.0, pkg)

public defn gen-tant-cap-cmp (cap:Double, tol:Double) :
  gen-tant-cap-cmp(cap, tol, 10.0)
; public defn default-bypass (component:Ref) :
;   bypass-cap-strap(component.gnd, component.vdd, 0.1)

  gen-tant-cap-cmp(cap, 20.0)
 
public pcb-component gen-res-cmp (r-type:String|False, res:Double, tol:Double, pwr:Double, pkg-name:String) :
  ; if r-type is Symbol and (r-type as Symbol) == `pot :
  ;   port p : pin[1 2 3]
  ; else :
  port p : pin[1 through 2]
  val sym = resistor-sym(r-type)
  val pkg = ipc-two-pin-landpattern(pkg-name)
  symbol = sym(p[1] => sym.p[1], p[2] => sym.p[2])
  landpattern = pkg(p[1] => pkg.p[1], p[2] => pkg.p[2])
  emodel = Resistor(res,tol,pwr)
  reference-prefix = "R"
  property(self.resistor) = true
  property(self.type) = "thick-film"
  property(self.rated-voltage) = 50.0
  property(self.rated-power) = pwr
  property(self.derating) = [[-60.0 1.0] [70.0 1.0] [155.0 0.0]]
  property(self.TCR) = 100.0e-6
  property(self.rated-temperature) = [-55.0 155.0]
  property(self.tolerance) = (tol * 0.01)
  property(self.resistance) = res
  spice :
    "[R] {p[1]} {p[2]} {res}"

public defn gen-res-cmp (res:Double, tol:Double, pkg:String) :
  gen-res-cmp(false, res, tol, 0.0625, pkg)

public defn gen-res-cmp (res:Double, tol:Double, pwr:Double) :
  gen-res-cmp(false, res, tol, 0.0625, "0402")

public defn gen-res-cmp (res:Double, tol:Double) :
  gen-res-cmp(res, tol, 0.0625)

public defn gen-res-cmp (res:Double, pkg:String) :
  gen-res-cmp(false, res, 2.0, 0.0625, pkg)

public defn gen-res-cmp (res:Double) :
  gen-res-cmp(res, 2.0)

public pcb-component gen-ind-cmp (ind:Double, tol:Double, max-i:Double) :
  port p : pin[1 through 2]
  emodel = Inductor(ind,tol,max-i)

  symbol = inductor-sym(p[1] => inductor-sym.p[1], p[2] => inductor-sym.p[2])
  landpattern = INDC1005X55(p[1] => INDC1005X55.p[1], p[2] => INDC1005X55.p[2]) ;TODO: all inductors 0402
  reference-prefix = "L"
  property(self.inductor) = true
  property(self.inductance) = ind
  property(self.tolerance) = (tol * 0.01)
  property(self.max-current) = max-i

public defn gen-ind-cmp (ind:Double, tol:Double) :
  gen-ind-cmp(ind, tol, 0.1)

public defn gen-ind-cmp (ind:Double) :
  gen-ind-cmp(ind, 10.0)

; ========================================================
; ============ Component strapping functions =============
; ========================================================
; These functions attach a passive component between two pins. e.g. strap a pull up resistor between an IO and VDD

public defn bypass-cap-strap (power-pin:JITXObject, gnd-pin:JITXObject, capacitance:Double ):
  inside pcb-module:
    val cap = cap-strap(gnd-pin, power-pin, capacitance)
    short-trace(cap.p[2], power-pin)
    cap

public defn bypass-caps (power-pin:JITXObject|Symbol, gnd-pin:JITXObject|Symbol, voltage:Double, caps:Tuple<Double>, name:Symbol) :
  inside pcb-module:
    val n-caps = length(caps)
    for i in 0 to n-caps do :
      inst c : ceramic-cap(["capacitance" => caps[i], "min-rated-voltage" => voltage])
      match(power-pin) :
        (r:JITXObject) :
          net (r, c.p[1])
        (s:Symbol) :
          make-net(s, false, [c.p[1]], false)
      match(gnd-pin) :
        (r:JITXObject) :
          net (r, c.p[2])
        (s:Symbol) :
          make-net(s, false, [c.p[2]], false)

public defn res-strap (first-pin:JITXObject, second-pin:JITXObject, params:Tuple<KeyValue>) :
  inside pcb-module:
    inst rid : chip-resistor(params)
    net (first-pin, rid.p[1])
    net (second-pin, rid.p[2])
    rid

public defn res-strap (first-pin:JITXObject, second-pin:JITXObject, res:Double, tol:Double) :
  res-strap(first-pin, second-pin, ["resistance" => res "tolerance" => tol])

public defn res-strap (first-pin:JITXObject, second-pin:JITXObject, res:Double ) :
  res-strap(first-pin, second-pin, ["resistance" => res])
  
public defn cap-strap (first-pin:JITXObject, second-pin:JITXObject, capacitance:Double ) :
  cap-strap( first-pin, second-pin, ["capacitance" => capacitance] )

public defn cap-strap (first-pin:JITXObject, second-pin:JITXObject, params:Tuple<KeyValue>) :
  inside pcb-module:
    inst cap : ceramic-cap(params)
    net (first-pin, cap.p[1])
    net (second-pin, cap.p[2])
    cap

public defn default-bypass (cmp:JITXObject) :
  inside pcb-module:
    for p in ports(cmp) do :
      if has-property?(p.power-pin) :
        inst cap : ceramic-cap(0.1)
        property(cap.bypass) = true
        net (cap.p[1], p)
        short-trace(cap.p[1], p)
        net (cap.p[2], cmp.gnd)

public defn block-cap (in:JITXObject, out:JITXObject, cap:Double) :
  inside pcb-module :
    inst block : ceramic-cap(cap)
    net (in, block.p[1])
    net (out, block.p[2])
    block

public defn block-cap (in:JITXObject, out:JITXObject) :
  block-cap(in, out, 100.0e-6)


; ========================================================
; =================== Generic connectors =================
; ========================================================

public defn banana-plug (color:String) :
  inside pcb-module :
    public inst b : ocdb/pomona/1581/component(color)
    b

public pcb-component pin-header (n-pin:Int, n-row:Int, pin-pitch:Double, row-pitch:Double) :
  description = "Generic pin-header"
  mpn = to-string("%_X%_-pin-header" % [n-pin n-row])
  if n-row == 1 :
    pin-properties :
      [pin:Ref     | pads:Int ...   | side:Dir]
      for i in 1 through n-pin do :
        [p[i] | i | Left]
  else :
    pin-properties :
      [pin:Ref     | pads:Int ...   | side:Dir]
      for i in 1 through n-pin by 2 do :
        [p[i] | i | Left]
        [p[i + 1] | (i + 1) | Right]

  assign-landpattern(pin-header-pkg(n-pin, n-row, pin-pitch, row-pitch))
  make-box-symbol()

  reference-prefix = "J"

public defn pin-header (n-pin:Int) :
  pin-header(n-pin, 1, 2.54)

public defn pin-header (n-pin:Int, n-row:Int) :
  pin-header(n-pin, n-row, 2.54)

public defn pin-header (n-pin: Int, pin-pitch:Double) :
  pin-header(n-pin, 1, pin-pitch)

public defn pin-header (n-pin: Int, n-row: Int, pin-pitch:Double) :
  pin-header(n-pin, n-row, pin-pitch, pin-pitch)

; [TODO] is gnd from self? Yes, it is from self. Add in when this area is uncommented.
;<gnd>
public defn sma-connector (sig:JITXObject, launch:String) :
  inside pcb-module :
    if launch == "end-launch":
      public inst c : ocdb/johnson/142-0761-881/component
      net (c.sig, sig)
      net (c.gnd, gnd)
      c
    else :
      fatal("Unsupported SMA Connector type")

public defn bnc-connector (sig:JITXObject, launch:String) :
  inside pcb-module :
    if launch == "end-launch":
      public inst c : ocdb/amphenol/112640/component
      net (c.sig, sig)
      net (c.gnd, gnd)
      c
    else :
      fatal("Unsupported SMA Connector type")

public defn ethernet-jack ():
  inside pcb-module :
    public inst c : ocdb/pulse-electronics/J0G-0009NL/component
    net (c.shield, gnd)
    c
;<gnd>

; USB2 device connection via USBA MicroB
public pcb-module micro-usb-connector :
  port usb-2 : usb-2
  public inst con : ocdb/amphenol/10103594-0001LF/component
  net VBUS (usb-2.vbus.vdd con.VCC)
  net (usb-2.vbus.gnd con.GND)
  ; RC filter connection for chassis gnd
  res-strap(con.SHIELD, con.GND, 100.0e3)
  cap-strap(con.SHIELD, con.GND, 0.1e-6)
  net D+ (usb-2.data.P con.D+)
  net D- (usb-2.data.N con.D-)
  net ID (usb-2.id con.ID)
  property(con.VCC.power-supply-pin) = PowerSupplyPin(5.0, 0.5)

; USB2 device connection via USBC connector
public pcb-module usb2-on-a-usb-c-connector :
  port usb-2 : usb-2
  public inst con : ocdb/belfuse/SS-52400-003/component

  net VBUS (usb-2.vbus.vdd con.vbus)
  net (usb-2.vbus.gnd con.GND)

  net D- (usb-2.data.N con.DN1, con.DN2)
  net D+ (usb-2.data.P con.DP1, con.DP2)

  ; RC filter connection for chassis gnd
  res-strap(con.SHIELD, con.GND, 100.0e3)
  cap-strap(con.SHIELD, con.GND, 0.1e-6)

  ; 5V Downstream USBC device configuration
  res-strap(con.CC1, con.GND, 5.1e3)
  res-strap(con.CC2, con.GND, 5.1e3)

  property(con.vbus) = PowerSupplyPin(5.0, 0.5)
  
; Add a tag-connect landing pattern to a processor
public defn attach-programming-connector (proc:JITXObject, vio:JITXObject, intf:String):
  inside pcb-module:
    public inst tag : ocdb/tag-connect/TC2050-IDC-NL/module
    net (vio, tag.pwr)
    if intf == "swd" :
      require swd:swd from proc
      net (swd tag.swd)
    else if intf == "jtag" :
      require jtag:jtag from proc
      net (jtag tag.jtag)

; ========================================================
; ================== Testpoint functions =================
; ========================================================

public defn add-testpoint (rs:Tuple<JITXObject>, method:String) :
  inside pcb-module :
    for r in rs do :
      if method == "pth-loop" :
        var color = "yellow"
        ; if r == #R(gnd):
        ;   color = "black"
        inst tp : ocdb/keystone/500xx/component(color)
        net (tp.p, r)
      else if method == "smd-loop" :
        inst tp : ocdb/te-connectivity/RC/component
        net (tp.p, r)
      else if method == "smd-pad" :
        inst tp : gen-testpad(2.0)
        net (tp.p, r)
      else :
        fatal("Unsupported add-testpoint method.")

public defn add-testpoint (rs:Tuple<JITXObject>) :
  add-testpoint(rs, "pth-loop")

public pcb-component gen-testpad (d:Double) :
  pin tp
  symbol = test-point-sym(tp => test-point-sym.p)
  val pkg = testpad(d)
  landpattern = pkg(tp => pkg.tp)

public defn add-testpad (rs:Tuple<JITXObject>, d:Double) :
  inside pcb-module :
    for r in rs do :
      public inst tp : gen-testpad(d)
      net (tp.tp, r)

public pcb-component smd-testpoint-cmp (d:Double, side:Side) :
  pin p
  symbol = test-point-sym(p => test-point-sym.p)
  val pkg = smd-testpoint-pkg(d, side)
  landpattern = pkg(p => pkg.p)
  reference-prefix = "TP"

public defn smd-testpoint-cmp (d:Double) :
  smd-testpoint-cmp(d, Top)

public defn smd-testpoint-cmp () :
  smd-testpoint-cmp(2.0)

public defn smd-testpoint-cmp (side:Side) :
  smd-testpoint-cmp(2.0, side)

;<gnd>
public defn filter (in:JITXObject, out:JITXObject, type:String, params:Tuple<Double>) :
  inside pcb-module:
    if type == "rc-lowpass" :
      public inst r : chip-resistor(params[0], 0.01)
      public inst c : ceramic-cap(params[1])
      net (in r.p[1])
      net (out r.p[2] c.p[1])
      net (gnd c.p[2])
    else :
      fatal("Unsupported add-testpoint method.")
;<gnd>

public defn dip-pull (pins:Tuple<JITXObject>, power-pin:JITXObject, gnd-pin:JITXObject) :
  inside pcb-module :
    val n = length(pins)
    public inst dip : ocdb/cts/219/component(n)
    require sws:SPST[n] from dip
    for i in 0 to n do :
      net (pins[i], sws[i].p)
      net (power-pin, sws[i].t)
      res-strap(pins[i], gnd-pin, 10.0e3)

; A mounting hole component with drill and mask radius. Pad radius is unused.
public pcb-component mounting-hole (drill-r:Double, pad-r:Double, mask-r:Double) :
  name             = "Generic Mounting Hole"
  description      = to-string("%~mm mounting hole" % [2. * drill-r])
  landpattern      = (non-plated-hole-landpattern(drill-r, mask-r))()
  symbol           = unplated-hole-sym()
  reference-prefix = "H"

; A mounting hole component with drill and pad radius. 
public defn mounting-hole (drill-r:Double, pad-r:Double) :
  mounting-hole(drill-r, pad-r, pad-r + 0.35)

; A mounting hole component with a fixed drill radius
public defn mounting-hole (drill-r:Double) :
  mounting-hole(drill-r, drill-r + 0.35)

; A non-standard hole with arbitrary shape
public pcb-component hole-component (hole-shape:Shape):
  name              = "Generic hole"
  description       = to-string("%_ shaped hole" % [hole-shape])
  landpattern       = (non-plated-hole-landpattern(hole-shape))()
  symbol            = unplated-hole-sym()
  reference-prefix = "H"

public defn board-mounting-holes (w:Double, h:Double, s:String|Double) :
  inside pcb-module :
    val r = match(s) :
      (screw:String) :
        switch(screw) :
          ;Loose Fit https://blogs.mentor.com/tom-hausherr/blog/tag/pcb-mounting-holes/
          "M2"   : 2.3 / 2.0
          "M2p5" : 2.85 / 2.0
          "M3"   : 3.5 / 2.0
          "M3p5" : 3.9 / 2.0
          else : fatal("Unsupported Screw size %_ for mounting hole" % [screw])
      (diameter:Double) : diameter / 2.0
      (f) : fatal("Unsupported call to mounting hole")
    public inst holes : mounting-hole(r)[4]
    val offs = r * 6.0 ;1.5X bolt diameter * 2
    for (l in grid-locs(2, 2, w - offs, h - offs), i in 0 to 4) do :
      place(holes[i]) at l on Top



; ========================================================
; ===================== Antennas =========================
; ========================================================

pcb-landpattern ant-2GHz4-inverted-f-geom :

  pad launch : smd-pad(0.5, 0.5) at loc(0.0, 0.25)
  pad gnd : smd-pad(0.9, 0.5) at loc(-2.1, 0.25)

  copper(LayerIndex(0)) = Line(0.5, [Point(-2.1, 5.15), Point(2.2, 5.15), Point(2.2, 2.51), 
                                Point(4.7, 2.51),  Point(4.7, 5.15), Point(6.9, 5.15), 
                                Point(6.9, 2.51),  Point(9.4, 2.51), Point(9.4, 5.15), 
                                Point(11.6, 5.15), Point(11.6, 0.96)])
  copper(LayerIndex(0)) = Line(0.5, [Point(0.0, 0.0),   Point(0.0, 5.15)])
  copper(LayerIndex(0)) = Rectangle(0.9, 5.4, loc(-2.1, 2.7))
  via(LayerIndex(0), LayerIndex(0, Bottom), 
    radius = 0.3, hole-radius = 0.125) 
    at Point(-2.1, 0.0)
  layer(ForbidCopper(LayerIndex(0), LayerIndex(0, Bottom))) = Rectangle(18.0, 5.5, loc(5.0, 3.25))
  layer(Courtyard(Top)) = Rectangle(18.0, 5.5, loc(5.0, 3.25))
  layer(Courtyard(Bottom)) = Rectangle(18.0, 5.5, loc(5.0, 3.25))
  layer(BoardEdge()) = Line(0.0, [Point(17.0, 6.0), Point(-7.0, 6.0)])

public pcb-component inverted-f-antenna-cmp :
  pin launch
  pin gnd
  description = "2.4 GHz Inverted F trace antenna"
  val pkg = ant-2GHz4-inverted-f-geom
  val sym = antenna-symbol(1,1)
  landpattern = pkg(launch => pkg.launch, gnd => pkg.gnd)
  symbol = sym(launch => sym.p[1], gnd => sym.p[2])


public pcb-module inverted-f-antenna :
  pin rf-in
  pin gnd
  public inst ant : inverted-f-antenna-cmp
  place(ant) at loc(0.0,0.0) on Top
  ; PI filter
  public inst c0 : ceramic-cap(["capacitance" => 5.6e-12 "temperature-coefficient.code" => "C0G"])
  public inst c1 : ceramic-cap(["capacitance" => 2.4e-12 "temperature-coefficient.code" => "C0G"])
  public inst l : smd-inductor(["inductance" => 2.7e-9 "min-self-resonant-frequency" => 7.0e6 "min-quality-factor" => 8.0])
  net (gnd, ant.gnd c1.p[1] l.p[1])
  net (ant.launch c0.p[2] c1.p[2])
  net (rf-in c0.p[1] l.p[2])
  place(c1) at loc(-0.5, -0.3) on Top
  place(c0) at loc(-0.05, -1.45, 90.0) on Top
  place(l) at loc(-0.5, -2.6) on Top

; ========================================================
; =================== Indicators =========================
; ========================================================

; 50 - 200 mcd for daylight
; 4 - 10 mcd for indoor

; Adds an RGB led to a processor using PWM pins
public defn add-rgb-indicator (brightness:Double, proc:JITXObject, vin:JITXObject):
  inside pcb-module:
    val vio = 3.3 ; TODO: get this from the vin pin property
    ; Calculate resistance to get appropriate brightness
    defn calc-r (l:JITXObject, brightness:Double, vio:Double):
      val i = PWL(property(l.mcd-current))[brightness]
      closest-std-val((vio - property(l.forward-voltage)) / i, 1.0)
    
    public inst led : ocdb/foshan-optoelectronics/FM-B2020RGBA-HG/component ; TODO: make generic
    require pwm:pwm[3] from proc
    res-strap(pwm[0].pwm, led.l.r, calc-r(led.l.r, brightness, vio))
    res-strap(pwm[1].pwm, led.l.g, calc-r(led.l.g, brightness, vio))
    res-strap(pwm[2].pwm, led.l.b, calc-r(led.l.b, brightness, vio))
    net (vin, led.l.a)
