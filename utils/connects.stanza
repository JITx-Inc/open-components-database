#use-added-syntax(jitx)
defpackage ocdb/connects :
  import core
  import collections
  import math
  import jitx
  import jitx/commands
  import ocdb/defaults
  import ocdb/land-patterns
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
  import ocdb/symbols
  import ocdb/box-symbol

public defn connect-isolate (a:JITXObject, b:JITXObject) :
  inside pcb-module :
    eval-when true :
      match(port-type(a), port-type(b)) :
        (pa:Bundle, pb:Bundle) :
          if pa == i2c and pb == i2c :
            inst buf : ocdb/texas-instruments/ISO1540/component
            net (a.sda, buf.SDA1)
            net (a.scl, buf.SCL1)
            net (b.sda, buf.SDA2)
            net (b.scl, buf.SCL2)
            property(buf.VCC1.requires-power) = true
            property(buf.VCC1.power-request) = [3.3 0.3 0.05]

public defn connect-phy (x:JITXObject, y:JITXObject) :
  inside pcb-module :
    eval-when true :
      match(port-type(x), port-type(y)) :
        (px:Bundle, py:Bundle) :
          if px == rgmii and py == ethernet-1000 :
            inst phy : ocdb/marvell/88E1510-A0-NNB2C000/module
            net (x, phy.rgmii)
            net (y.mdi, phy.mdi)
            schematic-group(phy) = phy

public defn connect-ft232 (x:JITXObject, y:JITXObject) :
  inside pcb-module :
    match(port-type(x), port-type(y)) :
      (px:Bundle, py:Bundle) :
        if px == usb-2 and py == uart-with([`dtr `rts]) :
          public inst xcvr : ocdb/future-designs/FT232RL/module
          net (x, xcvr.usb-2)
          net (y.tx, xcvr.uart.rx)
          net (y.rx, xcvr.uart.tx)
          net (y.dtr, xcvr.uart.dtr)
          net (y.rts, xcvr.uart.rts)
          schematic-group(xcvr) = xcvr
          xcvr

public defn connect-cp2105 (usb:JITXObject, uart:JITXObject) :
  inside pcb-module :
    match(port-type(usb), port-type(uart)) :
      (px:Bundle, py:Bundle) :
        if px == usb-2 and py == uart-with([`dtr `rts]) :
          public inst xcvr : ocdb/si-labs/CP2105/module
          net (usb, xcvr.usb-2)
          net (uart.tx, xcvr.e-uart.rx)
          net (uart.rx, xcvr.e-uart.tx)
          net (uart.dtr, xcvr.e-uart.dtr)
          net (uart.rts, xcvr.e-uart.rts)
          xcvr
