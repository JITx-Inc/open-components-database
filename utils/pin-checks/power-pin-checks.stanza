#use-added-syntax(jitx)
defpackage ocdb/utils/pin-checks/power-pin-checks :
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/property-structs
  import ocdb/utils/pin-checks/utils
  import ocdb/utils/tolerance

val NAME        = "Power Pin Checks"
val DESCRIPTION = "Check Power Pin Properties"

public defn check-power-pin (p:JITXObject) :
  inside pcb-module :
    check power-pin(p)

pcb-check power-pin (p:JITXObject) :
  val p-props = property(p.power-pin)

  #CHECK(
    condition = p-props is PowerPin
    name = NAME
    category = CATEGORY
    description = DESCRIPTION
    subcheck-description = "Verify the power pin has the correct PowerPin property attached"
    pass-message = "Pin %_ has the correct PowerPin property attached" 
      % [context(p)]
    fail-message = "Pin %_ does not have the correct PowerPin property attached" 
      % [context(p)]
    locators = [p]
  )

  #CHECK(
    condition = has-property?(p.net-voltage)
    name = NAME
    category = CATEGORY
    description = DESCRIPTION
    subcheck-description = "Verify the power pin has the correct net-voltage property attached",
    pass-message = "Pin %_ has the correct net-voltage property attached of %_V" 
      % [context(p), property(p.net-voltage)],
    fail-message =  "Pin %_ does not have the correct net-voltage property attached" 
      % [context(p)]
    locators = [p]
  )

  #CHECK(
    condition = in-range?(recommended-voltage(p-props), property(p.net-voltage))
    name = NAME
    category = CATEGORY
    description = DESCRIPTION
    subcheck-description = "Check the power pin's net-voltage property is within the design specification",
    pass-message = "Pin %_ net voltage %_V is correctly within the design specification %_V" 
      % [context(p), property(p.net-voltage), recommended-voltage(p-props)],
    fail-message = "Pin %_ net voltage %_V is not correctly within the design specification %_V" 
      % [context(p), property(p.net-voltage), recommended-voltage(p-props)],
    locators = [p]
  )
